      SUBROUTINE HOMOQP(R,PR,AL,AM,RMASS,A)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         INITIALIZE COORDINATES AND MOMENTA FOR A DIATOM
C
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/SELTB/QZ(NDA3),NSELT,NSFLAG,NACTA,NACTB,NLINA,NLINB,NSURF
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      DIMENSION AM(4),A(3),QCM(3),VCM(3)
C
C         SET CARTESIAN COORDINATES AND MOMENTA
C
      DO I=1,2
         J3=3*L(I)
         J2=J3-1
         J1=J2-1
         Q(J1)=0.0D0
         Q(J2)=0.0D0
         Q(J3)=0.0D0
         P(J2)=0.0D0
         P(J3)=0.0D0
      ENDDO
      Q(J1)=R
      VREL=PR/RMASS
      WT=W(L(1))+W(L(2))
      VELA=VREL*W(L(2))/WT
      VELB=VELA-VREL
      J1=3*L(1)-2
      P(J1)=W(L(1))*VELA
      J1=3*L(2)-2
      P(J1)=W(L(2))*VELB
C
C         SET INERTIA ARRAYS.  CHOOSE Y AND Z ANGULAR MOMENTUM COMPONENTS
C
      A(1)=1.0D+20
      A(2)=RMASS*R**2
      A(3)=A(2)
      RAND=RAND0(ISEED)
      DUM=TWOPI*RAND
      AM(1)=0.0D0
      AM(2)=AL*SIN(DUM)
      AM(3)=AL*COS(DUM)
C
C         CALCULATE CENTER OF MASS COORDIANTES QQ AND MOMENTA PP
C
      CALL CENMAS(WT,QCM,VCM,2)
C
C         MOVE PP ARRAY TO P ARRAY AND QQ ARRAY TO Q ARRAY
C
      DO I=1,2
         J=3*L(I)+1
         DO K=1,3
            Q(J-K)=QQ(J-K)
            P(J-K)=PP(J-K)
         ENDDO
      ENDDO
C
C         ADD THE ANGULAR MOMENTUM VECTOR.  CALCULATE THE REQUIRED
C         ANGULAR VELOCITY AND ADD IT TO THE DIATOM, WHICH LIES
C         ALONG THE X-AXIS.
C
      WX=0.0D0
      WY=-AM(2)/A(2)
      WZ=-AM(3)/A(3)
      CALL ANGVEL(2)
C
      IF (NATOMS.EQ.2) RETURN
C
C         RANDOMLY ROTATE THE DIATOM ABOUT ITS CENTER OF MASS BY
C         EULER'S ANGLES.  CENTER OF MASS COORDINATES QQ AND MOMENTA
C         PP ARE PASSED FROM SUBROUTINES CENMAS AND ANGVEL THROUGH
C         COMMON BLOCK WASTE.
C
      CALL VENUS_ROTATE(2)
C
C         CALCULATE ANGULAR MOMENTUM AND COMPONENTS.
C
      CALL ROTN(AM,EROT,2)
      RETURN
      END
