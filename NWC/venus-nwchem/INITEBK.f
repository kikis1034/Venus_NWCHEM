      SUBROUTINE INITEBK(N,J,RMIN,RMAX,DH,RMASS,ENJ,PTEST,AL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         INITIALIZE PARAMETERS FOR AN OSCILLATOR WITH GIVEN
C         QUANTUM NUMBERS N AND J BY SEMICLASSICAL EBK QUANTIZATION
C
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
C
      BN=DBLE(N)
      HNU=ENJ/(BN+0.5D0)
      AM=SQRT(DBLE(J*(J+1)))
      AL=AM*C7
      RMASS=W(L(1))*W(L(2))/(W(L(1))+W(L(2)))
      DUM=1.0D0
C
C         SOLVE FOR ENJ BY FIXED POINT APPROACH
C
      ICOUNT=0
      DO WHILE (ABS(DUM).GT.1.0D-6)
         CALL FINLNJ(ENJ,AM,RMIN,RMAX,DH,AN,AJ)
         DUM=BN-AN
         ENJ=ENJ+DUM*HNU
         ICOUNT=ICOUNT+1
         IF (ICOUNT.GT.200) STOP
      END DO
C
      RMIN=RMIN+0.001D0
      RMAX=RMAX-0.001D0
      PTEST=SQRT(0.0001D0*2.0D0*RMASS*ENJ)
      RETURN
      END
