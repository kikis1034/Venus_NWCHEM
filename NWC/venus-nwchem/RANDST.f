      SUBROUTINE RANDST(ISEED)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         SET UP DATA FOR RANDOM NUMBER GENERATOR.
C
C         WRITTEN BY DAVID SCHWENKE (NASA AMES)
C         FOR MACHINES WITH A MAXIMUM INTEGER OF (2**32 - 1) OR LARGER
C         AND HAS PRECISION OF 1 IN 2**64
C         AMENDED BY KIERAN F. LIM (STANFORD UNIVERSITY)
C         FOR MACHINES WITH A MAXIMUM INTEGER OF (2**16 - 1) OR LARGER
C         NOTE:  DEC MICROVAX HAS A MAXIMUM INTEGER OF (2**31 - 1)
C         LATEST UPDATE: 12 APRIL 1990
C
C         THIS ROUTINE HITS INTEGER OVERFLOW ONLY IF THE MAXIMUM
C         INTEGER IS (2**16 - 1) = 65535 OR SMALLER
C
C         EITHER READ IN EXISTING SEQUENCE OF RANDOM NUMBERS FROM UNIT 50
C         OR START A NEW SEQUENCE:
C
C            1.  CONVERT ISEED TO BASE IBFCTR  (IBFCTR=256=2**8)
C                0 .LE. ISEED .LE.(2**31 - 1)
C            2.  TAKE ISEED3=2*ISEED+1
C                1 .LE. ISEED3 .LE.(2**32 - 1)
C                WITH ISEED3 ODD
C            3.  EXERCISE RANDOM NUMBER GENERATOR IN CASE
C                ISEED NOT CHOSEN BETWEEN 0 AND (2**31 - 1)
C            4.  PUT 100 RANDOM NUMBERS INTO ARRAY X: THIS IS
C                NEW SEQUENCE
C
C         RANDOM NUMBER GENERATOR IS FUNCTION RAND1 WHICH
C         USES THE MULTIPLICATIVE CONGRUENTIAL METHOD
C
C         REFERENCE: D.E. KNUTH, "THE ART OF COMPUTER PROGRAMMING",
C         VOL. 2 OF "SEMINUMERICAL ALGORITHMS" (ADDISON-WESLEY, 1981).
C         2ND EDITION.
C
C         CALCULATIONS IN BASE (IBFCTR=256=2**8)
C
      COMMON/RANCOM/RANLST(100),ISEED3(8),IBFCTR
C
  910 FORMAT(/' INITIATING A RANDOM NUMBER SEQUENCE',
     *' - INPUTTED ISEED IN BASE 10 = ',I10,
     *//' ISEED3 = 2 * ISEED + 1',
     */' IN BASE 256 (I.E. BASE 2**8) = ',8I4,/)
C
      IBFCTR = 256
C
C         CONVERT ISEED TO BASE IBFCTR
C         ANSWER IS STORED IN ISEED3
C
      IS=ISEED
      DO I=1,8
         ISEED3(I)=MOD(IS,IBFCTR)
         IS=IS/IBFCTR
      ENDDO
C
C         DOUBLE ISEED3
C         AT THIS STAGE ISEED3 IS ENSURED TO BE AN EVEN NUMBER
C
C         PROGRAM WILL CRASH IN THIS SECTION IF ISEED.GE.(2**31)
C         I.E. IN DO LOOP 180 AT LINE
C                                    ISEED3(I+1) = ISEED3(I+1) + 1
C
      DO I=1,8
         ISEED3(I) = ISEED3(I) + ISEED3(I)
      ENDDO
      DO I=1,8
  150    CONTINUE
         IF (ISEED3(I)-IBFCTR) 170, 160, 160
  160    CONTINUE
         ISEED3(I) = ISEED3(I) - IBFCTR
         ISEED3(I+1) = ISEED3(I+1) + 1
         GOTO 150
  170    CONTINUE
      ENDDO
C
C         ISEED3 + 1
C         THIS ENSURES THAT ISEED3 IA A ODD NUMBER
C
      ISEED3(1) = ISEED3(1) + 1
      DO I=1,8
  240    CONTINUE
         IF (ISEED3(I)-IBFCTR) 260, 250, 250
  250    CONTINUE
         ISEED3(I) = ISEED3(I) - IBFCTR
         ISEED3(I+1) = ISEED3(I+1) + 1
         GOTO 240
  260    CONTINUE
      ENDDO
      WRITE(6,910) ISEED, (ISEED3(9-I), I=1,8)
C
C         EXERCISE RANDOM NUMBER GENERATOR IN CASE
C         ISEED NOT CHOSEN BETWEEN 0 AND (2**31 - 1)
C
C         PUT 100 RANDOM NUMBERS INTO ARRAY RANLST: THIS IS A 
C         NEW SEQUENCE.
C		Don't delete second loop inspite it is the same loop, but 
C         Result going to be different
      DO I=1,100
         RANLST(I)=RAND1(ISEED3)
      ENDDO
	DO I=1,100
         RANLST(I)=RAND1(ISEED3)
      ENDDO
 
      ISEED=0
      RETURN
      END
