      SUBROUTINE VTT(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE NON DIAGONAL BEND-BEND POTENTIAL
C         ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/BENDB/THETAZ(ND03),FBZ(ND03),CJ(ND03),CK(ND03),RJZ(ND03),
     *RKZ(ND03),FB(ND03),N3J(ND03),N3K(ND03),N3M(ND03)
      COMMON/VTTB/FKTTZ(ND12),FKTT(ND12),N12B(ND12),N12BB(ND12),
     *            NTT(ND12)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
C
C       ATOM AND COORDINATE INDICES
C
      IJ=N3J(N12B(NL))
      IK=N3K(N12B(NL))
      IM=N3M(N12B(NL))
      IJJ=N3J(N12BB(NL))
      IKK=N3K(N12BB(NL))
      IMM=N3M(N12BB(NL))
      J3=3*IJ
      J2=J3-1
      J1=J3-2
      K3=3*IK
      K2=K3-1
      K1=K3-2
      M3=3*IM
      M2=M3-1
      M1=M3-2
      JJ3=3*IJJ
      JJ2=JJ3-1
      JJ1=JJ3-2
      KK3=3*IKK
      KK2=KK3-1
      KK1=KK3-2
      MM3=3*IMM
      MM2=MM3-1
      MM1=MM3-2
C
C       CALCULATE RELATIVE INDICES FOR R'S.
C
      IF (IM.GT.IK) THEN
         KM=(IK-1)*(2*NATOMS-IK)/2+IM-IK
      ELSE
         KM=(IM-1)*(2*NATOMS-IM)/2+IK-IM
      ENDIF
      IF (IM.GT.IJ) THEN
         JM=(IJ-1)*(2*NATOMS-IJ)/2+IM-IJ
      ELSE
         JM=(IM-1)*(2*NATOMS-IM)/2+IJ-IM
      ENDIF
      IF (IMM.GT.IKK) THEN
         KKMM=(IKK-1)*(2*NATOMS-IKK)/2+IMM-IKK
      ELSE
         KKMM=(IMM-1)*(2*NATOMS-IMM)/2+IKK-IMM
      ENDIF
      IF (IMM.GT.IJJ) THEN
         JJMM=(IJJ-1)*(2*NATOMS-IJJ)/2+IMM-IJJ
      ELSE
         JJMM=(IMM-1)*(2*NATOMS-IMM)/2+IJJ-IMM
      ENDIF
C
C      CALCULATE RELATIVE COORDINATES
C
      T1=Q(J1)-Q(M1)
      T2=Q(J2)-Q(M2)
      T3=Q(J3)-Q(M3)
      T4=Q(K1)-Q(M1)
      T5=Q(K2)-Q(M2)
      T6=Q(K3)-Q(M3)
      TT1=Q(JJ1)-Q(MM1)
      TT2=Q(JJ2)-Q(MM2)
      TT3=Q(JJ3)-Q(MM3)
      TT4=Q(KK1)-Q(MM1)
      TT5=Q(KK2)-Q(MM2)
      TT6=Q(KK3)-Q(MM3)
C
C       CALCULATE ANGLES
C
      CTHETA=(T1*T4+T2*T5+T3*T6)/(R(KM)*R(JM))
      IF (CTHETA.GE.1.00D0) THEN
         CTHETA=1.D0
         THETA(N12B(NL))=0.D0
      ELSEIF (CTHETA.LE.-1.00D0) THEN
         CTHETA=-1.D0
         THETA(N12B(NL))=PI
      ELSE
         THETA(N12B(NL))=ACOS(CTHETA)
      ENDIF
      CTTHTA=(TT1*TT4+TT2*TT5+TT3*TT6)/(R(KKMM)*R(JJMM))
      IF (CTTHTA.GE.1.00D0) THEN
         CTTHTA=1.00D0
         THETA(N12BB(NL))=0.D0
      ELSEIF (CTTHTA.LE.-1.00D0) THEN
         CTTHTA=-1.00D0
         THETA(N12BB(NL))=PI
      ELSE
         THETA(N12BB(NL))=ACOS(CTTHTA)
      ENDIF
C
C     REDEFINE SWITCHING PARAMETERS
C
      CJJM=CJ(N12B(NL))
      CKKM=CK(N12B(NL))
      CJJJMM=CJ(N12BB(NL))
      CKKKMM=CK(N12BB(NL))
C
      IF (NTT(NL).EQ.-1) THEN
         CJJM=-ABS(CJJM)
         CKKM=-ABS(CKKM)
         CJJJMM=-ABS(CJJJMM)
         CKKKMM=-ABS(CKKKMM)
      ELSE
         IF (JM.EQ.JJMM.OR.JM.EQ.KKMM) CJJM = -ABS(CJJM)
         IF (KM.EQ.JJMM.OR.KM.EQ.KKMM) CKKM = -ABS(CKKM)
      ENDIF
C
C       CALCULATE SWITCHING FUNCTIONS AND FORCE CONTANTS
C
      SRJ=1.0D0
      IF (CJJM.GT.0.0D0) THEN
         DUM1=R(JM)-RJZ(N12B(NL))
         IF (DUM1.GT.0.0D0) SRJ=EXP(-CJJM*DUM1**2)
      ENDIF
      SRK=1.0D0
      IF (CKKM.GT.0.0D0) THEN
         DUM1=R(KM)-RKZ(N12B(NL))
         IF (DUM1.GT.0.0D0) SRK=EXP(-CKKM*DUM1**2)
      ENDIF
      SRJJ=1.0D0
      IF (CJJJMM.GT.0.0D0) THEN
         DUM1=R(JJMM)-RJZ(N12BB(NL))
         IF (DUM1.GT.0.0D0) SRJJ=EXP(-CJJJMM*DUM1**2)
      ENDIF
      SRKK=1.0D0
      IF (CKKKMM.GT.0.0D0) THEN
         DUM1=R(KKMM)-RKZ(N12BB(NL))
         IF (DUM1.GT.0.0D0) SRKK=EXP(-CKKKMM*DUM1**2)
      ENDIF
      FKTT(NL)=SRJ*SRK*SRJJ*SRKK*FKTTZ(NL)
C
C       DEFINE CONSTANTS
C
      DUM3=THETA(N12B(NL))-THETAZ(N12B(NL))
      DUM33=THETA(N12BB(NL))-THETAZ(N12BB(NL))
      TEM0=2.0D0*FKTT(NL)*DUM3*DUM33
      TEM1=FKTT(NL)*DUM3/SQRT(1-CTTHTA*CTTHTA)
      TEM2=FKTT(NL)*DUM33/SQRT(1-CTHETA*CTHETA)
C
C       CALCULATE (DV/DQ)
C
      TRKJ=1.D0/(R(KM)*R(JM))
      TRKK=1.D0/(R(KM)*R(KM))
      TRJJ=1.D0/(R(JM)*R(JM))
      PDOT(J1)=PDOT(J1)-TEM2*(T4*TRKJ-CTHETA*T1*TRJJ)
      PDOT(J2)=PDOT(J2)-TEM2*(T5*TRKJ-CTHETA*T2*TRJJ)
      PDOT(J3)=PDOT(J3)-TEM2*(T6*TRKJ-CTHETA*T3*TRJJ)
      PDOT(K1)=PDOT(K1)-TEM2*(T1*TRKJ-CTHETA*T4*TRKK)
      PDOT(K2)=PDOT(K2)-TEM2*(T2*TRKJ-CTHETA*T5*TRKK)
      PDOT(K3)=PDOT(K3)-TEM2*(T3*TRKJ-CTHETA*T6*TRKK)
      PDOT(M1)=PDOT(M1)+TEM2*((T1+T4)*TRKJ-CTHETA*(T1*TRJJ+T4*TRKK))
      PDOT(M2)=PDOT(M2)+TEM2*((T2+T5)*TRKJ-CTHETA*(T2*TRJJ+T5*TRKK))
      PDOT(M3)=PDOT(M3)+TEM2*((T3+T6)*TRKJ-CTHETA*(T3*TRJJ+T6*TRKK))
      TRKJ=1.D0/(R(KKMM)*R(JJMM))
      TRKK=1.D0/(R(KKMM)*R(KKMM))
      TRJJ=1.D0/(R(JJMM)*R(JJMM))
      PDOT(JJ1)=PDOT(JJ1)-TEM1*(TT4*TRKJ-CTTHTA*TT1*TRJJ)
      PDOT(JJ2)=PDOT(JJ2)-TEM1*(TT5*TRKJ-CTTHTA*TT2*TRJJ)
      PDOT(JJ3)=PDOT(JJ3)-TEM1*(TT6*TRKJ-CTTHTA*TT3*TRJJ)
      PDOT(KK1)=PDOT(KK1)-TEM1*(TT1*TRKJ-CTTHTA*TT4*TRKK)
      PDOT(KK2)=PDOT(KK2)-TEM1*(TT2*TRKJ-CTTHTA*TT5*TRKK)
      PDOT(KK3)=PDOT(KK3)-TEM1*(TT3*TRKJ-CTTHTA*TT6*TRKK)
      PDOT(MM1)=PDOT(MM1)+TEM1*((TT1+TT4)*TRKJ
     *          -CTTHTA*(TT1*TRJJ+TT4*TRKK))
      PDOT(MM2)=PDOT(MM2)+TEM1*((TT2+TT5)*TRKJ
     *          -CTTHTA*(TT2*TRJJ+TT5*TRKK))
      PDOT(MM3)=PDOT(MM3)+TEM1*((TT3+TT6)*TRKJ
     *          -CTTHTA*(TT3*TRJJ+TT6*TRKK))
C
C        CALCULATE FOR THE CONTRIBUTION OF SWITCHING FUNCTION
C
      DUM1=R(JM)-RJZ(N12B(NL))
      IF (CJJM.GE.0.0D0.AND.DUM1.GE.0.0D0) THEN
         DUM4=TEM0*CJJM*(1.0D0-RJZ(N12B(NL))/R(JM))
         PDOT(J1)=PDOT(J1)-DUM4*T1
         PDOT(J2)=PDOT(J2)-DUM4*T2
         PDOT(J3)=PDOT(J3)-DUM4*T3
         PDOT(M1)=PDOT(M1)+DUM4*T1
         PDOT(M2)=PDOT(M2)+DUM4*T2
         PDOT(M3)=PDOT(M3)+DUM4*T3
      ENDIF
C
      DUM1=R(KM)-RKZ(N12B(NL))
      IF (CKKM.GE.0.0D0.AND.DUM1.GE.0.0D0) THEN
         DUM4=TEM0*CKKM*(1.0D0-RKZ(N12B(NL))/R(KM))
         PDOT(K1)=PDOT(K1)-DUM4*T4
         PDOT(K2)=PDOT(K2)-DUM4*T5
         PDOT(K3)=PDOT(K3)-DUM4*T6
         PDOT(M1)=PDOT(M1)+DUM4*T4
         PDOT(M2)=PDOT(M2)+DUM4*T5
         PDOT(M3)=PDOT(M3)+DUM4*T6
      ENDIF
C
      DUM11=R(JJMM)-RJZ(N12BB(NL))
      IF (CJJJMM.GE.0.0D0.AND.DUM11.GE.0.0D0) THEN
         DUM44=TEM0*CJJJMM*(1.0D0-RJZ(N12BB(NL))/R(JJMM))
         PDOT(JJ1)=PDOT(JJ1)-DUM44*TT1
         PDOT(JJ2)=PDOT(JJ2)-DUM44*TT2
         PDOT(JJ3)=PDOT(JJ3)-DUM44*TT3
         PDOT(MM1)=PDOT(MM1)+DUM44*TT1
         PDOT(MM2)=PDOT(MM2)+DUM44*TT2
         PDOT(MM3)=PDOT(MM3)+DUM44*TT3
      ENDIF
C
      DUM11=R(KKMM)-RKZ(N12BB(NL))
      IF (CKKKMM.GE.0.0D0.AND.DUM11.GE.0.0D0) THEN
         DUM44=TEM0*CKKKMM*(1.0D0-RKZ(N12BB(NL))/R(KKMM))
         PDOT(KK1)=PDOT(KK1)-DUM44*TT4
         PDOT(KK2)=PDOT(KK2)-DUM44*TT5
         PDOT(KK3)=PDOT(KK3)-DUM44*TT6
         PDOT(MM1)=PDOT(MM1)+DUM44*TT4
         PDOT(MM2)=PDOT(MM2)+DUM44*TT5
         PDOT(MM3)=PDOT(MM3)+DUM44*TT6
      ENDIF
      RETURN
      END
