      SUBROUTINE DANGLE(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE DIHEDRAL ANGLE TORSION POTENTIAL ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/ANGLEB/FDH(ND13I,ND13J),GDH(ND13I,ND13J),NDH(ND13I),
     *N13I(ND13I),N13J(ND13I),N13K(ND13I),N13L(ND13I)
C
C         CALCULATE INDICES FOR COORDINATES
C
      I3=3*N13I(NL)
      I2=I3-1
      I1=I2-1
      J3=3*N13J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N13K(NL)
      K2=K3-1
      K1=K2-1
      L3=3*N13L(NL)
      L2=L3-1
      L1=L2-1
C
C         CALCULATE RELATIVE COORDINATES
C
      XJI=Q(J1)-Q(I1)
      YJI=Q(J2)-Q(I2)
      ZJI=Q(J3)-Q(I3)
      XKJ=Q(K1)-Q(J1)
      YKJ=Q(K2)-Q(J2)
      ZKJ=Q(K3)-Q(J3)
      XLK=Q(L1)-Q(K1)
      YLK=Q(L2)-Q(K2)
      ZLK=Q(L3)-Q(K3)
C
C         CALCULATE DIHEDRAL ANGLE
C
      AX=YJI*ZKJ - ZJI*YKJ
      AY=ZJI*XKJ - XJI*ZKJ
      AZ=XJI*YKJ - YJI*XKJ
      BX=YKJ*ZLK - ZKJ*YLK
      BY=ZKJ*XLK - XKJ*ZLK
      BZ=XKJ*YLK - YKJ*XLK
      RA=SQRT(AX*AX + AY*AY + AZ*AZ)
      RB=SQRT(BX*BX + BY*BY + BZ*BZ)
      CANG=(AX*BX + AY*BY +AZ*BZ)/RA/RB
      IF (CANG.GT.1.00D0) CANG=1.00D0
      IF (CANG.LT.-1.00D0) CANG=-1.00D0
      DANG(NL)=ACOS(CANG)
      SANG=SQRT(1.0D0-CANG**2)
      DUM1=AY*BZ - AZ*BY
      DUM2=AZ*BX - AX*BZ
      DUM3=AX*BY - AY*BX
      DUM4=DUM1*XKJ + DUM2*YKJ + DUM3*ZKJ
      IF (DUM4.LT.0.0D0) THEN
         SANG=-SANG
         DANG(NL)=-DANG(NL)
      ENDIF
C
C         CALCULATE DERIVATIVE OF CANG WITH RESPECT TO AX,AY & AZ AND
C         BX,BY & BZ.
C
      DUM1=RA*RB
      DUM2=CANG/RA/RA
      DCADAX=BX/DUM1 - DUM2*AX
      DCADAY=BY/DUM1 - DUM2*AY
      DCADAZ=BZ/DUM1 - DUM2*AZ
      DUM2=CANG/RB/RB
      DCADBX=AX/DUM1 - DUM2*BX
      DCADBY=AY/DUM1 - DUM2*BY
      DCADBZ=AZ/DUM1 - DUM2*BZ
C
C         CALCULATE (DV/DQ)'S
C
      DUM4=0.0D0
      IF (ABS(DANG(NL)).GT.0.1D0) THEN
         IF (ABS(ABS(DANG(NL))-PI).LE.0.1D0) GOTO 4
         DO I=1,NDH(NL)
            DUM1=DBLE(I)
            DUM4=DUM4+FDH(NL,I)*DUM1*SIN(DUM1*DANG(NL)-GDH(NL,I))
         ENDDO
         DUM4=DUM4/2.0D0/SANG
         GOTO 6
      ENDIF
C
      DO I=1,NDH(NL)
         DUM1=DBLE(I)
         DUM2=DUM1*DANG(NL)
         DUM3=(1.0D0 - DUM2**2/6.0D0+DUM2**4/120.0D0
     *        -DUM2**6/5040.0D0)
         DUM3=DUM3/(1.0D0-DANG(NL)**2/6.0D0+DANG(NL)**4/120.0D0
     *        -DANG(NL)**6/5040.0D0)
         DUM3=FDH(NL,I)*DUM1**2*DUM3
         IF (ABS(GDH(NL,I)-PI).LT.0.1D0) DUM3=-DUM3
         DUM4=DUM4+DUM3
      ENDDO
      DUM4=DUM4/2.0D0
      GOTO 6
C
    4 CONTINUE
      DO I=1,NDH(NL)
         DUM1=DBLE(I)
         DUM2=ABS(DANG(NL)) - PI
         DUM5=DUM1*DUM2
         DUM3=(1.0D0-DUM5**2/6.0D0+DUM5**4/120.0D0
     *        -DUM5**6/5040.0D0)
         DUM3=DUM3/(1.0D0-DUM2**2/6.0D0+DUM2**4/120.0D0
     *        -DUM2**6/5040.0D0)
         DUM3=FDH(NL,I)*DUM1**2*DUM3
         IF (COS(DUM1*PI+GDH(NL,I)).GT.0.0D0) DUM3=-DUM3
         DUM4=DUM4 + DUM3
      ENDDO
      DUM4=DUM4/2.0D0
C
    6 CONTINUE
C
C         CALCULATE DERIVATIVES WITH RESPECT TO ATOM I
C
      DUM1=DUM4*(DCADAY*ZKJ - DCADAZ*YKJ)
      DUM2=DUM4*(DCADAZ*XKJ - DCADAX*ZKJ)
      DUM3=DUM4*(DCADAX*YKJ - DCADAY*XKJ)
      PDOT(I1)=PDOT(I1) + DUM1
      PDOT(I2)=PDOT(I2) + DUM2
      PDOT(I3)=PDOT(I3) + DUM3
C
C         CALCULATE DERIVATIVES WITH RESPECT TO ATOM J
C
      DUM1=DUM4*(DCADAZ*(YKJ + YJI) - DCADAY*(ZJI + ZKJ)
     *     +DCADBY*ZLK - DCADBZ*YLK)
      DUM2=DUM4*(DCADAX*(ZKJ + ZJI) - DCADAZ*(XJI + XKJ)
     *     +DCADBZ*XLK - DCADBX*ZLK)
      DUM3=DUM4*(DCADAY*(XKJ + XJI) - DCADAX*(YJI + YKJ)
     *     +DCADBX*YLK - DCADBY*XLK)
      PDOT(J1)=PDOT(J1) + DUM1
      PDOT(J2)=PDOT(J2) + DUM2
      PDOT(J3)=PDOT(J3) + DUM3
C
C         CALCULATE DERIVATIVES WITH RESPECT TO ATOM K
C
      DUM1=DUM4*(DCADAY*ZJI - DCADAZ*YJI + DCADBZ*(YLK + YKJ)
     *     -DCADBY*(ZKJ + ZLK))
      DUM2=DUM4*(DCADAZ*XJI - DCADAX*ZJI + DCADBX*(ZLK + ZKJ)
     *     -DCADBZ*(XKJ + XLK))
      DUM3=DUM4*(DCADAX*YJI - DCADAY*XJI + DCADBY*(XLK + XKJ)
     *     -DCADBX*(YKJ + YLK))
      PDOT(K1)=PDOT(K1) + DUM1
      PDOT(K2)=PDOT(K2) + DUM2
      PDOT(K3)=PDOT(K3) + DUM3
C
C         CALCULATE DERIVATIVES WITH RESPECT TO ATOM L
C
      DUM1=DUM4*(DCADBY*ZKJ - DCADBZ*YKJ)
      DUM2=DUM4*(DCADBZ*XKJ - DCADBX*ZKJ)
      DUM3=DUM4*(DCADBX*YKJ - DCADBY*XKJ)
      PDOT(L1)=PDOT(L1) + DUM1
      PDOT(L2)=PDOT(L2) + DUM2
      PDOT(L3)=PDOT(L3) + DUM3
      RETURN
      END
