      SUBROUTINE HALPHA(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE HARMONIC ALPHA BEND POTENTIAL ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/ALPHAB/FA(ND04),N4J(ND04),N4K(ND04),N4M(ND04),N4N(ND04)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
C
C         CALCULATE INDICES FOR COORDINATES
C
      J3=3*N4J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N4K(NL)
      K2=K3-1
      K1=K2-1
      M3=3*N4M(NL)
      M2=M3-1
      M1=M2-1
      N3=3*N4N(NL)
      N2=N3-1
      N1=N2-1
C
C         CALCULATE INDICES FOR R
C
      IF (N4K(NL).LE.N4J(NL)) THEN
         JK=(N4K(NL)-1)*(2*NATOMS-N4K(NL))/2+N4J(NL)-N4K(NL)
      ELSE
         JK=(N4J(NL)-1)*(2*NATOMS-N4J(NL))/2+N4K(NL)-N4J(NL)
      ENDIF
C
C         CALCULATE RELATIVE COORDINATES
C
      T1=Q(K1)-Q(J1)
      T2=Q(K2)-Q(J2)
      T3=Q(K3)-Q(J3)
      T4=Q(M1)-Q(J1)
      T5=Q(M2)-Q(J2)
      T6=Q(M3)-Q(J3)
      T7=Q(N1)-Q(J1)
      T8=Q(N2)-Q(J2)
      T9=Q(N3)-Q(J3)
C
C         CALCULATE ALPHA ANGLES
C
      XA=T5*T9-T6*T8
      YA=T6*T7-T4*T9
      ZA=T4*T8-T5*T7
      RA=SQRT(XA*XA+YA*YA+ZA*ZA)
      CA=(XA*T1+YA*T2+ZA*T3)/RA/R(JK)
      ALPHA(NL)=ACOS(CA)+HALFPI
C
C         CALCULATE (DV/DQ)'S
C
      DUM4=-1.0D0/SQRT(1.0D0-CA**2)*FA(NL)*(ALPHA(NL)-PI)
      RKA=R(JK)*RA
      DUM5=CA/R(JK)/R(JK)
      DUM6=CA/RA/RA
      DUM1=DUM4*(XA/RKA-DUM5*T1)
      DUM2=DUM4*(YA/RKA-DUM5*T2)
      DUM3=DUM4*(ZA/RKA-DUM5*T3)
      PDOT(K1)=PDOT(K1)+DUM1
      PDOT(K2)=PDOT(K2)+DUM2
      PDOT(K3)=PDOT(K3)+DUM3
      T96=T9-T6
      T85=T8-T5
      T74=T7-T4
      DUMX=DUM4*(T1/RKA-DUM6*XA)
      DUMY=DUM4*(T2/RKA-DUM6*YA)
      DUMZ=DUM4*(T3/RKA-DUM6*ZA)
      PDOT(J1)=PDOT(J1)-DUM1+T96*DUMY-T85*DUMZ
      PDOT(J2)=PDOT(J2)-DUM2+T74*DUMZ-T96*DUMX
      PDOT(J3)=PDOT(J3)-DUM3+T85*DUMX-T74*DUMY
      PDOT(M1)=PDOT(M1)+T8*DUMZ-T9*DUMY
      PDOT(M2)=PDOT(M2)+T9*DUMX-T7*DUMZ
      PDOT(M3)=PDOT(M3)+T7*DUMY-T8*DUMX
      PDOT(N1)=PDOT(N1)-T5*DUMZ+T6*DUMY
      PDOT(N2)=PDOT(N2)-T6*DUMX+T4*DUMZ
      PDOT(N3)=PDOT(N3)-T4*DUMY+T5*DUMX
      RETURN
      END
