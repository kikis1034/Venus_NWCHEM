      SUBROUTINE VSN2
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE SN2 POTENTIAL ENERGY AND DERIVATIVES
C         THIS IS A GENEARAL GLOBAL POTENTIAL ENERGY SURFACE 
C         FOR TWO SN2 REACTIONS:
C            Cl(-) + CH3Cl -> ClCH3 + Cl(-)
C            Cl(-) + CH3Br -> ClCH3 + Br(-)
C
C         THE PARAMETER NEEDED AS INPUT IS NSN2, WHICH IS 
C         DESCRIBED IN SUBROUTINE PARSN2
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/PSN2/PESN2,GA,RA,RB
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/MORSEB/RMZ(ND02),B(ND02),D(ND02),N2J(ND02),N2K(ND02),
     *CM1(ND02),CM2(ND02),CM3(ND02),CM4(ND02),CM5(ND02)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/SN2A/SFACLR,SFBCLR,SLGAC,SLGBC,DCLUSA,DCLUSB,DCH3CL,
     *DCH3BR,BACLUS,BBCLUS,BAB,BBB,SBAC,SBBC,BAC,BBC,SDGAC,SDGBC,
     *FPHIAZ,FPHIBZ,APHIAZ,APHIBZ,GPHIAZ,GPHIBZ,GPHIAC,GPHIBC,
     *S1RAA1,S1RAA2,S1RAA3,S1RAA4,S1RAA5,S1RBA1,S1RBA2,S1RBA3,
     *S1RBA4,S1RBA5,S1RAC1,S1RAC2,S1RAC3,S1RAC4,S1RAC5,S1RBC1,
     *S1RBC2,S1RBC3,S1RBC4,S1RBC5,S1CAC,S1CBC,SEAC,SEBC,VH2AC,
     *VH2BC,PHIAOI,PHIAOB,SPAA1,SPAA2,SPAA3,SPAC1,SPAC2,SPAC3,
     *SPGAC,PHIBOI,PHIBOB,SPBA1,SPBA2,SPBA3,SPBC1,SPBC2,SPBC3,
     *SPGBC,PCLCLC,PCLCLG,SFTA,SFTC,SFTD,STHCA,STHCB,STHEA,STHEB,
     *FTHA,FTHB,FTHTS,ATHA,ATHB,ATHTS,BTHA,BTHB,BTHTS,THTS,THA,THB,
     *VLRAA,VLRBA,VCLAHD,VCLBHD,RAC,RBC,RAI,RBI,GATS
      COMMON/SN2B/SCALEP,SDAA1,SDAA2,SDAA3,SDAC1,SDAC2,SDAC3,SDBA1,
     *SDBA2,SDBA3,SDBC1,SDBC2,SDBC3,SRAA1,
     *SRBA1,SRAA2,SRBA2,SRAA3,SRBA3,SRAC1,SRBC1,SRAC2,SRBC2,SRAC3,
     *SRBC3,SRGAC,SRGBC,SRAE1,SRBE1,SRAE2,SRAE3,SRBE2,SRBE3,SSDA1,
     *SSDA2,SSDA3,SSDB1,SSDB2,SSDB3
      COMMON/SN2C/DCH,CRCH,CBCH,GARCH,GABCH,RCHI,RCHTS,BCHI,BCHTS
      DIMENSION DSLADQ(15),DSLBDQ(15),DRADQ(15),DRBDQ(15),
     *DSRADQ(9),DSRBDQ(9),DGADQ(9),DGBDQ(9),
     *DDADQ(9),DDBDQ(9),DS1ADQ(9),DS1BDQ(9),
     *T1(300),T2(300),T3(300),DBGADQ(9),
     *DBGBDQ(9),DS1CA(9),DS1CB(9),DSEADQ(9),DSEBDQ(9),DPHIOA(9),
     *DPHIOB(9),DGPHI(9),DCLCCL(9),DRCHDQ(9),DBCHDQ(9)
      LOGICAL FIRST
      DATA FIRST/.TRUE./
      SAVE FIRST
C
      IF (FIRST) THEN
         CALL PARSN2(NSN2)
         FIRST=.FALSE.
      ENDIF
C
C         DETERMINE RA, RB, GA, GB, RA EQUIL., AND RB EQUIL.
C
      RA=SQRT((Q(7)-Q(1))**2+(Q(8)-Q(2))**2+(Q(9)-Q(3))**2)
      RB=SQRT((Q(7)-Q(4))**2+(Q(8)-Q(5))**2+(Q(9)-Q(6))**2)
C
      GA=RA-RB
      GB=-GA
C
C         CALCULATION OF ALL ATOM-ATOM SEPARATIONS
C
      DO J=1,5
         DO K=2,NATOMS
            IF (K .GT. J) THEN
               JK=(J-1)*(2*NATOMS-J)/2+K-J
               J3=3*J
               J2=J3-1
               J1=J2-1
               K3=3*K
               K2=K3-1
               K1=K2-1
               T1(JK)=Q(K1)-Q(J1)
               T2(JK)=Q(K2)-Q(J2)
               T3(JK)=Q(K3)-Q(J3)
               R(JK)=SQRT(T1(JK)*T1(JK)+T2(JK)*T2(JK)
     *               +T3(JK)*T3(JK))
            ENDIF
         ENDDO
      ENDDO
C
C         DERIVATIVES OF RA
C
      DRADQ(1)=-(Q(7)-Q(1))/RA
      DRADQ(2)=-(Q(8)-Q(2))/RA
      DRADQ(3)=-(Q(9)-Q(3))/RA
      DRADQ(4)=0.0D0
      DRADQ(5)=0.0D0
      DRADQ(6)=0.0D0
      DRADQ(7)=-DRADQ(1)
      DRADQ(8)=-DRADQ(2)
      DRADQ(9)=-DRADQ(3)
C
C         DERIVATIVES OF RB
C
      DRBDQ(1)=0.0D0
      DRBDQ(2)=0.0D0
      DRBDQ(3)=0.0D0
      DRBDQ(4)=-(Q(7)-Q(4))/RB
      DRBDQ(5)=-(Q(8)-Q(5))/RB
      DRBDQ(6)=-(Q(9)-Q(6))/RB
      DRBDQ(7)=-DRBDQ(4)
      DRBDQ(8)=-DRBDQ(5)
      DRBDQ(9)=-DRBDQ(6)
C
C         DERIVATIVES OF GA AND GB
C
      DO I=1,9
         DGADQ(I)=DRADQ(I)-DRBDQ(I)
         DGBDQ(I)=-DGADQ(I)
      ENDDO
C
C         DERIVATIVES OF SLRA AND SLRB
C
      IF (GA .LE. SLGAC) THEN
         SLRA=0.0D0
         DO I=1,9
            DSLADQ(I)=0.0D0
         ENDDO
      ELSE
         DUM1=EXP(-SFACLR*(GA-SLGAC)**2)
         SLRA=1.0D0-DUM1
         DUM3=2.0D0*SFACLR*(GA-SLGAC)*DUM1
         DO I=1,9
            DSLADQ(I)=DUM3*DGADQ(I)
         ENDDO
      ENDIF
C
      IF (GB .LE. SLGBC) THEN
         SLRB=0.0D0
         DO I=1,9
            DSLBDQ(I)=0.0D0
         ENDDO
      ELSE
         DUM2=EXP(-SFBCLR*(GB-SLGBC)**2)
         SLRB=1.0D0-DUM2
         DUM4=2.0D0*SFBCLR*(GB-SLGBC)*DUM2
C
         DO I=1,9
            DSLBDQ(I)=DUM4*DGBDQ(I)
         ENDDO
      ENDIF
C  
C         CALCULATION OF SRGA AND SRGB AND RA AND RB EQUILIBRIUM
C
      DUMSLA=1.0D0-SLRA
      DUMSLB=1.0D0-SLRB
      IF (GA .GE. SRGAC)THEN
        SRGA=0.0D0
        RAO=RAC
        DO I=1,9
          DSRADQ(I)=0.0D0
        ENDDO
      ELSE
        DUM=SRGAC-GA
C
        DUM1=SRAA1*EXP(-SRAC1*DUM**SRAE1)
        DUM2=SRAA2*EXP(-SRAC2*DUM**SRAE2)
        DUM3=SRAA3*EXP(-SRAC3*DUM**SRAE3)
C
        SRGA=1.0D0-(DUM1+DUM2+DUM3)
        RAO=RAC+(RAI-RAC)*SRGA
C
C         CALCULATION OF DSRADQ
C
        PART1=-SRAC1*SRAE1*DUM**(SRAE1-1.0D0)*DUM1
        PART2=-SRAC2*SRAE2*DUM**(SRAE2-1.0D0)*DUM2
        PART3=-SRAC3*SRAE3*DUM**(SRAE3-1.0D0)*DUM3
C
        PART1=PART1+PART2+PART3
        DO I=1,9
          DSRADQ(I)=DGADQ(I)*PART1
        ENDDO
      ENDIF
      IF (GB .GE. SRGBC) THEN
         SRGB=0.0D0
         RBO=RBC
         DO I=1,9
            DSRBDQ(I)=0.0D0
         ENDDO
      ELSE
         DUM=SRGBC-GB
C
         DUM1=SRBA1*EXP(-SRBC1*DUM**SRBE1)
         DUM2=SRBA2*EXP(-SRBC2*DUM**SRBE2)
         DUM3=SRBA3*EXP(-SRBC3*DUM**SRBE3)
C
         SRGB=1.0D0-(DUM1+DUM2+DUM3)
         RBO=RBC+(RBI-RBC)*SRGB
C
C         CALCULATION OF DERIVATIVES OF SRGB
C
         PART1=-SRBC1*SRBE1*DUM**(SRBE1-1.0D0)*DUM1
         PART2=-SRBC2*SRBE2*DUM**(SRBE2-1.0D0)*DUM2
         PART3=-SRBC3*SRBE3*DUM**(SRBE3-1.0D0)*DUM3
C
         PART1=PART1+PART2+PART3
         DO I=1,9
            DSRBDQ(I)=DGBDQ(I)*PART1
         ENDDO
      ENDIF
C
C         CALCULATION OF SDGA
C
      DUM4=DCH3CL-DCLUSA
      DUM=SDGAC-GA
      IF (GA .GE. SDGAC) THEN
         SDGA=0.0D0
         DO I=1,9
            DDADQ(I)=0.0D0
         ENDDO
      ELSE
C
         DUM1=SDAA1*EXP(-SDAC1*DUM**SSDA1)
         DUM2=SDAA2*EXP(-SDAC2*DUM**SSDA2)
         DUM3=SDAA3*EXP(-SDAC3*DUM**SSDA3)
C
         SDGA=1.0D0-(DUM1+DUM2+DUM3)
C
C         CALCULATION OF DERIVATIVES OF SDGA AND DA
C
         PART1=-(SSDA1*SDAC1*DUM**(SSDA1-1.0D0)*DUM1)
         PART2=-(SSDA2*SDAC2*DUM**(SSDA2-1.0D0)*DUM2)
         PART3=-(SSDA3*SDAC3*DUM**(SSDA3-1.0D0)*DUM3)
C
         PART1=PART1+PART2+PART3
         DO I=1,9
            DDADQ(I)=DGADQ(I)*PART1*DUM4
         ENDDO
      ENDIF
C
C         CALCULATION OF SDGB
C
      DUM14=DCH3BR-DCLUSB
      DUM=SDGBC-GB
      IF (GB .GE. SDGBC) THEN
         SDGB=0.0D0
         DO I=1,9
            DDBDQ(I)=0.0D0
         ENDDO
      ELSE
C
         DUM1=SDBA1*EXP(-SDBC1*DUM**SSDB1)
         DUM2=SDBA2*EXP(-SDBC2*DUM**SSDB2)
         DUM3=SDBA3*EXP(-SDBC3*DUM**SSDB3)
C
         SDGB=1.0D0-(DUM1+DUM2+DUM3)
C
C         CALCULATION OF DERIVATIVES OF SDGB AND DB      
C
         PART1=-(SSDB1*SDBC1*DUM**(SSDB1-1.0D0)*DUM1)
         PART2=-(SSDB2*SDBC2*DUM**(SSDB2-1.0D0)*DUM2)
         PART3=-(SSDB3*SDBC3*DUM**(SSDB3-1.0D0)*DUM3)
C
         PART1=PART1+PART2+PART3
         DO I=1,9
            DDBDQ(I)=DGBDQ(I)*PART1*DUM14
         ENDDO
      ENDIF
C
C         CALCULATION OF DA AND DB
C
      DA=DCLUSA+DUM4*SDGA
      DB=DCLUSB+DUM14*SDGB
C
C         CALCULATION OF BETA(GA) AND ITS DERIVATIVE
C
      DUM=SBAC-GA
      IF (GA.GE.SBAC) THEN
         BGA=BACLUS
         DO I=1,9
            DBGADQ(I)=0.0D0
         ENDDO
      ELSE
         DUM1=EXP(-BAC*DUM**3)
         SBGA=1.0D0-DUM1
         BGA=BACLUS+(BAB-BACLUS)*SBGA
         DUM2=-3.0D0*BAC*DUM**2*DUM1*(BAB-BACLUS)
         DO I=1,9
            DBGADQ(I)=DUM2*DGADQ(I)
         ENDDO
      ENDIF
C
C         CALCULATION OF BETA(GB) AND ITS DERIVATIVE
C
      DUM=SBBC-GB
      IF (GB.GE.SBBC) THEN
         BGB=BBCLUS
         DO I=1,9
            DBGBDQ(I)=0.0D0
         ENDDO
      ELSE
         DUM1=EXP(-BBC*DUM**3)
         SBGB=1.0D0-DUM1
         BGB=BBCLUS+(BBB-BBCLUS)*SBGB
         DUM2=-3.0D0*BBC*DUM**2*DUM1*(BBB-BBCLUS)
         DO I=1,9
            DBGBDQ(I)=DUM2*DGBDQ(I)
         ENDDO
      ENDIF 
C
C         CALCULATION OF VMEPT
C
      DUM1=RA-RAO
      DUM2=RB-RBO
      DUM3=EXP(-2.0D0*BGA*DUM1)
      DUM4=EXP(-BGA*DUM1)
      DUM5=EXP(-2.0D0*BGB*DUM2)
      DUM6=EXP(-BGB*DUM2)
      PARTA=DUM3-2.0D0*DUM4
      PARTB=DUM5-2.0D0*DUM6
      VMEPT=DA*PARTA*DUMSLA+DB*PARTB*DUMSLB+DCH3BR
C
C         CALCULATION OF DVMEPTDQ
C
      DPARA1=2.0D0*BGA*(DUM4-DUM3)
      DPARB1=2.0D0*BGB*(DUM6-DUM5)
      DPARA2=2.0D0*DUM1*(DUM4-DUM3)
      DPARB2=2.0D0*DUM2*(DUM6-DUM5)
      DUMA=RAI-RAC
      DUMB=RBI-RBC
      DUM3=DPARA1*DUMSLA*DA
      DUM4=DPARB1*DUMSLB*DB
      DUM5=PARTA*DUMSLA
      DUM6=PARTB*DUMSLB      
      DUM7=DA*PARTA
      DUM8=DB*PARTB
      DUM9=DPARA2*DUMSLA*DA
      DUM10=DPARB2*DUMSLB*DB
      DO I=1,9
         PDOT(I)=DUM3*(DRADQ(I)-DUMA*DSRADQ(I))+DUM4*
     *           (DRBDQ(I)-DUMB*DSRBDQ(I))+PDOT(I)
         PDOT(I)=DDADQ(I)*DUM5+DDBDQ(I)*DUM6+PDOT(I)
         PDOT(I)=-DUM7*DSLADQ(I)-DUM8*DSLBDQ(I)+PDOT(I)       
         PDOT(I)=DUM9*DBGADQ(I)+DUM10*DBGBDQ(I)+PDOT(I)
      ENDDO
C
C         CALCULATION OF S1RA AND S1CA
C
      IF (RA.LE.RAI) THEN
         S1RA=1.0D0
         S1CA=1.0D0
         DO I=1,9
            DS1CA(I)=0.0D0
            DS1ADQ(I)=0.0D0
         ENDDO
      ELSE
         DUM=RA-RAI
         DUM1=S1RAA1*EXP(-S1RAC1*DUM**2)
         DUM2=S1RAA2*EXP(-S1RAC2*DUM**3)
         DUM3=S1RAA3*EXP(-S1RAC3*DUM**4)
         DUM4=S1RAA4*EXP(-S1RAC4*DUM**5)
         DUM5=S1RAA5*EXP(-S1RAC5*DUM**6)
         S1RA=DUM1+DUM2+DUM3+DUM4+DUM5
         S1CA=EXP(-S1CAC*DUM**2)
C
C         CALCULATION OF DERIVATIVES OF S1RA AND S1CA
C
         PART1=-2.0D0*S1RAC1*DUM*DUM1
         PART2=-3.0D0*S1RAC2*DUM**2*DUM2
         PART3=-4.0D0*S1RAC3*DUM**3*DUM3
         PART4=-5.0D0*S1RAC4*DUM**4*DUM4
         PART5=-6.0D0*S1RAC5*DUM**5*DUM5
         PART1=PART1+PART2+PART3+PART4+PART5
         PART6=-2.0D0*S1CAC*DUM*S1CA
         DO I=1,9
            DS1CA(I)=PART6*DRADQ(I)
            DS1ADQ(I)=PART1*DRADQ(I)
         ENDDO
      ENDIF
C
C         CALCULATION OF S1RB AND S1CB
C
      IF (RB.LE.RBI) THEN
         S1RB=1.0D0
         S1CB=1.0D0
         DO I=1,9
            DS1BDQ(I)=0.0D0
            DS1CB(I)=0.0D0
         ENDDO
      ELSE
         DUM=RB-RBI
         DUM1=S1RBA1*EXP(-S1RBC1*DUM**2)
         DUM2=S1RBA2*EXP(-S1RBC2*DUM**3)
         DUM3=S1RBA3*EXP(-S1RBC3*DUM**4)
         DUM4=S1RBA4*EXP(-S1RBC4*DUM**5)
         DUM5=S1RBA5*EXP(-S1RBC5*DUM**6)
         S1RB=DUM1+DUM2+DUM3+DUM4+DUM5
         S1CB=EXP(-S1CBC*DUM**2)
C
C         CALCULATION OF DERIVATIVES OF S1RB AND S1CB
C
         PART1=-2.0D0*S1RBC1*DUM*DUM1
         PART2=-3.0D0*S1RBC2*DUM**2*DUM2
         PART3=-4.0D0*S1RBC3*DUM**3*DUM3
         PART4=-5.0D0*S1RBC4*DUM**4*DUM4
         PART5=-6.0D0*S1RBC5*DUM**5*DUM5
         PART1=PART1+PART2+PART3+PART4+PART5
         PART6=-2.0D0*S1CBC*DUM*S1CB
         DO I=1,9
            DS1BDQ(I)=PART1*DRBDQ(I)
            DS1CB(I)=PART6*DRBDQ(I)
         ENDDO
      ENDIF
C
C         CALCULATION OF PHI FORCE CONSTANTS
C
      FPHIA=FPHIAZ*S1RA
      APHIA=APHIAZ*S1CA
      FPHIB=FPHIBZ*S1RB
      APHIB=APHIBZ*S1CB
C
C         CALCULATION OF SEA
C
      IF (RA.LE.2.50D0) THEN
         SEA=1.0D0
         DO I=1,9
            DSEADQ(I)=0.0D0
         ENDDO
      ELSE
         SEA=EXP(-SEAC*(RA-2.5D0)**2)
         DUM=-2.0D0*SEAC*(RA-2.5D0)*SEA
         DO I=1,9
            DSEADQ(I)=DUM*DRADQ(I)
         ENDDO
      ENDIF
C
C         CALCULATION OF SEB
C
      IF (RB.LE.2.50D0) THEN
         SEB=1.0D0
         DO I=1,9
            DSEBDQ(I)=0.0D0
         ENDDO
      ELSE
         SEB=EXP(-SEBC*(RB-2.5D0)**2)
         DUM=-2.0D0*SEBC*(RB-2.5D0)*SEB
         DO I=1,9
            DSEBDQ(I)=DUM*DRBDQ(I)
         ENDDO
      ENDIF
C
C         CALCULATION OF SPHIA AND PHIOA
C
      IF (GA.LE.SPGAC) THEN
         SPHIA=1.0D0
         DO I=1,9
            DPHIOA(I)=0.0D0
         ENDDO
         PHIAO=PHIAOB
      ELSE
         DUM=GA-SPGAC
         DUM1=SPAA1*EXP(-SPAC1*DUM**2)
         DUM2=SPAA2*EXP(-SPAC2*DUM**3)
         DUM3=SPAA3*EXP(-SPAC3*DUM**4)
         SPHIA=DUM1+DUM2+DUM3
         PHIAO=PHIAOI+(PHIAOB-PHIAOI)*SPHIA
C
C         CALCULATION OF DERIVATIVES OF PHIOA
C
         PART1=-2.0D0*SPAC1*DUM*DUM1
         PART2=-3.0D0*SPAC2*DUM**2*DUM2
         PART3=-4.0D0*SPAC3*DUM**3*DUM3
         PART1=(PART1+PART2+PART3)*(PHIAOB-PHIAOI)
         DO I=1,9
            DPHIOA(I)=PART1*DGADQ(I)
         ENDDO
      ENDIF      
C
C         CALCULATION OF SPHIB AND PHIOB
C
      IF (GB.LE.SPGBC) THEN
         SPHIB=1.0D0
         DO I=1,9
            DPHIOB(I)=0.0D0
         ENDDO
         PHIBO=PHIBOB
      ELSE
         DUM=GB-SPGBC
         DUM1=SPBA1*EXP(-SPBC1*DUM**2)
         DUM2=SPBA2*EXP(-SPBC2*DUM**3)
         DUM3=SPBA3*EXP(-SPBC3*DUM**4)
         SPHIB=DUM1+DUM2+DUM3
         PHIBO=PHIBOI+(PHIBOB-PHIBOI)*SPHIB
C
C         CALCULATION OF DERIVATIVES OF PHIOB
C
         PART1=-2.0D0*SPBC1*DUM*DUM1
         PART2=-3.0D0*SPBC2*DUM**2*DUM2
         PART3=-4.0D0*SPBC3*DUM**3*DUM3
         PART1=(PART1+PART2+PART3)*(PHIBOB-PHIBOI)
         DO I=1,9
            DPHIOB(I)=PART1*DGBDQ(I)
         ENDDO
      ENDIF      
C
C         CALCULATION OF PHIA
C
      VTPHI=0.0D0
      VH1=0.0D0
      VC=0.0D0
      VH2=0.0D0
C
C         CALCULATE INDICES FOR COORDINATES
C
      DO K=4,6
         M=3
         J=1
         J3=3
         J2=2
         J1=1
         K3=3*K
         K2=K3-1
         K1=K2-1
C
C         CALCULATE INDICES FOR R
C
         JM=(J-1)*(2*NATOMS-J)/2+M-J
         KM=(M-1)*(2*NATOMS-M)/2+K-M
C
C         CALCULATE RELATIVE COORDINATES
C
         D1=-T1(JM)
         D2=-T2(JM)
         D3=-T3(JM)
         D4=T1(KM)
         D5=T2(KM)
         D6=T3(KM)
C
C         CALCULATE PHIA 
C
         CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
         IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
         IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
         THETA(K+3*(J-2))=ACOS(CTHETA)
C
C         CALCULATION OF DPHIDQ
C  
         DUM=THETA(K+3*(J-2))-PHIAO
         DUMM=EXP(-VH2AC*DUM**2)
         PART1=0.5D0*FPHIA*DUM**2*SCALEP
         PART2=APHIA*DUM**3*SCALEP
         PART3=0.5D0*FPHIA*DUMM*DUM**2
         VH1=PART1+VH1
         VC=PART2+VC
         VH2=PART3+VH2
         DUM1=0.5D0*FPHIAZ*DUM**2*SCALEP
         DUM2=DUM**3*APHIAZ*SCALEP
         DUM3=DUM1*DUMM/SCALEP
         DO I=1,9
            PDOT(I)=DSEADQ(I)*(PART1+PART2-PART3)*DUMSLA+PDOT(I)
            PDOT(I)=(DS1ADQ(I)*DUM1+DS1CA(I)*DUM2)*SEA*DUMSLA+PDOT(I)
            PDOT(I)=(DS1ADQ(I)*DUM3)*(1.0D0-SEA)*DUMSLA+PDOT(I)
         ENDDO
         DUM4=FPHIA*DUM*SEA*DUMSLA*SCALEP
         DUM5=3.0D0*DUM**2*SEA*DUMSLA*APHIA*SCALEP
         DUM6=-(VH2AC*DUM**3*DUMM*FPHIA)*(1.0D0-SEA)*DUMSLA
         DUM7=(FPHIA*DUMM*DUM)*(1.0D0-SEA)*DUMSLA
         DUM8=DUM4+DUM5+DUM6+DUM7
         DO I=1,9
            PDOT(I)=-DPHIOA(I)*DUM8+PDOT(I)
         ENDDO
         DUM1=-1.0D0/SQRT(1.0D0-CTHETA**2)*DUM8
         RJK=R(JM)*R(KM)
         RJ2=R(JM)**2
         RK2=R(KM)**2
         DUM2=CTHETA/RJ2
         PDOT(J1)=PDOT(J1)+DUM1*(D4/RJK-D1*DUM2)
         PDOT(J2)=PDOT(J2)+DUM1*(D5/RJK-D2*DUM2)
         PDOT(J3)=PDOT(J3)+DUM1*(D6/RJK-D3*DUM2)
         DUM2=CTHETA/RK2
         PDOT(K1)=PDOT(K1)+DUM1*(D1/RJK-D4*DUM2)
         PDOT(K2)=PDOT(K2)+DUM1*(D2/RJK-D5*DUM2)
         PDOT(K3)=PDOT(K3)+DUM1*(D3/RJK-D6*DUM2)
         PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
         PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
         PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
      ENDDO
      VPHIA=((VH1+VC)*SEA+VH2*(1.0D0-SEA))
      VTPHIA=VPHIA*DUMSLA
      DO I=1,9
         IF (GA.LE.SLGAC) GOTO 110
         PDOT(I)=-DSLADQ(I)*VPHIA+PDOT(I)
      ENDDO
C
C         CALCULATION OF PHIB
C
  110 VH1=0.0D0
      VC=0.0D0
      VH2=0.0D0
C
C         CALCULATE INDICES FOR COORDINATES
C
      DO K=4,6
         M=3
         J=2
         J3=6
         J2=5
         J1=4
         K3=3*K
         K2=K3-1
         K1=K2-1
C
C         CALCULATE INDICES FOR R
C
         JM=(J-1)*(2*NATOMS-J)/2+M-J
         KM=(M-1)*(2*NATOMS-M)/2+K-M
C
C         CALCULATE RELATIVE COORDINATES
C
         D1=-T1(JM)
         D2=-T2(JM)
         D3=-T3(JM)
         D4=T1(KM)
         D5=T2(KM)
         D6=T3(KM)
C
C         CALCULATE PHIB
C
         CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
         IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
         IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
         THETA(K+3*(J-2))=ACOS(CTHETA)
C
C         CALCULATION OF DPHIBDQ
C  
         DUM=THETA(K+3*(J-2))-PHIBO
         DUMM=EXP(-VH2BC*DUM**2)
         PART1=0.5D0*FPHIB*DUM**2*SCALEP
         PART2=APHIB*DUM**3*SCALEP
         PART3=0.5D0*FPHIB*DUMM*DUM**2
         VH1=PART1+VH1
         VC=PART2+VC
         VH2=PART3+VH2
         DUM1=0.5D0*FPHIBZ*DUM**2*SCALEP
         DUM2=DUM**3*APHIBZ*SCALEP
         DUM3=DUM1*DUMM/SCALEP
         DO I=1,9
            PDOT(I)=DSEBDQ(I)*(PART1+PART2-PART3)*DUMSLB+PDOT(I)
            PDOT(I)=(DS1BDQ(I)*DUM1+DS1CB(I)*DUM2)*SEB*DUMSLB+PDOT(I)
            PDOT(I)=(DS1BDQ(I)*DUM3)*(1.0D0-SEB)*DUMSLB+PDOT(I)
         ENDDO
         DUM4=FPHIB*DUM*SEB*DUMSLB*SCALEP
         DUM5=3.0D0*DUM**2*SEB*DUMSLB*APHIB*SCALEP
         DUM6=-(VH2BC*DUM**3*DUMM*FPHIB)*(1.0D0-SEB)*DUMSLB
         DUM7=(FPHIB*DUMM*DUM)*(1.0D0-SEB)*DUMSLB
         DUM8=DUM4+DUM5+DUM6+DUM7
         DO I=1,9
            PDOT(I)=-DPHIOB(I)*DUM8+PDOT(I)
         ENDDO
         DUM1=-1.0D0/SQRT(1.0D0-CTHETA**2)*DUM8
         RJK=R(JM)*R(KM)
         RJ2=R(JM)**2
         RK2=R(KM)**2
         DUM2=CTHETA/RJ2
         PDOT(J1)=PDOT(J1)+DUM1*(D4/RJK-D1*DUM2)
         PDOT(J2)=PDOT(J2)+DUM1*(D5/RJK-D2*DUM2)
         PDOT(J3)=PDOT(J3)+DUM1*(D6/RJK-D3*DUM2)
         DUM2=CTHETA/RK2
         PDOT(K1)=PDOT(K1)+DUM1*(D1/RJK-D4*DUM2)
         PDOT(K2)=PDOT(K2)+DUM1*(D2/RJK-D5*DUM2)
         PDOT(K3)=PDOT(K3)+DUM1*(D3/RJK-D6*DUM2)
         PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
         PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
         PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
      ENDDO
      VPHIB=((VH1+VC)*SEB+VH2*(1.0D0-SEB))
      VTPHIB=VPHIB*DUMSLB
      DO I=1,9
         IF (GB.LE.SLGBC) GOTO 149
         PDOT(I)=-DSLBDQ(I)*VPHIB+PDOT(I)
      ENDDO
C
C         CALCULATION OF VCLACLB AND ITS DERIVATIVES
C
C         CALCULATE INDICES FOR COORDINATES
C
  149 J=1
      M=3
      K=2
C
C         CALCULATE INDICES FOR R
C
      JM=(J-1)*(2*NATOMS-J)/2+M-J
      KM=(K-1)*(2*NATOMS-K)/2+M-K
      JK=(J-1)*(2*NATOMS-J)/2+K-J
C
C         CALCULATE RELATIVE COOR.
C
      D1=-T1(JM)
      D2=-T2(JM)
      D3=-T3(JM)
      D4=-T1(KM)
      D5=-T2(KM)
      D6=-T3(KM)
      D7=-T1(JK)
      D8=-T2(JK)
      D9=-T3(JK)
C
C         CALCULATE CLACCLB ANGLE
C
      CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
      IF(CTHETA .GT. 1.0D0)CTHETA=1.0D0
      IF(CTHETA .LT. -1.0D0)CTHETA=-1.0D0
      THETA(7)=ACOS(CTHETA)
      DTHET=THETA(7)
C
C         CALCULATION OF VCLACLB
C
      DUM=(COS(DTHET/2.0D0))**2
      VCLCL=(PCLCLC/R(JK)**6+PCLCLG/R(JK)**4)*DUM
      VTCLCL=VCLCL*DUMSLA*DUMSLB
C
C         CALCULATION OF DVTCLCLDQ
C
      IF (DTHET .LE. 0.1D0) THEN
         DUM1=0.5D0-DTHET**2/48.0D0+DTHET**4/3840.D0-DTHET**6/645120.D0
         DUM2=1.0D0-DTHET**2/6.0D0+DTHET**4/120.0D0-DTHET**6/5040.0D0
         DUM1=DUM1/DUM2*COS(DTHET/2.0D0)
      ELSEIF ((PI-DTHET).LE.0.1D0) THEN
         DUMM=PI-DTHET
         DUM1=0.5D0-DUMM**2/48.0D0+DUMM**4/3840.D0-DUMM**6/645120.D0
         DUM2=1.0D0-DUMM**2/6.0D0+DUMM**4/120.0D0-DUMM**6/5040.0D0
         DUM1=DUM1/DUM2*SIN(DTHET/2.0D0)
      ELSE
        DUM1=COS(DTHET/2.0D0)*SIN(DTHET/2.0D0)/SIN(DTHET)
      ENDIF
C
      DUM4=DUMSLA*DUMSLB*(PCLCLC/R(JK)**6+
     #PCLCLG/R(JK)**4)
      RJK=R(JM)*R(KM)
      RJ2=R(JM)**2
      RK2=R(KM)**2
      DUM2=CTHETA/RJ2
      PDOT(1)=PDOT(1)+DUM1*(D4/RJK-D1*DUM2)*DUM4
      PDOT(2)=PDOT(2)+DUM1*(D5/RJK-D2*DUM2)*DUM4
      PDOT(3)=PDOT(3)+DUM1*(D6/RJK-D3*DUM2)*DUM4
      DUM2=CTHETA/RK2
      PDOT(4)=PDOT(4)+DUM1*(D1/RJK-D4*DUM2)*DUM4     
      PDOT(5)=PDOT(5)+DUM1*(D2/RJK-D5*DUM2)*DUM4
      PDOT(6)=PDOT(6)+DUM1*(D3/RJK-D6*DUM2)*DUM4
      PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))*DUM4
      PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))*DUM4
      PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))*DUM4
      DUM4=DUMSLA*DUMSLB*DUM
      DUM5=-PCLCLC*6.0D0/R(JK)**8-PCLCLG*4.0D0/R(JK)**6
      PDOT(1)=PDOT(1)+DUM5*D7*DUM4
      PDOT(2)=PDOT(2)+DUM5*D8*DUM4
      PDOT(3)=PDOT(3)+DUM5*D9*DUM4
      PDOT(4)=PDOT(4)-(DUM5*D7*DUM4)
      PDOT(5)=PDOT(5)-(DUM5*D8*DUM4)
      PDOT(6)=PDOT(6)-(DUM5*D9*DUM4)
      DO I=1,9
         PDOT(I)=-VCLCL*DSLADQ(I)*DUMSLB+PDOT(I)
         PDOT(I)=-VCLCL*DSLBDQ(I)*DUMSLA+PDOT(I)
      ENDDO
C
C         CALCULATION OF SFT,STH,VHCH, AND THEIR DERIVATIVES
C
      IF (GA.GE.GATS) THEN
         SIGN=1 
         STHC=STHCA
         STHE=STHEA
         THR=THA
      ELSE
         SIGN=-1
         STHC=STHCB
         STHE=STHEB
         THR=THB
      ENDIF
      GATH=SIGN*(GA-GATS)
      SFT=SFTA*EXP(-SFTC*GATH**2)+(1.0D0-SFTA)*EXP(-SFTD*GATH**4)
      STH=EXP(-STHC*GATH**STHE)
      DUM1=-2.0D0*SFTC*SIGN*GATH*SFTA*EXP(-SFTC*GATH**2)
      DUM2=-4.0D0*SIGN*GATH**3*(1.0D0-SFTA)*EXP(-SFTD*GATH**4)*SFTD
      DUM3=-SIGN*STHE*STHC*GATH**(STHE-1.0D0)*STH*(THTS-THR)
      DUM1=DUM1+DUM2
      DUM4=DUM1*(FTHTS-FTHB)
      DUM5=DUM1*(ATHTS-ATHB)
      DUM6=DUM1*(BTHTS-BTHB)
      FTH=FTHB+(FTHTS-FTHB)*SFT
      ATH=ATHB+(ATHTS-ATHB)*SFT
      BTH=BTHB+(BTHTS-BTHB)*SFT
      THO=THR+(THTS-THR)*STH
      M=3
      VHCH=0.0D0
      DO I=1,3
         IF (I .EQ. 1) THEN
            J=4
            K=5
         ELSEIF (I .EQ. 2) THEN
            J=4
            K=6
         ELSE
            J=5
            K=6
         ENDIF
C
C         CALCULATE INDICES FOR COORDINATES
C
         J3=3*J
         J2=J3-1
         J1=J2-1
         K3=3*K
         K2=K3-1
         K1=K2-1
C
C         CALCULATE INDICES FOR R
C
         JM=(M-1)*(2*NATOMS-M)/2+J-M
         KM=(M-1)*(2*NATOMS-M)/2+K-M
C
C         CALCULATE RELATIVE COORDINATES
C
         D1=T1(JM)
         D2=T2(JM)
         D3=T3(JM)
         D4=T1(KM)
         D5=T2(KM)
         D6=T3(KM)
C
C         CALCULATE HCH ANGLE
C
         CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
         IF (CTHETA .GT.1.0D0) CTHETA=1.0D00
         IF (CTHETA .LE.-1.0D0) CTHETA=-1.0D0
         THETA(I+7)=ACOS(CTHETA)
         DTHET=THETA(I+7)
         DUM=DTHET-THO
         VHCH=0.5D0*FTH*DUM**2+ATH*DUM**3+BTH*DUM**4+VHCH
         DUM7=0.5D0*DUM4*DUM**2+DUM5*DUM**3+DUM6*DUM**4
         DO L=1,9
            PDOT(L)=DUM7*DGADQ(L)+PDOT(L)
         ENDDO
         DUM7=-FTH*DUM*DUM3
         DUM8=-ATH*DUM**2*DUM3*3.0D0
         DUM9=-BTH*DUM**3*DUM3*4.0D0
         DUM7=DUM7+DUM8+DUM9
         DO L=1,9
            PDOT(L)=DUM7*DGADQ(L)+PDOT(L)
         ENDDO
         DUM1=-(FTH*DUM+3.0D0*ATH*DUM**2+4.0D0*BTH*DUM**3)/SIN(DTHET)
         RJK=R(JM)*R(KM)
         RJ2=R(JM)**2
         RK2=R(KM)**2
         DUM2=CTHETA/RJ2
         PDOT(J1)=PDOT(J1)+DUM1*(D4/RJK-D1*DUM2)
         PDOT(J2)=PDOT(J2)+DUM1*(D5/RJK-D2*DUM2)
         PDOT(J3)=PDOT(J3)+DUM1*(D6/RJK-D3*DUM2)
         DUM2=CTHETA/RK2
         PDOT(K1)=PDOT(K1)+DUM1*(D1/RJK-D4*DUM2)
         PDOT(K2)=PDOT(K2)+DUM1*(D2/RJK-D5*DUM2)
         PDOT(K3)=PDOT(K3)+DUM1*(D3/RJK-D6*DUM2)
         PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
         PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
         PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
      ENDDO
C
C         CALCULATION OF THE LONG RANGE POTENTIAL AND DERIVATIVES
C         VTLRA. DETERMINE R RELATIVE BETWEEN CLB OR BR AND C.
C
      VTLRA=0.0D0
      IF (GA.GT.SLGAC) THEN
        MK=6
        T1R=-T1(MK)/R(MK)
        T2R=-T2(MK)/R(MK)
        T3R=-T3(MK)/R(MK)
C
C         COORDINATES OF THE MASS CORRESPONDING TO 3 HYDROGENS
C
        CHD=0.341149D0
        X3H=Q(7)-CHD*T1R
        Y3H=Q(8)-CHD*T2R
        Z3H=Q(9)-CHD*T3R
        W3H=3.0D0*W(4)
        WT=W(2)+W(3)+W3H
        WTI=1.0D0/WT
        XCM=WTI*(W3H*X3H+W(3)*Q(7)+W(2)*Q(4))
        YCM=WTI*(W3H*Y3H+W(3)*Q(8)+W(2)*Q(5))
        ZCM=WTI*(W3H*Z3H+W(3)*Q(9)+W(2)*Q(6))
        T1CM=Q(1)-XCM
        T2CM=Q(2)-YCM
        T3CM=Q(3)-ZCM
        T4CM=Q(4)-XCM
        T5CM=Q(5)-YCM
        T6CM=Q(6)-ZCM
        RJCM=SQRT(T1CM**2+T2CM**2+T3CM**2)
        RKCM=SQRT(T4CM**2+T5CM**2+T6CM**2)
        CDELTA=(T1CM*T4CM+T2CM*T5CM+T3CM*T6CM)/RJCM/RKCM
        IF (CDELTA.GT.1.0D0) CDELTA=1.0D0
        IF (CDELTA.LT.-1.0D0) CDELTA=-1.0D0
        DELTA=ACOS(CDELTA)
        THET=PI-DELTA
        CTHETA=COS(THET)
        DUM1=VLRAA/RJCM**2*CTHETA
        VLRA=DUM1
        VTLRA=VLRA*SLRA
        DUM=W(3)*WTI
        DU=W3H*WTI
        DUM4=1.0D0/R(MK)
        PART1=T1(MK)/R(MK)**2
        PART2=T2(MK)/R(MK)**2
        PART3=T3(MK)/R(MK)**2
        DXCDXM=DUM+DU*(1.0D0-CHD*(-DUM4+PART1*DRBDQ(7)))
        DYCDXM=-DU*CHD*PART2*DRBDQ(7)
        DZCDXM=-DU*CHD*PART3*DRBDQ(7)
        DXCDYM=-DU*CHD*PART1*DRBDQ(8)
        DYCDYM=DUM+DU*(1.0D0-CHD*(-DUM4+PART2*DRBDQ(8)))
        DZCDYM=-DU*CHD*PART3*DRBDQ(8)
        DXCDZM=-DU*CHD*PART1*DRBDQ(9)
        DYCDZM=-DU*CHD*PART2*DRBDQ(9)
        DZCDZM=DUM+DU*(1.0D0-CHD*(-DUM4+PART3*DRBDQ(9)))
        DUM=W(2)*WTI
        DU=DU*CHD
        DXCDXK=DUM-DU*(DUM4+PART1*DRBDQ(4))
        DYCDXK=-DU*PART2*DRBDQ(4)
        DZCDXK=-DU*PART3*DRBDQ(4)
        DXCDYK=-DU*PART1*DRBDQ(5)
        DYCDYK=DUM-DU*(DUM4+PART2*DRBDQ(5))
        DZCDYK=-DU*PART3*DRBDQ(5)
        DXCDZK=-DU*PART1*DRBDQ(6)
        DYCDZK=-DU*PART2*DRBDQ(6)
        DZCDZK=DUM-DU*(DUM4+PART3*DRBDQ(6))
        DRJDXK=-(T1CM*DXCDXK+T2CM*DYCDXK+T3CM*DZCDXK)/RJCM
        DRJDYK=-(T1CM*DXCDYK+T2CM*DYCDYK+T3CM*DZCDYK)/RJCM
        DRJDZK=-(T1CM*DXCDZK+T2CM*DYCDZK+T3CM*DZCDZK)/RJCM
        DRJDXM=-(T1CM*DXCDXM+T2CM*DYCDXM+T3CM*DZCDXM)/RJCM
        DRJDYM=-(T1CM*DXCDYM+T2CM*DYCDYM+T3CM*DZCDYM)/RJCM
        DRJDZM=-(T1CM*DXCDZM+T2CM*DYCDZM+T3CM*DZCDZM)/RJCM
        DRKDXK=(T4CM*(1.0D0-DXCDXK)-T5CM*DYCDXK-T6CM*DZCDXK)/RKCM
        DRKDYK=(-T4CM*DXCDYK+T5CM*(1.0D0-DYCDYK)-T6CM*DZCDYK)/RKCM
        DRKDZK=(-T4CM*DXCDZK-T5CM*DYCDZK+T6CM*(1.0D0-DZCDZK))/RKCM
        DRKDXM=-(T4CM*DXCDXM+T5CM*DYCDXM+T6CM*DZCDXM)/RKCM
        DRKDYM=-(T4CM*DXCDYM+T5CM*DYCDYM+T6CM*DZCDYM)/RKCM
        DRKDZM=-(T4CM*DXCDZM+T5CM*DYCDZM+T6CM*DZCDZM)/RKCM
        DO I=1,9
          PDOT(I)=PDOT(I)+VLRA*DSLADQ(I)
        ENDDO
        DUM3=(-DUM1*2.0D0/RJCM)*SLRA
        PDOT(1)=PDOT(1)+DUM3*T1CM/RJCM
        PDOT(2)=PDOT(2)+DUM3*T2CM/RJCM
        PDOT(3)=PDOT(3)+DUM3*T3CM/RJCM
        PDOT(4)=PDOT(4)+DUM3*DRJDXK
        PDOT(5)=PDOT(5)+DUM3*DRJDYK
        PDOT(6)=PDOT(6)+DUM3*DRJDZK
        PDOT(7)=PDOT(7)+DUM3*DRJDXM
        PDOT(8)=PDOT(8)+DUM3*DRJDYM
        PDOT(9)=PDOT(9)+DUM3*DRJDZM
        RJK=RJCM*RKCM
        RJ2=RJCM**2
        RK2=RKCM**2
        DUM1=VLRAA/RJ2*SLRA
        PDOT(1)=-(T4CM/RJK-T1CM/RJ2*CDELTA)*DUM1+PDOT(1)
        PDOT(2)=-(T5CM/RJK-T2CM/RJ2*CDELTA)*DUM1+PDOT(2)
        PDOT(3)=-(T6CM/RJK-T3CM/RJ2*CDELTA)*DUM1+PDOT(3)
C
        PDOT(7)=PDOT(7)-DUM1*((-DXCDXM*(T4CM+T1CM)-DYCDXM*(T5CM+T2CM)
     #  -DZCDXM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDXM/RJCM+DRKDXM/RKCM))
C
        PDOT(8)=PDOT(8)-DUM1*((-DXCDYM*(T4CM+T1CM)-DYCDYM*(T5CM+T2CM)
     #  -DZCDYM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDYM/RJCM+DRKDYM/RKCM))
        PDOT(9)=PDOT(9)-DUM1*((-DXCDZM*(T4CM+T1CM)-DYCDZM*(T5CM+T2CM)
     #  -DZCDZM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDZM/RJCM+DRKDZM/RKCM))
C
        PDOT(4)=-DUM1*((-DXCDXK*T4CM+(1.0D0-DXCDXK)*T1CM-DYCDXK*(T5CM
     #    +T2CM)-DZCDXK*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDXK/RJCM+
     #     DRKDXK/RKCM))+PDOT(4)
        PDOT(5)=-DUM1*((-DYCDYK*T5CM+(1.0D0-DYCDYK)*T2CM-DXCDYK*(T4CM
     #    +T1CM)-DZCDYK*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDYK/RJCM+
     #     DRKDYK/RKCM))+PDOT(5)
        PDOT(6)=-DUM1*((-DZCDZK*T6CM+(1.0D0-DZCDZK)*T3CM-DYCDZK*(T5CM
     #    +T2CM)-DXCDZK*(T4CM+T1CM))/RJCM/RKCM-CDELTA*(DRJDZK/RJCM+
     #     DRKDZK/RKCM))+PDOT(6)
      ENDIF
C
C         CALCULATE VTLRB
C         DETERMINE R RELATIVE BETWEEN CLA AND C
C
      VTLRB=0.0D0
      IF (GB.GT.SLGBC) THEN
         MK=2
         T1R=-T1(MK)/R(MK)
         T2R=-T2(MK)/R(MK)
         T3R=-T3(MK)/R(MK)
C
C         COORDINATES OF THE MASS CORRESPONDING TO 3 HYDROGENS
C
         CHD=0.341149D0
         X3H=Q(7)-CHD*T1R
         Y3H=Q(8)-CHD*T2R
         Z3H=Q(9)-CHD*T3R
         W3H=3.0D0*W(4)
         WT=W(1)+W(3)+W3H
         WTI=1.0D0/WT
         XCM=WTI*(W3H*X3H+W(3)*Q(7)+W(1)*Q(1))
         YCM=WTI*(W3H*Y3H+W(3)*Q(8)+W(1)*Q(2))
         ZCM=WTI*(W3H*Z3H+W(3)*Q(9)+W(1)*Q(3))
         T1CM=Q(4)-XCM
         T2CM=Q(5)-YCM
         T3CM=Q(6)-ZCM
         T4CM=Q(1)-XCM
         T5CM=Q(2)-YCM
         T6CM=Q(3)-ZCM
         RJCM=SQRT(T1CM**2+T2CM**2+T3CM**2)
         RKCM=SQRT(T4CM**2+T5CM**2+T6CM**2)
         CDELTA=(T1CM*T4CM+T2CM*T5CM+T3CM*T6CM)/RJCM/RKCM
         IF (CDELTA.GT.1.0D0) CDELTA=1.0D0
         IF (CDELTA.LT.-1.0D0) CDELTA=-1.0D0
         DELTA=ACOS(CDELTA)
         THET=PI-DELTA
         CTHETA=COS(THET)
         DUM1=VLRBA/RJCM**2*CTHETA
         VLRB=DUM1
         VTLRB=VLRB*SLRB
         DUM=W(3)*WTI
         DU=W3H*WTI
         DUM4=1.0D0/R(MK)
         PART1=T1(MK)/R(MK)**2
         PART2=T2(MK)/R(MK)**2
         PART3=T3(MK)/R(MK)**2
         DXCDXM=DUM+DU*(1.0D0-CHD*(-DUM4+PART1*DRADQ(7)))
         DYCDXM=-DU*CHD*PART2*DRADQ(7)
         DZCDXM=-DU*CHD*PART3*DRADQ(7)
         DXCDYM=-DU*CHD*PART1*DRADQ(8)
         DYCDYM=DUM+DU*(1.0D0-CHD*(-DUM4+PART2*DRADQ(8)))
         DZCDYM=-DU*CHD*PART3*DRADQ(8)
         DXCDZM=-DU*CHD*PART1*DRADQ(9)
         DYCDZM=-DU*CHD*PART2*DRADQ(9)
         DZCDZM=DUM+DU*(1.0D0-CHD*(-DUM4+PART3*DRADQ(9)))
         DUM=W(1)*WTI
         DU=DU*CHD
         DXCDXK=DUM-DU*(DUM4+PART1*DRADQ(1))
         DYCDXK=-DU*PART2*DRADQ(1)
         DZCDXK=-DU*PART3*DRADQ(1)
         DXCDYK=-DU*PART1*DRADQ(2)
         DYCDYK=DUM-DU*(DUM4+PART2*DRADQ(2))
         DZCDYK=-DU*PART3*DRADQ(2)
         DXCDZK=-DU*PART1*DRADQ(3)
         DYCDZK=-DU*PART2*DRADQ(3)
         DZCDZK=DUM-DU*(DUM4+PART3*DRADQ(3))
         DRJDXK=-(T1CM*DXCDXK+T2CM*DYCDXK+T3CM*DZCDXK)/RJCM
         DRJDYK=-(T1CM*DXCDYK+T2CM*DYCDYK+T3CM*DZCDYK)/RJCM
         DRJDZK=-(T1CM*DXCDZK+T2CM*DYCDZK+T3CM*DZCDZK)/RJCM
         DRJDXM=-(T1CM*DXCDXM+T2CM*DYCDXM+T3CM*DZCDXM)/RJCM
         DRJDYM=-(T1CM*DXCDYM+T2CM*DYCDYM+T3CM*DZCDYM)/RJCM
         DRJDZM=-(T1CM*DXCDZM+T2CM*DYCDZM+T3CM*DZCDZM)/RJCM
         DRKDXK=(T4CM*(1.0D0-DXCDXK)-T5CM*DYCDXK-T6CM*DZCDXK)/RKCM
         DRKDYK=(-T4CM*DXCDYK+T5CM*(1.0D0-DYCDYK)-T6CM*DZCDYK)/RKCM
         DRKDZK=(-T4CM*DXCDZK-T5CM*DYCDZK+T6CM*(1.0D0-DZCDZK))/RKCM
         DRKDXM=-(T4CM*DXCDXM+T5CM*DYCDXM+T6CM*DZCDXM)/RKCM
         DRKDYM=-(T4CM*DXCDYM+T5CM*DYCDYM+T6CM*DZCDYM)/RKCM
         DRKDZM=-(T4CM*DXCDZM+T5CM*DYCDZM+T6CM*DZCDZM)/RKCM
         DO I=1,9
            PDOT(I)=PDOT(I)+VLRB*DSLBDQ(I)
         ENDDO
         DUM3=(-DUM1*2.0D0/RJCM)*SLRB
         PDOT(4)=PDOT(4)+DUM3*T1CM/RJCM
         PDOT(5)=PDOT(5)+DUM3*T2CM/RJCM
         PDOT(6)=PDOT(6)+DUM3*T3CM/RJCM
         PDOT(1)=PDOT(1)+DUM3*DRJDXK
         PDOT(2)=PDOT(2)+DUM3*DRJDYK
         PDOT(3)=PDOT(3)+DUM3*DRJDZK
         PDOT(7)=PDOT(7)+DUM3*DRJDXM
         PDOT(8)=PDOT(8)+DUM3*DRJDYM
         PDOT(9)=PDOT(9)+DUM3*DRJDZM
         RJK=RJCM*RKCM
         RJ2=RJCM**2
         RK2=RKCM**2
         DUM1=VLRBA/RJ2*SLRB
         PDOT(4)=-(T4CM/RJK-T1CM/RJ2*CDELTA)*DUM1+PDOT(4)
         PDOT(5)=-(T5CM/RJK-T2CM/RJ2*CDELTA)*DUM1+PDOT(5)
         PDOT(6)=-(T6CM/RJK-T3CM/RJ2*CDELTA)*DUM1+PDOT(6)
C
         PDOT(7)=PDOT(7)-DUM1*((-DXCDXM*(T4CM+T1CM)-DYCDXM*(T5CM+T2CM)
     #  -DZCDXM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDXM/RJCM+DRKDXM/RKCM))
C
         PDOT(8)=PDOT(8)-DUM1*((-DXCDYM*(T4CM+T1CM)-DYCDYM*(T5CM+T2CM)
     #  -DZCDYM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDYM/RJCM+DRKDYM/RKCM))
         PDOT(9)=PDOT(9)-DUM1*((-DXCDZM*(T4CM+T1CM)-DYCDZM*(T5CM+T2CM)
     #  -DZCDZM*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDZM/RJCM+DRKDZM/RKCM))
C
         PDOT(1)=-DUM1*((-DXCDXK*T4CM+(1.0D0-DXCDXK)*T1CM-DYCDXK*(T5CM
     #         +T2CM)-DZCDXK*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDXK/RJCM+
     #         DRKDXK/RKCM))+PDOT(1)
         PDOT(2)=-DUM1*((-DYCDYK*T5CM+(1.0D0-DYCDYK)*T2CM-DXCDYK*(T4CM
     #         +T1CM)-DZCDYK*(T6CM+T3CM))/RJCM/RKCM-CDELTA*(DRJDYK/RJCM+
     #         DRKDYK/RKCM))+PDOT(2)
         PDOT(3)=-DUM1*((-DZCDZK*T6CM+(1.0D0-DZCDZK)*T3CM-DYCDZK*(T5CM
     #         +T2CM)-DXCDZK*(T4CM+T1CM))/RJCM/RKCM-CDELTA*(DRJDZK/RJCM+
     #         DRKDZK/RKCM))+PDOT(3)
      ENDIF
C
C         CALCULATION OF VCLHA
C
      VTCLAH=0.0D0
      IF(GA.GT.SLGAC)THEN
         VCLAH=0.0D0
         J=1
         DO K=4,6
            K3=3*K
            K2=K3-1
            K1=K2-1
            JK=(J-1)*(2*NATOMS-J)/2+K-J
            DUM1=VCLAHD/R(JK)**4
            DUM=4.0D0*DUM1/R(JK)**2*SLRA
            PDOT(1)=DUM*T1(JK)+PDOT(1)
            PDOT(2)=DUM*T2(JK)+PDOT(2)
            PDOT(3)=DUM*T3(JK)+PDOT(3)
            PDOT(K1)=-DUM*T1(JK)+PDOT(K1)
            PDOT(K2)=-DUM*T2(JK)+PDOT(K2)
            PDOT(K3)=-DUM*T3(JK)+PDOT(K3)
            VCLAH=DUM1+VCLAH
         ENDDO
         DO I=1,9
            PDOT(I)=DSLADQ(I)*VCLAH+PDOT(I)
         ENDDO
         VTCLAH=VCLAH*SLRA
      ENDIF
C
C         CALCULATION OF VCLHB
C
      VTCLBH=0.0D0
      IF (GB.GT.SLGBC) THEN
         VCLBH=0.0D0
         J=2
         DO K=4,6
            K3=3*K
            K2=K3-1
            K1=K2-1
            JK=(J-1)*(2*NATOMS-J)/2+K-J
            DUM1=VCLBHD/R(JK)**4
            DUM=4.0D0*DUM1/R(JK)**2*SLRB
            PDOT(4)=DUM*T1(JK)+PDOT(4)
            PDOT(5)=DUM*T2(JK)+PDOT(5)
            PDOT(6)=DUM*T3(JK)+PDOT(6)
            PDOT(K1)=-DUM*T1(JK)+PDOT(K1)
            PDOT(K2)=-DUM*T2(JK)+PDOT(K2)
            PDOT(K3)=-DUM*T3(JK)+PDOT(K3)
            VCLBH=DUM1+VCLBH
         ENDDO
         DO I=1,9
            PDOT(I)=DSLBDQ(I)*VCLBH+PDOT(I)
         ENDDO
         VTCLBH=VCLBH*SLRB
      ENDIF
C
C         CALCULATION OF PHIA AND ETA OFF DIAGONAL TERMS
C
      VTPEA=0.0D0
      IF (GA.GT.SLGAC) THEN
         DUM=EXP(-GPHIAC*(RA-RAC)**2)
         GPHI=GPHIAZ*DUM
         DDUM=-2.0D0*GPHIAC*(RA-RAC)*GPHI
         DO I=1,9
            DGPHI(I)=DDUM*DRADQ(I)
         ENDDO
         M=3
         J=1
         K=2
C
C         CALCULATE INDICES FOR R
C
         JM=(J-1)*(2*NATOMS-J)/2+M-J
         KM=(K-1)*(2*NATOMS-K)/2+M-K
C
C         CALCULATE RELATIVE COOR.
C
         D1=-T1(JM)
         D2=-T2(JM)
         D3=-T3(JM)
         D4=-T1(KM)
         D5=-T2(KM)
         D6=-T3(KM)
C
C         CALCULATE CLACCLB ANGLE
C
         CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
         IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
         IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
         THETA(7)=ACOS(CTHETA)
         DTHET=THETA(7)
         DUMM=PI-DTHET
         IF (DUMM.LE.0.1D0) THEN
            DUM1=1.0D0/(1.0D0-DUMM**2/81.0D0+DUMM**4/120.0D0
     *            -DUMM**6/5040.D0)
         ELSE
            DUM1=DUMM/SIN(DUMM)
         ENDIF
C
         RJK=R(JM)*R(KM)
         RJ2=R(JM)**2
         RK2=R(KM)**2
         DUM2=CTHETA/RJ2
         DCLCCL(1)=DUM1*(D4/RJK-D1*DUM2)
         DCLCCL(2)=DUM1*(D5/RJK-D2*DUM2)
         DCLCCL(3)=DUM1*(D6/RJK-D3*DUM2)
         DUM2=CTHETA/RK2
         DCLCCL(4)=DUM1*(D1/RJK-D4*DUM2)
         DCLCCL(5)=DUM1*(D2/RJK-D5*DUM2)
         DCLCCL(6)=DUM1*(D3/RJK-D6*DUM2)
         DCLCCL(7)=DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
         DCLCCL(8)=DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
         DCLCCL(9)=DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
C
C         CALCULATE PHIA ANGLES
C
         VPE=0.0D0
         DO K=4,6
            K3=3*K
            K2=K3-1
            K1=K2-1
C
C         CALCULATE INDICES FOR R
C
            KM=(M-1)*(2*NATOMS-M)/2+K-M
C
C         CALCULATE RELATIVE COORDINATES
C
            D4=T1(KM)
            D5=T2(KM)
            D6=T3(KM)
C
C         CALCULATE PHIA 
C
            CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
            IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
            IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
            THETA(K+3*(J-2))=ACOS(CTHETA)
C
C         CALCULATION OF DPHIDQ
C  
            DUM=THETA(K+3*(J-2))-PHIAO
            WHAM=GPHI*DUM*DUMM**2
            VPE=WHAM+VPE
            PART1=DUM*DUMM**2*SLRA
            PART2=2.0D0*GPHI*DUM*SLRA
            PART3=GPHI*DUMM**2*SLRA
            DO I=1,9
               PDOT(I)=PDOT(I)+DGPHI(I)*PART1
               PDOT(I)=PDOT(I)+DCLCCL(I)*PART2
               PDOT(I)=PDOT(I)-DPHIOA(I)*PART3
            ENDDO
            DUM1=-1.0D0/SQRT(1.0D0-CTHETA**2)*PART3
            RJK=R(JM)*R(KM)
            RJ2=R(JM)**2
            RK2=R(KM)**2
            DUM2=CTHETA/RJ2
            PDOT(1)=PDOT(1)+DUM1*(D4/RJK-D1*DUM2)
            PDOT(2)=PDOT(2)+DUM1*(D5/RJK-D2*DUM2)
            PDOT(3)=PDOT(3)+DUM1*(D6/RJK-D3*DUM2)
            DUM2=CTHETA/RK2
            PDOT(K1)=PDOT(K1)+DUM1*(D1/RJK-D4*DUM2)
            PDOT(K2)=PDOT(K2)+DUM1*(D2/RJK-D5*DUM2)
            PDOT(K3)=PDOT(K3)+DUM1*(D3/RJK-D6*DUM2)
            PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
            PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
            PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
         ENDDO
         VTPEA=VPE*SLRA
         DO I=1,9
            PDOT(I)=PDOT(I)+DSLADQ(I)*VPE
         ENDDO
      ENDIF
C
C         CALCULATION OF PHIB AND ETA OFF DIAGONAL TERMS
C
      VTPEB=0.0D0
      IF (GB.GT.SLGBC) THEN
         DUM=EXP(-GPHIBC*(RB-RBC)**2)
         GPHI=GPHIBZ*DUM
         DDUM=-2.0D0*GPHIBC*(RB-RBC)*GPHI
         DO I=1,9
            DGPHI(I)=DDUM*DRBDQ(I)
         ENDDO
         M=3
         J=2
         K=1
C
C         CALCULATE INDICES FOR R
C
         JM=(J-1)*(2*NATOMS-J)/2+M-J
         KM=(K-1)*(2*NATOMS-K)/2+M-K
C
C         CALCULATE RELATIVE COOR.
C
         D1=-T1(JM)
         D2=-T2(JM)
         D3=-T3(JM)
         D4=-T1(KM)
         D5=-T2(KM)
         D6=-T3(KM)
C
C         CALCULATE CLACCLB ANGLE
C
         CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
         IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
         IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
         THETA(7)=ACOS(CTHETA)
         DTHET=THETA(7)
         DUMM=PI-DTHET
         IF (DUMM.LE.0.1D0) THEN
            DUM1=1.0D0/(1.0D0-DUMM**2/81.0D0+DUMM**4/120.0D0
     *            -DUMM**6/5040.D0)
         ELSE
            DUM1=DUMM/SIN(DUMM)
         ENDIF
C
         RJK=R(JM)*R(KM)
         RJ2=R(JM)**2
         RK2=R(KM)**2
         DUM2=CTHETA/RJ2
         DCLCCL(4)=DUM1*(D4/RJK-D1*DUM2)
         DCLCCL(5)=DUM1*(D5/RJK-D2*DUM2)
         DCLCCL(6)=DUM1*(D6/RJK-D3*DUM2)
         DUM2=CTHETA/RK2
         DCLCCL(1)=DUM1*(D1/RJK-D4*DUM2)
         DCLCCL(2)=DUM1*(D2/RJK-D5*DUM2)
         DCLCCL(3)=DUM1*(D3/RJK-D6*DUM2)
         DCLCCL(7)=DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
         DCLCCL(8)=DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
         DCLCCL(9)=DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
C
C         CALCULATE PHIB ANGLES
C
         VPE=0.0D0
         DO K=4,6
            K3=3*K
            K2=K3-1
            K1=K2-1
C
C         CALCULATE INDICES FOR R
C
            KM=(M-1)*(2*NATOMS-M)/2+K-M
C
C         CALCULATE RELATIVE COORDINATES
C
            D4=T1(KM)
            D5=T2(KM)
            D6=T3(KM)
C     CALCULATE PHIB 
            CTHETA=(D1*D4+D2*D5+D3*D6)/R(JM)/R(KM)
            IF (CTHETA .GT. 1.0D0) CTHETA=1.0D0
            IF (CTHETA .LT. -1.0D0) CTHETA=-1.0D0
            THETA(K+3*(J-2))=ACOS(CTHETA)
C
C         CALCULATION OF DPHIDQ
C  
            DUM=THETA(K+3*(J-2))-PHIBO
            WHAM=GPHI*DUM*DUMM**2
            VPE=WHAM+VPE
            PART1=DUM*DUMM**2*SLRB
            PART2=2.0D0*GPHI*DUM*SLRB
            PART3=GPHI*DUMM**2*SLRB
            DO I=1,9
               PDOT(I)=PDOT(I)+DGPHI(I)*PART1
               PDOT(I)=PDOT(I)+DCLCCL(I)*PART2
               PDOT(I)=PDOT(I)-DPHIOB(I)*PART3
            ENDDO
            DUM1=-1.0D0/SQRT(1.0D0-CTHETA**2)*PART3
            RJK=R(JM)*R(KM)
            RJ2=R(JM)**2
            RK2=R(KM)**2
            DUM2=CTHETA/RJ2
            PDOT(4)=PDOT(4)+DUM1*(D4/RJK-D1*DUM2)
            PDOT(5)=PDOT(5)+DUM1*(D5/RJK-D2*DUM2)
            PDOT(6)=PDOT(6)+DUM1*(D6/RJK-D3*DUM2)
            DUM2=CTHETA/RK2
            PDOT(K1)=PDOT(K1)+DUM1*(D1/RJK-D4*DUM2)
            PDOT(K2)=PDOT(K2)+DUM1*(D2/RJK-D5*DUM2)
            PDOT(K3)=PDOT(K3)+DUM1*(D3/RJK-D6*DUM2)
            PDOT(7)=PDOT(7)+DUM1*((-D1-D4)/RJK+CTHETA*(D1/RJ2+D4/RK2))
            PDOT(8)=PDOT(8)+DUM1*((-D2-D5)/RJK+CTHETA*(D2/RJ2+D5/RK2))
            PDOT(9)=PDOT(9)+DUM1*((-D3-D6)/RJK+CTHETA*(D3/RJ2+D6/RK2))
         ENDDO
         VTPEB=VPE*SLRB
         DO I=1,9
            PDOT(I)=PDOT(I)+DSLBDQ(I)*VPE
         ENDDO
      ENDIF
C
C         THE MORSE STRETCHING OF C-H BOND
C
      NCH=3
      VMCH=0.0D0
C
C         INDICES FOR C-H STRETCHING
C
      N2J(1)=3
      N2K(1)=4
      N2J(2)=3
      N2K(2)=5
      N2J(3)=3
      N2K(3)=6
C
C         OTHER PARAMETERS NEED TO BE INPUT
C         C-H BOND LENGTH TO BE VARIED ALONG THE REACTION PATH
C
      SRCH=EXP(-CRCH*(GA-GARCH)**2)
      RMZ(1)=RCHI+(RCHTS-RCHI)*SRCH
      RMZ(2)=RCHI+(RCHTS-RCHI)*SRCH
      RMZ(3)=RCHI+(RCHTS-RCHI)*SRCH
C
C         CALCULATE DERIVATIVES OF SRCH
C
      DUM1=RCHTS-RCHI
      PART1=-2.0D0*CRCH*(GA-GARCH)*SRCH
      DO I=1,9
         DRCHDQ(I)=DUM1*PART1*DGADQ(I)
      ENDDO
C
C         THE BETA VALUE TO BE VARIED ALONG THE REACTION PATH
C
      SBCH=EXP(-CBCH*(GA-GABCH)**2)
      B(1)=BCHI+(BCHTS-BCHI)*SBCH
      B(2)=BCHI+(BCHTS-BCHI)*SBCH
      B(3)=BCHI+(BCHTS-BCHI)*SBCH
C
C         CALCULATE DERIVATIBES OF SBCH
C
      DUM1=BCHTS-BCHI
      PART1=-2.0D0*CBCH*(GA-GABCH)*SBCH
      DO I=1,9
         DBCHDQ(I)=DUM1*PART1*DGADQ(I)
      ENDDO
C
      D(1)=DCH
      D(2)=DCH
      D(3)=DCH
C
      DO I=1,NCH
         NJ=N2J(I)
         NK=N2K(I)
         JK=(NJ-1)*(2*NATOMS-NJ)/2+NK-NJ
         BETAM=B(I)
         RLCM=R(JK)-RMZ(I)
         VMCH=VMCH+D(I)*(1.0D0-EXP(-BETAM*RLCM))**2
      ENDDO
C
      DO J=1,NCH
         NJ=N2J(J)
         NK=N2K(J)
         JK=(NJ-1)*(2*NATOMS-NJ)/2+NK-NJ
         DUM=EXP(-B(J)*(R(JK)-RMZ(J)))
         DUM1=1.0D0-DUM
         DUM2=2.0D0*D(J)*DUM1
         DO I=1,9
            PDOT(I)=PDOT(I)-DUM2*DUM*(B(J)*DRCHDQ(I)-
     *               (R(JK)-RMZ(J))*DBCHDQ(I))
         ENDDO
      ENDDO
C
      DO I=1,NCH
         CALL MORSE(I)
      ENDDO
C
C         CALCULATION OF TOTAL POTENTIAL ENERGY FOR THE SN2 REACTION
C
      PESN2=VMEPT+VTPHIA+VTPHIB+VTCLCL+VHCH+VTLRA+VTLRB
     #+VTCLAH+VTCLBH+VTPEA+VTPEB+VMCH
      RETURN
      END
