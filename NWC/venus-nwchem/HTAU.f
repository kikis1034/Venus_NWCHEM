      SUBROUTINE HTAU(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE N-FOLD TORSION POTENTIAL ENERGY DERIVATIVES
C         (N=2 OR 3)
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/TAUB/VZTAU(ND06),N6I(ND06),N6J(ND06),N6K(ND06),N6L(ND06),
     *N6M(ND06),N6N(ND06)
C
C         CALCULATE INDICES FOR COORDINATES
C
      I3=3*N6I(NL)
      I2=I3-1
      I1=I2-1
      J3=3*N6J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N6K(NL)
      K2=K3-1
      K1=K2-1
      L3=3*N6L(NL)
      L2=L3-1
      L1=L2-1
      M3=3*N6M(NL)
      M2=M3-1
      M1=M2-1
      N3=3*N6N(NL)
      N2=N3-1
      N1=N2-1
C
C         CALCULATE RELATIVE COORDINATES
C
      XIL=Q(I1)-Q(L1)
      YIL=Q(I2)-Q(L2)
      ZIL=Q(I3)-Q(L3)
      XJK=Q(J1)-Q(K1)
      YJK=Q(J2)-Q(K2)
      ZJK=Q(J3)-Q(K3)
      XMN=Q(M1)-Q(N1)
      YMN=Q(M2)-Q(N2)
      ZMN=Q(M3)-Q(N3)
C
C         CALCULATE COSINE OF TAU ANGLE
C
      AX=YMN*ZIL-ZMN*YIL
      AY=ZMN*XIL-XMN*ZIL
      AZ=XMN*YIL-YMN*XIL
      BX=YJK*ZIL-ZJK*YIL
      BY=ZJK*XIL-XJK*ZIL
      BZ=XJK*YIL-YJK*XIL
      RA=SQRT(AX*AX+AY*AY+AZ*AZ)
      RB=SQRT(BX*BX+BY*BY+BZ*BZ)
      COSTAU=(AX*BX+AY*BY+AZ*BZ)/RA/RB
      IF (COSTAU.LT.-1.00D0) COSTAU=-1.00D0
      IF (COSTAU.GT. 1.00D0) COSTAU= 1.00D0
      CTAU(NL)=COSTAU
C
C         CALCULATE TORSION POTENTIALS
C         VZTAU .LT. ZERO INDICATES 3-FOLD TORSION
C
      DUM4=2.0D0*COSTAU
      IF (VZTAU(NL).LT.0.0D0) DUM4=6.0D0*COSTAU**2-1.5D0
      DUM4=-VZTAU(NL)*DUM4
C
C         CALCULATE(DV/DQ)'S
C
      DUM1=RA*RB
      DUM2=COSTAU/RA/RA
      DCTDAX=BX/DUM1-DUM2*AX
      DCTDAY=BY/DUM1-DUM2*AY
      DCTDAZ=BZ/DUM1-DUM2*AZ
      DUM2=COSTAU/RB/RB
      DCTDBX=AX/DUM1-DUM2*BX
      DCTDBY=AY/DUM1-DUM2*BY
      DCTDBZ=AZ/DUM1-DUM2*BZ
      DUM1=DUM4*(DCTDAY*ZMN-DCTDAZ*YMN+DCTDBY*ZJK-DCTDBZ*YJK)
      DUM2=DUM4*(DCTDAZ*XMN-DCTDAX*ZMN+DCTDBZ*XJK-DCTDBX*ZJK)
      DUM3=DUM4*(DCTDAX*YMN-DCTDAY*XMN+DCTDBX*YJK-DCTDBY*XJK)
      PDOT(I1)=PDOT(I1)+DUM1
      PDOT(I2)=PDOT(I2)+DUM2
      PDOT(I3)=PDOT(I3)+DUM3
      PDOT(L1)=PDOT(L1)-DUM1
      PDOT(L2)=PDOT(L2)-DUM2
      PDOT(L3)=PDOT(L3)-DUM3
      DUM1=DUM4*(DCTDBZ*YIL-DCTDBY*ZIL)
      DUM2=DUM4*(DCTDBX*ZIL-DCTDBZ*XIL)
      DUM3=DUM4*(DCTDBY*XIL-DCTDBX*YIL)
      PDOT(J1)=PDOT(J1)+DUM1
      PDOT(J2)=PDOT(J2)+DUM2
      PDOT(J3)=PDOT(J3)+DUM3
      PDOT(K1)=PDOT(K1)-DUM1
      PDOT(K2)=PDOT(K2)-DUM2
      PDOT(K3)=PDOT(K3)-DUM3
      DUM1=DUM4*(DCTDAZ*YIL-DCTDAY*ZIL)
      DUM2=DUM4*(DCTDAX*ZIL-DCTDAZ*XIL)
      DUM3=DUM4*(DCTDAY*XIL-DCTDAX*YIL)
      PDOT(M1)=PDOT(M1)+DUM1
      PDOT(M2)=PDOT(M2)+DUM2
      PDOT(M3)=PDOT(M3)+DUM3
      PDOT(N1)=PDOT(N1)-DUM1
      PDOT(N2)=PDOT(N2)-DUM2
      PDOT(N3)=PDOT(N3)-DUM3
      RETURN
      END
