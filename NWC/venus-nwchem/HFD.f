      SUBROUTINE HFD(JNL,KNL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE HARTREE-FOCK DIFFUSION POTENTIAL ENERGY
C         AND DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/HFDB/AHFD(100),BHFD(100),RHFD(100),VHFD(100),C6HFD(100),
     *C8HFD(100),C10HFD(100),N17J(100),N17K(100)
      COMMON/FORCES/NATOMS,I3N,NS,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
C
C         DATA FOR THE FIT F(R) FUNCTION OF R. AHLRICHS, R. PENCO 
C         & G. SCOLES, CHEMICAL PHYSICS 19 (1977), 119-130
C
      DATA AAHFD,BBHFD/1.28D0,2.0D0/
C
C         LOOP ON ALL POTENTIAL FUNCTIONS
C
      DO NL=JNL,KNL
C
C         CALCULATE INDICES FOR COORDINATES
C
         J3=3*N17J(NL)
         J2=J3-1
         J1=J2-1
         K3=3*N17K(NL)
         K2=K3-1
         K1=K2-1
C
C        CALCULATE  INDEX FOR R
C
         JK=(N17J(NL)-1)*(2*NATOMS-N17J(NL))/2+N17K(NL)-N17J(NL)
C
C        CALCULATE RELATIVE COORDINATES
C
         T1=Q(K1)-Q(J1)
         T2=Q(K2)-Q(J2)
         T3=Q(K3)-Q(J3)
         R(JK)=SQRT(T1*T1+T2*T2+T3*T3)
C
C         HARTREE-FOCK REPULSION PART
C
         DEHF=AHFD(NL)*EXP(-BHFD(NL)*R(JK))
C
C         F(R) FUNCTION AND DERIVATIVE
C
         XX=AAHFD*RHFD(NL)/R(JK)-1.0D0
         IF (XX.LT.0.0D0) XX=0.0D0
         FR=EXP(-XX**BBHFD)
         DFDR=AAHFD*BBHFD*RHFD(NL)*(XX**(BBHFD-1.0D0))*FR/(R(JK)**3)
C
C         DISPERSION EXPANSION AND DERIVATIVE
C
         R4=1.0D0/R(JK)**4
         R6=1.0D0/R(JK)**6
         CCC=C6HFD(NL)*R6+C8HFD(NL)*R4*R4+C10HFD(NL)*R4*R6
         DDD=-6.0D0*C6HFD(NL)*R4*R4-8.0D0*C8HFD(NL)*R4*R6-
     *   10.0D0*C10HFD(NL)*R6*R6
C
C         POTENTIAL ENERGY INCREMENT
C
         VHFD(NL)=DEHF-CCC*FR
         DUM=BHFD(NL)*DEHF/R(JK)+CCC*DFDR+DDD*FR
C
C        CALCULATE (DV/DQ)'S
C
         TMP=DUM*T1
         PDOT(K1)=PDOT(K1)-TMP
         PDOT(J1)=PDOT(J1)+TMP
         TMP=DUM*T2
         PDOT(K2)=PDOT(K2)-TMP
         PDOT(J2)=PDOT(J2)+TMP
         TMP=DUM*T3
         PDOT(K3)=PDOT(K3)-TMP
         PDOT(J3)=PDOT(J3)+TMP
C
      ENDDO
      RETURN
      END
