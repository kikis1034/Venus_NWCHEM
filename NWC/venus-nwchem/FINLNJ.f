      SUBROUTINE FINLNJ(ENJ,AM,RMIN,RMAX,DH,AN,AJ)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE VIBRATIONAL AND ROTATIONAL QUANTUM NUMBERS FOR
C         A PRODUCT DIATOM
C
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/STRETB/RSZ(ND01),FS(ND01),N1J(ND01),N1K(ND01)
      COMMON/MORSEB/RMZ(ND02),B(ND02),D(ND02),N2J(ND02),N2K(ND02),
     *CM1(ND02),CM2(ND02),CM3(ND02),CM4(ND02),CM5(ND02)
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      DIMENSION QO(6),XGL(NCF),WGL(NCF)
    5 FORMAT(/5X,'WARNING : A PROBLEM WITH THE SELECTED PRODUCT ',
     *'DIATOM HAS OCCURRED'/15X,'THE DIATOM VIBRATIONAL QUANTUM ',
     *'NUMBER CAN NOT BE EVALUATED.'/)
C
C         FIND THE ROTATIONAL QUANTUM NUMBER AJ FROM THE ROTATIONAL
C         ANGULAR MOMENTUM AM IN UNITS OF H-BAR.
C
      AN=0.0D0
      DUM=AM*AM
      AJ=0.5D0*(-1.0D0+SQRT(1.0D0+4.0D0*DUM))
C
C         FIND THE VIBRATIONAL QUANTUM NUMBER AN, USING AN INVERSION
C         OF THE SEMICLASSICAL RYDBERG-KLEIN-REES (RKR) APPROACH.
C
C         INITIALIZE SOME VARIABLES FOR THE DIATOM.
C
      L1=MIN0(L(1),L(2))
      L2=MAX0(L(1),L(2))
      RMASS=W(L1)*W(L2)/(W(L1)+W(L2))
      ENJ=ENJ/C1
      T3=Q(3*L2)-Q(3*L1)
      T2=Q(3*L2-1)-Q(3*L1-1)
      T1=Q(3*L2-2)-Q(3*L1-2)
      RO=SQRT(T1*T1+T2*T2+T3*T3)
      IF (RO.GT.6.0D0) THEN
         WRITE(6,5)
         GOTO 60
      ENDIF
      DO I=1,3
         QO(I)=Q(3*L1-3+I)
         QO(I+3)=Q(3*L2-3+I)
         Q(3*L1-3+I)=(W(L1)*QO(I)+W(L2)*QO(I+3))/(W(L1)+W(L2))
         Q(3*L2-3+I)=(W(L1)*QO(I)+W(L2)*QO(I+3))/(W(L1)+W(L2))
      ENDDO
      Q(3*L1)=Q(3*L1)-0.5D0*RO
      Q(3*L2)=Q(3*L2)+0.5D0*RO
      CALL DVDQ
      CALL ENERGY
      VEFF=V-DH+0.5D0*DUM*C7*C7/(C1*RMASS*RO*RO)
      IF (VEFF.GT.ENJ) THEN
         WRITE(6,5)
         GOTO 60
      ENDIF
C
C         DETERMINE BOUNDARIES OF THE SEMICLASSICAL INTEGRAL
C
   20 Q(3*L2)=Q(3*L2)+0.001D0
      CALL DVDQ
      CALL ENERGY
      RZ=Q(3*L2)-Q(3*L1)
      VEFF=V-DH+0.5D0*DUM*C7*C7/(C1*RMASS*RZ*RZ)
      IF (VEFF.LT.ENJ.AND.RZ.LT.50.0D0) GOTO 20
      RMAX=RZ
      Q(3*L2)=Q(3*L1)+RO
   30 Q(3*L2)=Q(3*L2)-0.001D0
      CALL DVDQ
      CALL ENERGY
      RZ=Q(3*L2)-Q(3*L1)
      VEFF=V-DH+0.5D0*DUM*C7*C7/(C1*RMASS*RZ*RZ)
      IF (VEFF.LT.ENJ) GOTO 30
      RMIN=RZ
C
C         EVALUATE THE SEMICLASSICAL INTEGRAL BY GAUSSIAN QUADRATURE
C
      NGL=50
      CALL GLPAR(RMIN,RMAX,XGL,WGL,NGL)
      ASUM=0.0D0
      DO J=1,NGL
         RZ=XGL(J)
         Q(3*L2)=Q(3*L1)+RZ
         CALL DVDQ
         CALL ENERGY
         VEFF=V-DH+0.5D0*DUM*C7*C7/(C1*RMASS*RZ*RZ)
         IF (ENJ.GT.VEFF) ASUM=ASUM+WGL(J)*SQRT(ENJ-VEFF)
      ENDDO
      DO I=1,3
         Q(3*L1-3+I)=QO(I)
         Q(3*L2-3+I)=QO(I+3)
      ENDDO
      AN=SQRT(8.0D0*C1*RMASS)*ASUM/TWOPI/C7
   60 ENJ=ENJ*C1
      AN=AN-0.5D0
      RETURN
      END
