      SUBROUTINE POTEN(THETAA,PHI,QCM,N)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         ROTATE MOLECULE BY THETAA AND PHI ON ITS CENTER OF MASS AND
C         CALCULATE POTENTIAL ENERGY
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      DIMENSION QCM(3)
C
C          CALCULATE ANGLES
C
      THETH=THETAA-HALFPI
      STHET=SIN(THETH)
      CTHET=COS(THETH)
      SPHI=SIN(PHI)
      CPHI=COS(PHI)
C
C          ROTATE FRAGMENT ABOUT ITS CENTER OF MASS.  FIRST ROTATE BY
C          THETAA ABOUT THE X-AXIS AND THEN BY PHI ABOUT THE Z-AXIS.
C          THETAA = 0-PI, AND PHI = 0-TWOPI.
C          THE REACTION PATH IS ALONG THE Y-AXIS.
C
      DO I=1,N
         J=3*L(I)
         Q(J)=STHET*QQ(J-1)+CTHET*QQ(J)+QCM(3)
         Q(J-1)=-SPHI*QQ(J-2)+CPHI*CTHET*QQ(J-1)-CPHI*STHET*QQ(J)
     *          +QCM(2)
         Q(J-2)=CPHI*QQ(J-2)+SPHI*CTHET*QQ(J-1)-SPHI*STHET*QQ(J)+QCM(1)
      ENDDO
      CALL DVDQ
      CALL ENERGY
      RETURN
      END
