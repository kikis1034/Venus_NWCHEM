      SUBROUTINE RELAX(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE POTENTIAL ENERGY AND DERIVATIVES, ASSOCIATED WITH
C         THE INTERACTION OF A H-ATOM WITH A C-ATOM RADICAL SITE ON A
C         DIAMOND SURFACE. FROM J. CHEM. PHYS. 101, 2476(1994).
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/MORSEB/RMZ(ND02),B(ND02),D(ND02),N2J(ND02),N2K(ND02),
     *CM1(ND02),CM2(ND02),CM3(ND02),CM4(ND02),CM5(ND02)
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/RELAXA/N21I(ND21),N21J(ND21),N21K(ND21),N21L(ND21),
     *N21M(ND21),N21N(ND21),N21O(ND21),N21P(ND21),N21Q(ND21),
     *N21R(ND21),N21S(ND21),N21T(ND21),N21U(ND21),N21V(ND21),
     *PH0D(3,ND21),PH0R(3,ND21),F0D(3,ND21),TH0D(3,ND21),TH0R(3,ND21),
     *FCCC(3,ND21),CC0D(3,ND21),CC0R(3,ND21),BET(3,ND21),DIS(3,ND21),
     *GA0D(6,ND21),GA0R(6,ND21),FCC1(6,ND21),FCC2(3,ND21)
      COMMON/RELAXB/VRELAX(ND21)
      DIMENSION RC(NDA3),DRDQ(4,NDA3),CAN(15),DANDQ(15,NDA3),
     +DS1DQ(NDA3),DSWDQ(NDA3),DSWDQ2(NDA3),DSWDQ3(NDA3),
     +DSWDQ4(NDA3),DUMP(3),DUMPS(3),DUMT(3),DUMTS(3),
     +DUMPP(3),DUMPPS(3),DUMC(3),DUMG(6),DUMGS(6),PH0(3),TH0(3),
     +CC0(4),GA0(6),ANGL(15),IQ(42),IBD(4),IANG(45)
      DATA IANG/1,14,2,1,14,3,1,14,4,2,14,3,2,14,4,3,14,4,14,2,6,14,2,5,
     +  14,3,8,14,3,9,14,4,11,14,4,12,14,2,7,14,3,10,14,4,13/
      DATA AA1/0.819623483D0/,BB1/0.782089410D0/,BB2/0.0831161759D0/
      DATA ASW/0.355275203D0/,BSW/83.2162319D0/,CSW/2.02226904D0/
C
C   INITIALIZE ARRAYS AND POTENTIAL TERMS
C
      DO J=1,NDA3
        DSWDQ(J)=0.0D0
        RC(J)=0.0D0
        DS1DQ(J)=0.0D0
        DO I=1,4
          DRDQ(I,J)=0.0D0
        ENDDO
      ENDDO
C
      VPHI=0.0D0
      VTHETA=0.0D0
      VCC=0.0D0
      VGAM=0.0D0
      VPHIP=0.0D0
C 
C   DEFINE H---C EQUILIBRIUM BOND LENGTH
C
      DO J=1,NM
        IF (N2J(J).EQ.N21I(NL).OR.N2K(J).EQ.N21I(NL)) THEN 
          R0=RMZ(J)
        ENDIF
      END DO
C
C   CALCULATION OF INDICES FOR COORDINATES 
C
      IQ(3)=3*N21I(NL)
      IQ(2)=IQ(3)-1
      IQ(1)=IQ(2)-1
      IQ(6)=3*N21J(NL)
      IQ(5)=IQ(6)-1
      IQ(4)=IQ(5)-1
      IQ(9)=3*N21K(NL)
      IQ(8)=IQ(9)-1
      IQ(7)=IQ(8)-1
      IQ(12)=3*N21L(NL)
      IQ(11)=IQ(12)-1
      IQ(10)=IQ(11)-1
      IQ(15)=3*N21M(NL)
      IQ(14)=IQ(15)-1
      IQ(13)=IQ(14)-1
      IQ(18)=3*N21N(NL)
      IQ(17)=IQ(18)-1
      IQ(16)=IQ(17)-1
      IQ(21)=3*N21O(NL)
      IQ(20)=IQ(21)-1
      IQ(19)=IQ(20)-1
      IQ(24)=3*N21P(NL)
      IQ(23)=IQ(24)-1
      IQ(22)=IQ(23)-1
      IQ(27)=3*N21Q(NL)
      IQ(26)=IQ(27)-1
      IQ(25)=IQ(26)-1
      IQ(30)=3*N21R(NL)
      IQ(29)=IQ(30)-1
      IQ(28)=IQ(29)-1
      IQ(33)=3*N21S(NL)
      IQ(32)=IQ(33)-1
      IQ(31)=IQ(32)-1
      IQ(36)=3*N21T(NL)
      IQ(35)=IQ(36)-1
      IQ(34)=IQ(35)-1
      IQ(39)=3*N21U(NL)
      IQ(38)=IQ(39)-1
      IQ(37)=IQ(38)-1
      IQ(42)=3*N21V(NL)
      IQ(41)=IQ(42)-1
      IQ(40)=IQ(41)-1
C
C   CALCULATION OF INDICES FOR R'S
C
      NUM1=(N21V(NL)-1)*(2*NATOMS-N21V(NL))/2
      IF(N21V(NL).GT.N21I(NL)) THEN
        IBD(1)=(N21I(NL)-1)*(2*NATOMS-N21I(NL))/2+N21V(NL)-
     *          N21I(NL)
      ELSE
        IBD(1)=NUM1+N21I(NL)-N21V(NL)
      ENDIF
      IF(N21V(NL).GT.N21J(NL)) THEN
        IBD(2)=(N21J(NL)-1)*(2*NATOMS-N21J(NL))/2+N21V(NL)-
     *          N21J(NL)
      ELSE
        IBD(2)=NUM1+N21J(NL)-N21V(NL)
      ENDIF
      IF(N21V(NL).GT.N21K(NL)) THEN
        IBD(3)=(N21K(NL)-1)*(2*NATOMS-N21K(NL))/2+N21V(NL)-
     *          N21K(NL)
      ELSE
        IBD(3)=NUM1+N21K(NL)-N21V(NL)
      ENDIF
      IF(N21V(NL).GT.N21L(NL)) THEN
        IBD(4)=(N21L(NL)-1)*(2*NATOMS-N21L(NL))/2+N21V(NL)-
     *          N21L(NL)
      ELSE
        IBD(4)=NUM1+N21L(NL)-N21V(NL)
      ENDIF
C
C  CALCULATION OF RELATIVE COORDINATES
C
      DO I=1,4
        J=3*I-2
        RC(IQ(J))=Q(IQ(J))-Q(IQ(40))
        RC(IQ(J+1))=Q(IQ(J+1))-Q(IQ(41))
        RC(IQ(J+2))=Q(IQ(J+2))-Q(IQ(42))
      ENDDO
C
C DEFINE R
C
      DO I=1,4
        J=3*I-2
        R(IBD(I))=SQRT(RC(IQ(J))*RC(IQ(J))+RC(IQ(J+1))*
     +    RC(IQ(J+1))+RC(IQ(J+2))*RC(IQ(J+2)))
      ENDDO
C
C  DERIVATIVE OF R WITH RESPECT TO Q
C
      DO I=1,4
        J=3*I-2
        DO K=0,2
          DRDQ(I,IQ(J+K))=RC(IQ(J+K))/R(IBD(I))
          DRDQ(I,IQ(40+K))=-RC(IQ(J+K))/R(IBD(I))
        ENDDO
      ENDDO
C
C  CALCULATION OF 15 ANGLES AND THEIR DERIVATIVE
C  WITH RESPECT TO Q
C
      DO I=1,15
        DO J=1,NDA3
          DANDQ(I,J)=0.0D0
        ENDDO
      ENDDO
C
      I=0
      DO M=1,43,3
        I=I+1
        J=3*IANG(M)
        K=3*IANG(M+1)
        L=3*IANG(M+2)
        I3=IQ(J)
        J3=IQ(K)
        K3=IQ(L) 
        I2=I3-1
        I1=I2-1
        J2=J3-1
        J1=J2-1
        K2=K3-1
        K1=K2-1
        RCI1=Q(I1)-Q(J1)
        RCI2=Q(I2)-Q(J2)
        RCI3=Q(I3)-Q(J3)
        RCK1=Q(K1)-Q(J1)
        RCK2=Q(K2)-Q(J2)
        RCK3=Q(K3)-Q(J3)
        DUM1=SQRT(RCI1*RCI1+RCI2*RCI2+RCI3*RCI3)
        DUM2=SQRT(RCK1*RCK1+RCK2*RCK2+RCK3*RCK3)
        CAN(I)=(RCI1*RCK1+RCI2*RCK2+RCI3*RCK3)/(DUM1*DUM2)
        ANGL(I)=ACOS(CAN(I))
        DUM3=-SQRT(1.0D0-CAN(I)*CAN(I))
        TD123=1.D0/(DUM2*DUM1*DUM1*DUM3)
        DANDQ(I,I1)=(RCK1*DUM1-CAN(I)*DUM2*RCI1)*TD123
        DANDQ(I,I2)=(RCK2*DUM1-CAN(I)*DUM2*RCI2)*TD123
        DANDQ(I,I3)=(RCK3*DUM1-CAN(I)*DUM2*RCI3)*TD123
        TD123=1.D0/(DUM1*DUM2*DUM2*DUM3)
        DANDQ(I,K1)=(RCI1*DUM2-CAN(I)*DUM1*RCK1)*TD123
        DANDQ(I,K2)=(RCI2*DUM2-CAN(I)*DUM1*RCK2)*TD123
        DANDQ(I,K3)=(RCI3*DUM2-CAN(I)*DUM1*RCK3)*TD123
        DANDQ(I,J1)=-DANDQ(I,I1)-DANDQ(I,K1)
        DANDQ(I,J2)=-DANDQ(I,I2)-DANDQ(I,K2)
        DANDQ(I,J3)=-DANDQ(I,I3)-DANDQ(I,K3)
      ENDDO
C
C  VALUES AND DERIVATIVES OF SWITCHING FUNCTIONS
C  WITH RESPECT TO R(IBD(1))
C
      DUMS1=R(IBD(1))-R0
      IF(DUMS1.GE.0.0D0) THEN
        DUMSA=AA1*EXP(-BB1*DUMS1*DUMS1)
        DUMSB=(1.0D0-AA1)*EXP(-BB2*(DUMS1**7.0D0))
        S1=DUMSA+DUMSB
        DS1DR1=-2.0D0*BB1*DUMS1*DUMSA-7.0D0*BB2*(DUMS1**6.0D0)*
     *         DUMSB
        DO J=1,42
          DS1DQ(IQ(J))=DS1DR1*DRDQ(1,IQ(J))
        ENDDO
      ELSE
        S1=1.0D0
      ENDIF
C
      DUMSA=ASW*EXP(-BSW*DUMS1*DUMS1)
      DUMSB=(1.0D0-ASW)*EXP(-CSW*DUMS1*DUMS1)
      SW=DUMSA+DUMSB
      DSWDR1=-2.0D0*BSW*DUMS1*DUMSA-2.0D0*CSW*DUMS1*DUMSB
C
C  DERIVATIVE OF SWITCHING FUNCTIONS WITH RESPECT TO Q
C
      DO J=1,42
        DSWDQ(IQ(J))=DSWDR1*DRDQ(1,IQ(J))
      ENDDO
C
C  CALCULATION OF X0,X-X0,(X-X0)**2,V(X) FOR EACH POTENTIAL TERM
C
C                  VPHI,VTHETA,VPHIP,VCC,VGAM
C
      DO I=1,3
C
        PH0(I)=PH0R(I,NL)+(PH0D(I,NL)-PH0R(I,NL))*SW
        DUMP(I)=ANGL(I)-PH0(I)
        DUMPS(I)=DUMP(I)*DUMP(I)
        VPHI=VPHI+0.5D0*F0D(I,NL)*S1*DUMPS(I)
C
        TH0(I)=TH0R(I,NL)+(TH0D(I,NL)-TH0R(I,NL))*SW
        DUMT(I)=ANGL(I+3)-TH0(I)
        DUMTS(I)=DUMT(I)*DUMT(I)
        VTHETA=VTHETA+0.5D0*FCCC(I,NL)*DUMTS(I)
C
        DUMPP(I)=ANGL(I+12)-PH0(I)
        DUMPPS(I)=DUMPP(I)*DUMPP(I)
        VPHIP=VPHIP+0.5D0*FCC2(I,NL)*DUMPPS(I)
C
        CC0(I)=CC0R(I,NL)+(CC0D(I,NL)-CC0R(I,NL))*SW
        DUMC(I)=EXP(-BET(I,NL)*(R(IBD(I+1))-CC0(I)))
        VCC=VCC+DIS(I,NL)*(1.0D0-DUMC(I))*(1.0D0-DUMC(I))
      ENDDO
C
      DO I=1,6
        GA0(I)=GA0R(I,NL)+(GA0D(I,NL)-GA0R(I,NL))*SW
        DUMG(I)=ANGL(I+6)-GA0(I)
        DUMGS(I)=DUMG(I)*DUMG(I)
        VGAM=VGAM+0.5D0*FCC1(I,NL)*DUMGS(I)
      ENDDO
C
C  CALCULATION OF PDOT FOR EACH POTENTIAL TERM
C
C           VPHI,VTHETA,VPHIP,VCC,VGAM
C
      DO J=1,42
        DO I=1,3
          PDOT(IQ(J))=PDOT(IQ(J))+0.5D0*DUMPS(I)*F0D(I,NL)*
     +      DS1DQ(IQ(J))+F0D(I,NL)*S1*DUMP(I)*(DANDQ(I,IQ(J))-
     +      (PH0D(I,NL)-PH0R(I,NL))*DSWDQ(IQ(J)))
C
          PDOT(IQ(J))=PDOT(IQ(J))+
     +      FCCC(I,NL)*DUMT(I)*(DANDQ(I+3,IQ(J))-
     +      (TH0D(I,NL)-TH0R(I,NL))*DSWDQ(IQ(J)))
C
          PDOT(IQ(J))=PDOT(IQ(J))+
     +      FCC2(I,NL)*DUMPP(I)*(DANDQ(I+12,IQ(J))-
     +      (PH0D(I,NL)-PH0R(I,NL))*DSWDQ(IQ(J)))
C
          PDOT(IQ(J))=PDOT(IQ(J))+
     +      2.0D0*BET(I,NL)*DIS(I,NL)*DUMC(I)*(1.0D0-DUMC(I))*
     +      (DRDQ(I+1,IQ(J))-(CC0D(I,NL)-CC0R(I,NL))*DSWDQ(IQ(J)))
        ENDDO
      ENDDO
C
      DO J=1,42
        DO I=1,6
          PDOT(IQ(J))=PDOT(IQ(J))+
     +      FCC1(I,NL)*DUMG(I)*(DANDQ(I+6,IQ(J))-
     +      (GA0D(I,NL)-GA0R(I,NL))*DSWDQ(IQ(J)))
        ENDDO
      ENDDO
C
C  DEFINE VRELAX
C
      VRELAX(NL)=VPHI+VTHETA+VCC+VGAM+VPHIP
      RETURN
      END
