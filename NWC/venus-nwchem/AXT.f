      SUBROUTINE AXT(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE AXILROD-TELLER POTENTIAL ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/AXTB/ZAXT(300),VAXT(300),N14I(300),N14J(300),N14K(300)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      DIMENSION TIJ(3),TIK(3),TJK(3),DVDTIJ(3),DVDTIK(3),DVDTJK(3)
C
C         CALCULATE INDICES FOR COORDINATES
C
      I3=3*N14I(NL)
      I2=I3-1
      I1=I2-1
      J3=3*N14J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N14K(NL)
      K2=K3-1
      K1=K2-1
C
C         CALCULATE INDEX FOR R
C
      IJ=(N14I(NL)-1)*(2*NATOMS-N14I(NL))/2+N14J(NL)-N14I(NL)
      IK=(N14I(NL)-1)*(2*NATOMS-N14I(NL))/2+N14K(NL)-N14I(NL)
      JK=(N14J(NL)-1)*(2*NATOMS-N14J(NL))/2+N14K(NL)-N14J(NL)
C
C         CALCULATE RELATIVE COORDINATES
C
      TIJ(1)=Q(J1)-Q(I1)
      TIJ(2)=Q(J2)-Q(I2)
      TIJ(3)=Q(J3)-Q(I3)
      TIK(1)=Q(K1)-Q(I1)
      TIK(2)=Q(K2)-Q(I2)
      TIK(3)=Q(K3)-Q(I3)
      TJK(1)=Q(K1)-Q(J1)
      TJK(2)=Q(K2)-Q(J2)
      TJK(3)=Q(K3)-Q(J3)
      R(IJ)=SQRT(TIJ(1)*TIJ(1)+TIJ(2)*TIJ(2)+TIJ(3)*TIJ(3))
      R(IK)=SQRT(TIK(1)*TIK(1)+TIK(2)*TIK(2)+TIK(3)*TIK(3))
      R(JK)=SQRT(TJK(1)*TJK(1)+TJK(2)*TJK(2)+TJK(3)*TJK(3))
C
C         CALCULATE COSINE OF ANGLES
C
      CI=TIJ(1)*TIK(1)+TIJ(2)*TIK(2)+TIJ(3)*TIK(3)
      CJ=-TIJ(1)*TJK(1)-TIJ(2)*TJK(2)-TIJ(3)*TJK(3)
      CK=TIK(1)*TJK(1)+TIK(2)*TJK(2)+TIK(3)*TJK(3)
      COSI=CI/R(IJ)/R(IK)
      COSJ=CJ/R(IJ)/R(JK)
      COSK=CK/R(IK)/R(JK)
C
C         CALCULATE POTENTIAL INCREMENT
C
      V1=ZAXT(NL)/(R(IJ)*R(IK)*R(JK))**3
      V2=3.D0*ZAXT(NL)/(R(IJ)*R(IK)*R(JK))**5
      VTEMP=V2*CI*CJ*CK
      VAXT(NL)=V1+VTEMP
C
C         CALCULATE (DV/DQ)'S
C
      DUM=-3.D0*V1-5.D0*VTEMP
      DUMIJ=DUM/R(IJ)/R(IJ)
      DUMIK=DUM/R(IK)/R(IK)
      DUMJK=DUM/R(JK)/R(JK)
      DO I=1,3
         DVDTIJ(I)=V2*CK*(TIK(I)*CJ-TJK(I)*CI)+DUMIJ*TIJ(I)
         DVDTIK(I)=V2*CJ*(TIJ(I)*CK+TJK(I)*CI)+DUMIK*TIK(I)
         DVDTJK(I)=V2*CI*(TIK(I)*CJ-TIJ(I)*CK)+DUMJK*TJK(I)
      ENDDO
      PDOT(I1)=PDOT(I1)-DVDTIJ(1)-DVDTIK(1)
      PDOT(I2)=PDOT(I2)-DVDTIJ(2)-DVDTIK(2)
      PDOT(I3)=PDOT(I3)-DVDTIJ(3)-DVDTIK(3)
      PDOT(J1)=PDOT(J1)+DVDTIJ(1)-DVDTJK(1)
      PDOT(J2)=PDOT(J2)+DVDTIJ(2)-DVDTJK(2)
      PDOT(J3)=PDOT(J3)+DVDTIJ(3)-DVDTJK(3)
      PDOT(K1)=PDOT(K1)+DVDTIK(1)+DVDTJK(1)
      PDOT(K2)=PDOT(K2)+DVDTIK(2)+DVDTJK(2)
      PDOT(K3)=PDOT(K3)+DVDTIK(3)+DVDTJK(3)
      RETURN
      END
