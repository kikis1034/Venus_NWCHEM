      SUBROUTINE INITQP(WW,A,C,AM,WT,EINT,EROTS,AI,EROT,N,NM)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         INITIALIZE COORDINATES AND MOMENTA FROM NORMAL MODE
C         PARAMETERS (FREQUENCY, AMPLITUDE,...)
C
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/SELTB/QZ(NDA3),NSELT,NSFLAG,NACTA,NACTB,NLINA,NLINB,NSURF
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/FINALB/EROTA,EROTB,EA(3),EB(3),AMA(4),AMB(4),AN,AJ,BN,BJ,
     *OAM(4),EREL,ERELSQ,BF,SDA,SDB,DELH(NDP),ANG(NDG),NFINAL
      COMMON/LMODEB/ENON,EDELTA,RWANT,PWANT,NEXM,NLEV,JFLAG
      COMMON/FORCES/NATOMS,I3N,NST,NMM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/SADDLE/EBAR,TBAR,EZERO,NBAR 
      DIMENSION WW(NDA3),A(NDA3),C(NDA3yf,NDA3yf),QCM(3),VCM(3),AM(4),
     *AI(3),COOR(NDA3),DCOOR(NDA3)
    5 FORMAT('  A-B INTERACTION ENERGY WHEN ENTERING INITQP =',
     *1PE18.9,' KCAL/MOL'/)
   15 FORMAT(15X,'INTERNAL ENERGY =',1PE18.9,' KCAL/MOL')
   45 FORMAT(/,15X,'ONLY KINETIC ENERGY FOR FIRST ',I3,' MODES')
C
      ESEL=EINT
C
C         CALCULATE THE TOTAL ENERGY, WHICH IS THE REFERENCE ENERGY
C         (EZERO) WITH RESPECT TO ADDING EINT.
C
      CALL DVDQ
      CALL ENERGY
      EZERO=H
      WRITE(6,5)EZERO
C
C         SET COUNTER FOR NUMBER OF SCALING ATTEMPTS
C
      NSCALE=0
C
C         SET IFLAG WHICH IS USED FOR LOCAL MODE EXCITATION BETWEEN
C         ATOMS NONI AND NONJ
C
      IFLAG=0
      IF (NACTA.EQ.4) CALL LMODE(1,ENU,EDELTU,ENL,EDELTL)
C
C         STORE THE ANGULAR MOMENTUM VECTOR FROM SELECT
C
      DUM1=AM(1)
      DUM2=AM(2)
      DUM3=AM(3)
C
C         CALCULATE NORMAL MODE COORDINATES AND VELOCITIES
C
C         SET COOR(I) AND DCOOR(I) FOR MODES WHICH ONLY RECEIVE
C         KINETIC ENERGY SPECIFIED BY NUMP
C
      NUMP=0
  48  IF (NUMP .GE. 1) THEN
         DO I = 1,NUMP
            COOR(I)=0.D0
            DCOOR(I)=-WW(I)*A(I)
         ENDDO
      ENDIF
C
      DO I=1+NUMP,NM
         RAND=RAND0(ISEED)
         DUM=TWOPI*RAND
         COOR(I)=A(I)*COS(DUM)
         DCOOR(I)=-WW(I)*A(I)*SIN(DUM)
      ENDDO
C
C         TRANSFORM FROM NORMAL MODE TO CARTESIAN COORDINATES AND VELOCIT
C
      DO II=1,N
         DO K=1,3
            JJ=3*II+1-K
            J=3*L(II)+1-K
            Q(J)=0.0D0
            P(J)=0.0D0
            DO I=1,NM
               Q(J)=Q(J)+C(JJ,I)*COOR(I)
               P(J)=P(J)+C(JJ,I)*DCOOR(I)
            ENDDO
            P(J)=P(J)*W(L(II))
            Q(J)=Q(J)+QZ(J)
         ENDDO
      ENDDO
C
C         CALCULATE CENTER OF MASS COORDINATES QQ AND MOMENTA PP
C
   50 CALL CENMAS(WT,QCM,VCM,N)
C
C         MOVE PP ARRAY TO P ARRAY AND QQ ARRAY TO Q ARRAY
C
      DO I=1,N
         J=3*L(I)+1
         DO K=1,3
            Q(J-K)=QQ(J-K)
            P(J-K)=PP(J-K)
         ENDDO
      ENDDO
C
C         ADD ANGULAR MOMENTUM VECTOR FROM SELECT TO THE MOLECULE.
C         CALCULATE THE REQUIRED ANGULAR VELOCITY AND ADD IT TO THE
C         MOLECULE.
C
      CALL ROTN(AM,EROT,N)
      AM(1)=DUM1-AM(1)
      AM(2)=DUM2-AM(2)
      AM(3)=DUM3-AM(3)
      NAM=1
      CALL ROTN(AM,EROT,N)
      NAM=0
      WX=-WX
      WY=-WY
      WZ=-WZ
      CALL ANGVEL(N)
C
C         SCALE COORDINATES AND MOMENTA TO FIT THE TOTAL ENERGY
C         THE INITIAL CONDITION IS ACCEPTED IF THE CALCULATED AND DESIRED
C         ENERGY AGREE TO WITHIN 0.1 PER-CENT.
C
C         EINT IS THE SUM OF THE SELECTED INTERNAL VIBRATIONAL AND
C         ROTATIONAL ENERGIES.  DO NOT SCALE IF THE SELECTED VIBRATIONAL
C         ENERGY IS ZERO.
C
      IF (IFLAG.NE.1) THEN
         DDD=EINT-EROTS
         IF (DDD.NE.0.0D0) THEN
            CALL DVDQ
            CALL ENERGY
            ESEL=H-EZERO
            SDUM=ABS(EINT-ESEL)/EINT
C
C         TEST TO SEE IF CALCULATED ENERGY IS OUT OF RANGE FOR ACCURATE
C         SCALING. IF SO, ONLY ADD KINETIC ENERGY TO AN ADDITIONAL MODE
C
            IF (SDUM.GE.0.1D0 .AND. NUMP.LE.NM) THEN
               NUMP=NUMP+1
               GOTO 48
            ENDIF
            WRITE(6,15)ESEL
            IF (SDUM.GE.0.001D0) THEN
               NSCALE=NSCALE+1
               IF (NSCALE.GT.50) STOP
               SDUM=SQRT(EINT/ESEL)
               DO I=1,N
                  J=3*L(I)+1
                  DO K=1,3
                     P(J-K)=P(J-K)*SDUM
                     Q(J-K)=(Q(J-K)-QZ(J-K))*SDUM+QZ(J-K)
                  ENDDO
               ENDDO
               GOTO 50
            ENDIF
            IF (NUMP.GT.0) WRITE(6,45)NUMP
         ENDIF
         IF (NACTA.NE.4) GOTO 120
      ENDIF
C
C         CHOOSE CONDITIONS FOR LOCAL MODE(NACT=4)
C
      CALL LMEXCT
      IFLAG=1
      IF (JFLAG.EQ.0) GOTO 50
C
C         CALCULATE THE TOTAL ENERGY
C
      CALL DVDQ
      CALL ENERGY
      ESEL=H-EZERO
C
C         RANDOMLY ROTATE THE MOLECULE ABOUT ITS CENTER OF MASS
C         BY EULER'S ANGLES.
C         CENTER OF MASS COORDINATES QQ AND MOMENTA PP ARE PASSED FROM
C         SUBROUTINES CENMAS AND ANGVEL THROUGH COMMON BLOCK WASTE.
C
  120 CONTINUE
      IF ((N.NE.NATOMS).AND.(NSURF.EQ.0)) CALL VENUS_ROTATE(N)
C
C         CALCULATE THE ROTATIONAL ENERGY
C
      CALL ROTN(AM,EROT,N)
C
      EINT=ESEL
C
      RETURN
      END
