      SUBROUTINE LMEXCT
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE ENERGY IN (MORSE OSCILLATOR) LOCAL MODE.
C         ACCEPT CONDITION IF ENERGY AGREES WITH ENON FROM SUBROUTINE
C         LMODE TO WITHIN 0.05%
C
      COMMON/LMODEB/ENON,EDELTA,RWANT,PWANT,NEXM,NLEV,JFLAG
      COMMON/MORSEB/RMZ(ND02),B(ND02),D(ND02),N2J(ND02),N2K(ND02),
     *CM1(ND02),CM2(ND02),CM3(ND02),CM4(ND02),CM5(ND02)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
 9997 FORMAT(' CH EXCITATION** RCH=',F10.4,' VCH=',F10.4)
 9998 FORMAT(' CH EXCITATION** TCH=',F10.4)
 9999 FORMAT(' CH EXCITATION** ECH=',F10.4)
      CALL EBOND(ECH,TCH,R,NEXM)
      VCH=ECH-TCH
      JFLAG=0
      IF(ABS(ECH-ENON)/(ENON*c1).LE.0.0005)THEN
         WRITE(6,9997)R,VCH
         WRITE(6,9998)TCH
         WRITE(6,9999)ECH
         JFLAG=1
      ELSE
C
C         CHOOSE MORSE OSCILLATOR COORDINATES AND
C         MOMENTA RANDOMLY AS DESCRIBED IN
C         Ref.: J. CHEM. PHYS.,63,2214(1975)
C
         IATOMJ=N2J(NEXM)
         IATOMK=N2K(NEXM)
         IJZ=3*IATOMJ
         IJY=IJZ-1
         IJX=IJY-1
         IKZ=3*IATOMK
         IKY=IKZ-1
         IKX=IKY-1
         QX=Q(IJX)-Q(IKX)
         QY=Q(IJY)-Q(IKY)
         QZ=Q(IJZ)-Q(IKZ)
         RX=QX/R
         RY=QY/R
         RZ=QZ/R
         DX=(RWANT-R)*RX
         DY=(RWANT-R)*RY
         DZ=(RWANT-R)*RZ
         Q(IJX)=Q(IJX)+DX
         Q(IJY)=Q(IJY)+DY
         Q(IJZ)=Q(IJZ)+DZ
C
C         ADD APPROPRIATE KINETIC ENERGY TO THE CH
C         LOCAL MODE SO THAT ENON=ECH
C
         PXWANT=PWANT*RX
         PYWANT=PWANT*RY
         PZWANT=PWANT*RZ
         WJK=W(IATOMJ)+W(IATOMK)
         WJ=W(IATOMJ)/WJK
         WK=W(IATOMK)/WJK
         PX=WK*P(IJX)-WJ*P(IKX)
         PY=WK*P(IJY)-WJ*P(IKY)
         PZ=WK*P(IJZ)-WJ*P(IKZ)
         DELPX=(PXWANT-PX)/WK
         DELPY=(PYWANT-PY)/WK
         DELPZ=(PZWANT-PZ)/WK
         P(IJX)=P(IJX)+DELPX
         P(IJY)=P(IJY)+DELPY
         P(IJZ)=P(IJZ)+DELPZ
      ENDIF
      RETURN
      END
