      SUBROUTINE GINROT(AI,SYM,VZ,WT,Q,N,K)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         DETERMINE CLASSICAL HINDERED ROTOR PARTITION FUNCTION BY
C         10-POINT GAUSS QUADRATURE.
C
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/GPATHB/WM(NDA3),TEMP(NDP),AI1D(5),AAI(2),BBI(2),SYMM(5),
     *SYMA,SYMB,GTEMP(NDP),NFLAG(NDP),N1DR,N2DR
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      DIMENSION X(NCF),W(NCF),AI(2),QCM(3),VCM(3),VTP(10,10)
C
C         NP IS THE NUMBER OF INTEGRATION INTERVALS FOR PHI MOMENTUM (PPHI)
C         LIMIT FOR PPHI INTEGRATION IS CHOSEN SO THAT PPHI**2/2IKT IS 
C         10.0 FOR PPHI=PLIM.  WITH NP=2 THE PARTITION FUNCTION ERROR SHOULD
C         BE LESS THAN 0.1%.
C
C         MOMENT OF INERTIA AI(1) IS FOR THE THETA ROTATION.
C         MOMENT OF INERTIA AI(2) IS FOR THE PHI ROTATION.
C
      CALL GLPAR(-1.0D0,1.0D0,X,W,10)
      NP=2
      PLIM=SQRT(10.0D0*2.0D0*AI(2)*C5*TEMP(K))
      BETA=1.0D0/C5/TEMP(K)
      PDEL=PLIM/DBLE(NP)
C
C         CALCULATE CENTER OF MASS COORDINATES FOR THE FRAGMENT
C
      CALL CENMAS(WT,QCM,VCM,N)
C
C         CALCULATE POTENTIAL ENERGY FOR THETA AND PHI ANGLES
C
      IF (K.LE.1.OR.N2DR.NE.1) THEN
         DO J=1,10
            THETA=HALFPI*(1.0D0+X(J))
            DO L=1,10
               PHI=PI*(1.0D0+X(L))
               CALL POTEN(THETA,PHI,QCM,N)
               VTP(J,L)=V
            ENDDO
         ENDDO
      ENDIF
C
      Q=0.0D0
      DO II=1,NP
         PMAX=PDEL*DBLE(II)
         PMIN=PMAX-PDEL
         DO I=1,10
            PPHI=(PMAX-PMIN)/2.0D0*X(I)+(PMAX+PMIN)/2.0D0
            FPHI=0.0D0
            DO J=1,10
               THETA=HALFPI*(1.0D0+X(J))
               FTHETA=0.0D0
               DO L=1,10
                  VIR=(VTP(J,L)-VZ)*C1
                  FTHETA=FTHETA+EXP(-BETA*VIR)*W(L)
               ENDDO
               FTHETA=FTHETA*PI
               FPHI=FPHI+FTHETA*EXP(-BETA*PPHI**2
     *              /(2.D0*AI(2)*SIN(THETA)**2))*W(J)
            ENDDO
            FPHI=FPHI*HALFPI
            Q=Q+FPHI*W(I)
         ENDDO
      ENDDO
      Q=Q*PDEL/2.0D0
      Q=Q*0.143545D0*SQRT(AI(1)*TEMP(K))*2.0D0/SYM
      WRITE(6,53)Q
   53 FORMAT(32X,'INTERNAL ROTATION PARTITION FUNCTION=',1PE12.5)
      RETURN
      END
