      SUBROUTINE VENUS_ROTATE(N)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         RANDOMLY ROTATE A MOLECULE ABOUT ITS CENTER OF MASS BY
C         EULER'S ANGLES.
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/WASTE/QQ(NDA3),PP(NDA3),WX,WY,WZ,L(NDA),NAM
      COMMON/SELTB/QZ(NDA3),NSELT,NSFLAG,NACTA,NACTB,NLINA,NLINB,NSURF
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
C
C         CENTER OF MASS COORDINATES(QQ) AND MOMENTA(PP) ARE PASSED FROM
C         SUBROUTINES CENMAS AND ANGVEL THROUGH COMMON BLOCK WASTE.
C
      RAND=RAND0(ISEED)
      PHI=TWOPI*RAND
      RAND=RAND0(ISEED)
      CSTHTA=2.0D0*RAND-1.0D0
      RAND=RAND0(ISEED)
      CHI=TWOPI*RAND
      THTA=ACOS(CSTHTA)
      SNTHTA=SIN(THTA)
      SNPHI=SIN(PHI)
      CSPHI=COS(PHI)
      SNCHI=SIN(CHI)
      CSCHI=COS(CHI)
      RXX=CSTHTA*CSPHI*CSCHI-SNPHI*SNCHI
      RXY=-CSTHTA*CSPHI*SNCHI-SNPHI*CSCHI
      RXZ=SNTHTA*CSPHI
      RYX=CSTHTA*SNPHI*CSCHI+CSPHI*SNCHI
      RYY=-CSTHTA*SNPHI*SNCHI+CSPHI*CSCHI
      RYZ=SNTHTA*SNPHI
      RZX=-SNTHTA*CSCHI
      RZY=SNTHTA*SNCHI
      RZZ=CSTHTA
      DO I=1,N
      J=3*L(I)
         Q(J-2)=QQ(J-2)*RXX+QQ(J-1)*RXY+QQ(J)*RXZ
         Q(J-1)=QQ(J-2)*RYX+QQ(J-1)*RYY+QQ(J)*RYZ
         Q(J)=QQ(J-2)*RZX+QQ(J-1)*RZY+QQ(J)*RZZ
         QQ(J-2)=Q(J-2)
         QQ(J-1)=Q(J-1)
         QQ(J)=Q(J) 
         P(J-2)=PP(J-2)*RXX+PP(J-1)*RXY+PP(J)*RXZ
         P(J-1)=PP(J-2)*RYX+PP(J-1)*RYY+PP(J)*RYZ
         P(J)=PP(J-2)*RZX+PP(J-1)*RZY+PP(J)*RZZ
         PP(J-2)=P(J-2)
         PP(J-1)=P(J-1)
         PP(J)=P(J) 
      ENDDO
      RETURN
      END
