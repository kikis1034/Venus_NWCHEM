      SUBROUTINE VRR(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE NON DIAGONAL STRETCH-STRETCH POTENTIAL
C         ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/VRRB/FKRRZ(ND10),FKRR(ND10),CIJ(ND10),CKL(ND10),
     *            RIJ0(ND10),RKL0(ND10),N10I(ND10),N10J(ND10),
     *            N10K(ND10),N10L(ND10)
C
C         R-R COUPLING  BOND RIJ TO RKL
C         INDEX REQUIRMENTS   I < J   AND   K < L
C         EACH DISTANCE MUST ALREADY BE COMPUTED
C
C         COMPUTE X, Y, Z INDECIES
C
      MI=N10I(NL)
      MJ=N10J(NL)
      MK=N10K(NL)
      ML=N10L(NL)
      IZ=MI*3
      JZ=MJ*3
      KZ=MK*3
      LZ=ML*3
      IY=IZ-1
      JY=JZ-1
      KY=KZ-1
      LY=LZ-1
      IX=IY-1
      JX=JY-1
      KX=KY-1
      LX=LY-1
C
C         INDEX FOR RELATIVE R'S
C
      IJ=(MI-1)*(2*NATOMS-MI)/2+MJ-MI
      KL=(MK-1)*(2*NATOMS-MK)/2+ML-MK
C
C         RECOMPUTE THE RELATIVE VECTOR (WASN'T SAVED)
C         AND COMPUTE SOME QUANTITIES
C
      X1=Q(IX)-Q(JX)
      Y1=Q(IY)-Q(JY)
      Z1=Q(IZ)-Q(JZ)
      X2=Q(KX)-Q(LX)
      Y2=Q(KY)-Q(LY)
      Z2=Q(KZ)-Q(LZ)
      R1=R(IJ)-RIJ0(NL)
      R2=R(KL)-RKL0(NL)
C
C         CALCULATE THE SWITCHING FUNCTIONS
C         AND THE FORCE CONSTANT
C
      SR1=0.0D0
      DUM1=CIJ(NL)*R1**2
      IF (DUM1.LE.85.0D0) THEN 
         SR1=1.0D0
         IF (CIJ(NL).GT.0.0D0.AND.R1.GT.0.0D0) SR1=EXP(-DUM1)
      ENDIF
      SR2=0.0D0
      DUM2=CKL(NL)*R2**2
C
      IF (DUM2.LE.85.0D0) THEN
         SR2=1.0D0
         IF (CKL(NL).GT.0.0D0.AND.R2.GT.0.0D0) SR2=EXP(-DUM2)
      ENDIF
      FKRR(NL)=SR1*SR2*FKRRZ(NL)
C
C         CALCULATE   DV/DQ
C
      DUM=FKRR(NL)*R2/R(IJ)
      DUMX=DUM*X1
      DUMY=DUM*Y1
      DUMZ=DUM*Z1
      PDOT(IX) = PDOT(IX) + DUMX
      PDOT(IY) = PDOT(IY) + DUMY
      PDOT(IZ) = PDOT(IZ) + DUMZ
      PDOT(JX) = PDOT(JX) - DUMX
      PDOT(JY) = PDOT(JY) - DUMY
      PDOT(JZ) = PDOT(JZ) - DUMZ
      IF (CIJ(NL).GE.0.0D0.AND.R1.GE.0.0D0) THEN
         DUM11=FKRR(NL)*R1*R2/R(IJ)
         DUMXX=-2.0D0*CIJ(NL)*R1*X1
         DUMYY=-2.0D0*CIJ(NL)*R1*Y1
         DUMZZ=-2.0D0*CIJ(NL)*R1*Z1
         PDOT(IX) = PDOT(IX) + DUM11*DUMXX
         PDOT(IY) = PDOT(IY) + DUM11*DUMYY
         PDOT(IZ) = PDOT(IZ) + DUM11*DUMZZ
         PDOT(JX) = PDOT(JX) - DUM11*DUMXX
         PDOT(JY) = PDOT(JY) - DUM11*DUMYY
         PDOT(JZ) = PDOT(JZ) - DUM11*DUMZZ
      ENDIF
      DUM=FKRR(NL)*R1/R(KL)
      DUMX=DUM*X2
      DUMY=DUM*Y2
      DUMZ=DUM*Z2
      PDOT(KX) = PDOT(KX) + DUMX
      PDOT(KY) = PDOT(KY) + DUMY
      PDOT(KZ) = PDOT(KZ) + DUMZ
      PDOT(LX) = PDOT(LX) - DUMX
      PDOT(LY) = PDOT(LY) - DUMY
      PDOT(LZ) = PDOT(LZ) - DUMZ
      IF (CKL(NL).GE.0.0D0.AND.R2.GE.0.0D0) THEN
         DUM22=FKRR(NL)*R1*R2/R(KL)
         DUMXX=-2.0D0*CKL(NL)*R2*X2
         DUMYY=-2.0D0*CKL(NL)*R2*Y2
         DUMZZ=-2.0D0*CKL(NL)*R2*Z2
         PDOT(KX) = PDOT(KX) + DUM22*DUMXX
         PDOT(KY) = PDOT(KY) + DUM22*DUMYY
         PDOT(KZ) = PDOT(KZ) + DUM22*DUMZZ
         PDOT(LX) = PDOT(LX) - DUM22*DUMXX
         PDOT(LY) = PDOT(LY) - DUM22*DUMYY
         PDOT(LZ) = PDOT(LZ) - DUM22*DUMZZ
      ENDIF
      RETURN
      END
