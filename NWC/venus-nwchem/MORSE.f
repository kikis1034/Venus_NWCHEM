      SUBROUTINE MORSE(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE MORSE POTENTIAL ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/MORSEB/RMZ(ND02),B(ND02),D(ND02),N2J(ND02),N2K(ND02),
     *CM1(ND02),CM2(ND02),CM3(ND02),CM4(ND02),CM5(ND02)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
C
C         CALCULATE INDICES FOR COORDINATES
C
      J3=3*N2J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N2K(NL)
      K2=K3-1
      K1=K2-1
C
C         CALCULATE  INDEX FOR R
C
      JK=(N2J(NL)-1)*(2*NATOMS-N2J(NL))/2+N2K(NL)-N2J(NL)
C
C         CALCULATE RELATIVE COORDINATES AND R
C
      T1=Q(K1)-Q(J1)
      T2=Q(K2)-Q(J2)
      T3=Q(K3)-Q(J3)
      R(JK)=SQRT(T1*T1+T2*T2+T3*T3)
C
C         CALCULATE (DV/DQ)'S
C
      IF (B(NL).GT.0.0D0) THEN
         DUM1=EXP(-B(NL)*(R(JK)-RMZ(NL)))
         DUM1=2.0D0*D(NL)*(1.0D0-DUM1)*B(NL)*DUM1/R(JK)
         DUM2=T1*DUM1
         PDOT(K1)=PDOT(K1)+DUM2
         PDOT(J1)=PDOT(J1)-DUM2
         DUM2=T2*DUM1
         PDOT(K2)=PDOT(K2)+DUM2
         PDOT(J2)=PDOT(J2)-DUM2
         DUM2=T3*DUM1
         PDOT(K3)=PDOT(K3)+DUM2
         PDOT(J3)=PDOT(J3)-DUM2
      ELSE
C
C         THE BETA PARAMETER IN THE MORSE FUNCTION IS REPRESENTED
C         AS A QUARTIC POLYNOMIAL
C
         RLCM=R(JK)-RMZ(NL)
         BETAM=CM1(NL)+CM2(NL)*RLCM+CM3(NL)*RLCM**2+CM4(NL)*RLCM**3
     *     +CM5(NL)*RLCM**4
         DBETAM=CM2(NL)+2.0D0*CM3(NL)*RLCM+3.0D0*CM4(NL)*RLCM**2
     *              +4.0D0*CM5(NL)*RLCM**3
         DENOM=1.0D0/R(JK)
         DUM1=EXP(-BETAM*RLCM)
         DUM2=DBETAM*(1.0D0-RMZ(NL)*DENOM)+BETAM*DENOM
         DUM3=2.0D0*D(NL)*(1.0D0-DUM1)*DUM1*DUM2
         DERV=DUM3*T1
         PDOT(K1)=PDOT(K1)+DERV
         PDOT(J1)=PDOT(J1)-DERV
         DERV=DUM3*T2
         PDOT(K2)=PDOT(K2)+DERV
         PDOT(J2)=PDOT(J2)-DERV
         DERV=DUM3*T3
         PDOT(K3)=PDOT(K3)+DERV
         PDOT(J3)=PDOT(J3)-DERV
      ENDIF
      RETURN
      END
