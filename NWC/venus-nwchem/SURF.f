      SUBROUTINE SURF(NSURF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         BEAM TYPE AND THERMAL TYPE SURFACE SAMPLING 
C
      COMMON/TRANSB/TRANS,NREL
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/FRAGB/WTA(NDP),WTB(NDP),LA(NDP,NDA),LB(NDP,NDA),
     *QZA(NDP,NDA3),QZB(NDP,NDA3),NATOMA(NDP),NATOMB(NDP)
      COMMON/CHEMAC/WWA(NDA3),CA(NDA3yf,NDA3yf),AI(3),ENMTA,
     *AMPA(NDA3),WWB(NDA3),CB(NDA3yf,NDA3yf),BI(3),ENMTB,
     *AMPB(NDA3),SEREL,S,BMAX,TROTA,TROTB,ANQA(NDA3),ANQB(NDA3),
     *TVIBA,TVIBB,NROTA,NROTB,NOB
      COMMON/VECTB/VI(4),OAMI(4),AMAI(4),AMBI(4),ETAI,ERAI,ETBI,ERBI
      COMMON/SURFB/NN1,NN2,NN3,NN4,THTA,PHI,NCHI,CHI,RX0,RY0,RZ0,
     *NTHET,THET,NPHI1,PHI1,NPHI2,PHI2
C
  10  FORMAT(/15X,'IMPACT PARAMETER=',F7.3,' A')
  12  FORMAT(/5X,'BEAM-TYPE STUDY: THETA=',F8.3,'  PHI=',F8.3,
     *'  KAI=',F8.3,' (DEG)'/)
  14  FORMAT(/5X,'THERMAL-TYPE STUDY: THETA=',F8.3,'  PHI1=',F8.3,
     *'  PHI2=',F8.3,' (DEG)'/)
  16  FORMAT(/5X,'RELATIVE TRANSLATIONAL ENERGY SELECTED: ',F7.2,
     *' KCAL/MOL'/)
  18  FORMAT(/5X,'CHOSEN:   LX,LY,LZ =',1P3D13.5,' H-BAR')
C
C     DEFINE REFERENCE FRAME BASED ON THE THREE ATOMS ON THE SURFACE,
C     FIRST ATOM BEING THE ORIGIN. FOR THIS REFERENCE FRAME:
C        X-AXIS UNIT VECTOR: (DUMXX,DUMXY,DUMXZ)
C        Y-AXIS UNIT VECTOR: (DUMYX,DUMYY,DUMYZ) 
C        Z-AXIS UNIT VECTOR: (DUMZX,DUMZY,DUMZZ)
C     REFERENCE Z-AXIS VECTOR IS ORTHOGONAL TO THE SURFACE PLANE
C
      DUMXX=Q(3*NN2-2)-Q(3*NN1-2)
      DUMXY=Q(3*NN2-1)-Q(3*NN1-1)
      DUMXZ=Q(3*NN2)-Q(3*NN1)
      DN12=SQRT(DUMXX*DUMXX+DUMXY*DUMXY+DUMXZ*DUMXZ)
      DUMXX=DUMXX/DN12
      DUMXY=DUMXY/DN12
      DUMXZ=DUMXZ/DN12
C
      DUMYX=Q(3*NN3-2)-Q(3*NN1-2)
      DUMYY=Q(3*NN3-1)-Q(3*NN1-1)
      DUMYZ=Q(3*NN3)-Q(3*NN1)
      DUMZX=DUMXY*DUMYZ-DUMXZ*DUMYY
      DUMZY=DUMXZ*DUMYX-DUMXX*DUMYZ
      DUMZZ=DUMXX*DUMYY-DUMXY*DUMYX
      PNORM=SQRT(DUMZX*DUMZX+DUMZY*DUMZY+DUMZZ*DUMZZ)
      DUMZX=DUMZX/PNORM
      DUMZY=DUMZY/PNORM
      DUMZZ=DUMZZ/PNORM
C
      DUMYX=DUMZY*DUMXZ-DUMZZ*DUMXY
      DUMYY=DUMZZ*DUMXX-DUMZX*DUMXZ
      DUMYZ=DUMZX*DUMXY-DUMZY*DUMXX
C
C     SELECT IMPACT PARAMETER (BEAM OR THERMAL TYPE)
C      FOR DEFINITION : SEE MANUAL
C
      SB=BMAX
      IF (NOB.NE.1) THEN
         RAND=RAND0(ISEED)
         SB=BMAX*SQRT(RAND)
      ENDIF
      WRITE(6,10)SB
C
C     BEAM TYPE HANDLING
C
      IF (NSURF.EQ.1) THEN
C
C        SELECT THETA,PHI AND CHI ANGLES 
C
         THTA0=THTA
         RAND=RAND0(ISEED)
C     TODO list: eliminate PHI sometimes, not this time
         PHI0=TWOHI*RAND
         CHI0=CHI
         IF (NCHI.NE.1) THEN
            RAND=RAND0(ISEED)
            CHI0=CHI*RAND
         ENDIF
         WRITE(6,12)THTA0/C4,PHI0/C4,CHI0/C4
C
C        DETERMINATE AIMING POINT POSITION (RX0,RY0,RZ0) IN THE 
C        REFERENCE FRAME
C
         DUMAPX=Q(3*NN4-2)-Q(3*NN1-2)
         DUMAPY=Q(3*NN4-1)-Q(3*NN1-1)
         DUMAPZ=Q(3*NN4)-Q(3*NN1)
         RX0=DUMAPX*DUMXX+DUMAPY*DUMXY+DUMAPZ*DUMXZ
         RY0=DUMAPX*DUMYX+DUMAPY*DUMYY+DUMAPZ*DUMYZ
         RZ0=DUMAPX*DUMZX+DUMAPY*DUMZY+DUMAPZ*DUMZZ
C
C        DETERMINATE RELATIVE POSITION OF THE MASS CENTER OF FRAGMENT 
C        A (ACX,ACY,ACZ) TO THE AIMING POINT IN THE REFERENCE FRAME
C
         CSTHETA=COS(THTA0)
         SNTHETA=SIN(THTA0)
         CSPHI=COS(PHI0) 
         SNPHI=SIN(PHI0) 
         CSCHI=COS(CHI0)
         SNCHI=SIN(CHI0)
         DUM1=SB*SNPHI
         DUM2=S*SNTHETA-SB*CSPHI*CSTHETA
         ACX=-DUM2*CSCHI-DUM1*SNCHI
         ACY=-DUM2*SNCHI+DUM1*CSCHI
         ACZ=S*CSTHETA+SB*CSPHI*SNTHETA
C
C     THERMAL TYPE HANDLING
C
      ELSEIF (NSURF.EQ.2) THEN
C          
C        SELECT THETA,PHI1,PHI2 ANGLES
C
         THTE0=THET
         IF (NTHET.NE.1) THEN
            RAND=RAND0(ISEED)
            THTE0=ACOS(1.0D0-RAND*(1.0D0-COS(THET)))
         ENDIF
         PHI10=PHI1
         IF (NPHI1.NE.1) THEN
            RAND=RAND0(ISEED)
            PHI10=PHI1*RAND
         ENDIF
         PHI20=PHI2
         IF (NPHI2.NE.1) THEN
            RAND=RAND0(ISEED)
            PHI20=PHI2*RAND
         ENDIF
         WRITE(6,14)THTE0/C4,PHI10/C4,PHI20/C4
C
C        DETERMINATE RELATIVE POSITION OF THE MASS CENTER OF FRAGMENT
C        A (ACX,ACY,ACZ) TO THE AIMING POINT IN THE REFERENCE FRAME
C
         CSTHETA=COS(THTE0)
         SNTHETA=SIN(THTE0)
         CSPHI1=COS(PHI10)
         SNPHI1=SIN(PHI10)
         CSPHI2=COS(PHI20)
         SNPHI2=SIN(PHI20)
         ACX=S*SNTHETA*CSPHI2+SB*CSPHI1
         ACY=S*SNTHETA*SNPHI2+SB*SNPHI1
         ACZ=S*CSTHETA
      ENDIF
C
C     DETERMINATE COORDINATES OF THE MASS CENTER OF FRAGMENT A IN THE 
C     REFERENCE FRAME
C
      ACX=ACX+RX0
      ACY=ACY+RY0
      ACZ=ACZ+RZ0
C
C     TRANSFORM COORDINATES OF THE MASS CENTER OF FRAGMENT A FROM REFERENCE
C     FRAME TO THE PROGRAM'S COORDINATE FRAME (ORIGNATED AT THE MASS CENTER
C     OF FRAGMENT B/PRINCIPAL AXIS OF ROTATION) AND THUS GET COORDINATES OF 
C     ALL ATOMS OF FRAGMENT A IN THE PROGRAM'S COORDINATE SYSTEM)
C              (B IS THE SURFACE)
C
      XDUM=ACX
      YDUM=ACY
      ZDUM=ACZ
      ACX=DUMXX*XDUM+DUMYX*YDUM+DUMZX*ZDUM+Q(3*NN1-2)
      ACY=DUMXY*XDUM+DUMYY*YDUM+DUMZY*ZDUM+Q(3*NN1-1)
      ACZ=DUMXZ*XDUM+DUMYZ*YDUM+DUMZZ*ZDUM+Q(3*NN1)
      N=NATOMA(1)
      DO I=1,N
         J=3*LA(1,I)
         Q(J)=Q(J)+ACZ
         Q(J-1)=Q(J-1)+ACY
         Q(J-2)=Q(J-2)+ACX
      ENDDO
C
C     IF NREL = 0
C     CHOOSE RELATIVE ENERGY FROM BOLTZMANN DISTRIBUTION
C
      IF (NREL.EQ.0) THEN
         DUM=GAMA(2,ISEED)
         SEREL=0.00198717D0*DUM*TRANS
         WRITE(6,16)SEREL
         SEREL=SEREL*C1
      ENDIF
C
C     ADD RELATIVE TRANSLATIONAL ENERGY
C     RELATIVE VELOCITY VECTOR IS FROM BEAM CENTER TO THE AIMING POINT
C
      WT=WTA(1)+WTB(1)
      SDUM=WTA(1)*WTB(1)/WT
      DUM=SQRT(2.0D0*SEREL/SDUM)
      VELA=DUM*WTB(1)/WT
      VELB=VELA-DUM
C     
C     UNIT RELATIVE VELOCITY VECTOR IN THE SURFACE REFERENCE FRAME
C
      IF (NSURF.EQ.1) THEN
         UIX=SNTHETA*CSCHI    
         UIY=SNTHETA*SNCHI
         UIZ=-CSTHETA
      ELSEIF (NSURF.EQ.2) THEN
         UIX=-SNTHETA*CSPHI2
         UIY=-SNTHETA*SNPHI2
         UIZ=-CSTHETA
      ENDIF
C
C     UNIT RELATIVE VELOCITY VECTOY IN THE PROGRAM'S COORDINATE SYSTEM
C
      XDUM=UIX
      YDUM=UIY
      ZDUM=UIZ
      UIX=DUMXX*XDUM+DUMYX*YDUM+DUMZX*ZDUM
      UIY=DUMXY*XDUM+DUMYY*YDUM+DUMZY*ZDUM
      UIZ=DUMXZ*XDUM+DUMYZ*YDUM+DUMZZ*ZDUM
C
C     CONTRIBUTION OF RELATIVE VELOCITY TO A AND B MOMENTUM
C
      N=NATOMA(1)
      DO I=1,N 
         J=3*LA(1,I)
         PMOD=W(LA(1,I))*VELA
         P(J)=P(J)+PMOD*UIZ
         P(J-1)=P(J-1)+PMOD*UIY
         P(J-2)=P(J-2)+PMOD*UIX
      ENDDO
      N=NATOMB(1)
      DO I=1,N
         J=3*LB(1,I)
         PMOD=W(LB(1,I))*VELB
         P(J)=P(J)+PMOD*UIZ
         P(J-1)=P(J-1)+PMOD*UIY
         P(J-2)=P(J-2)+PMOD*UIX
      ENDDO
      CALL DVDQ
      CALL ENERGY
C
C     SAVE INITIAL RELATIVE VELOCITY AND ORBITAL ANGULAR MOMENTUM
C
      VI(1)=DUM*UIX
      VI(2)=DUM*UIY
      VI(3)=DUM*UIZ
      VI(4)=DUM
C
C     SAVE INITIAL ORBITAL ANGULAR MOMENTUM (NOTE THAT MASS CENTER OF B
C     IS THE ORIGIN OF PROGRAM'S COORDINAT SYSTEM): 
C               ->      ->    ->           ->
C               OAM = (QCMA -QCMB) X (SDUM*VI)
C
      OAMI(1)=SDUM*DUM*(ACY*UIZ-ACZ*UIY)/C7
      OAMI(2)=SDUM*DUM*(ACZ*UIX-ACX*UIZ)/C7
      OAMI(3)=SDUM*DUM*(ACX*UIY-ACY*UIX)/C7
      OAMI(4)=SQRT(OAMI(1)*OAMI(1)+OAMI(2)*OAMI(2)+
     *       OAMI(3)*OAMI(3))
      WRITE(6,18)OAMI(1),OAMI(2),OAMI(3)
C
      RETURN
      END
