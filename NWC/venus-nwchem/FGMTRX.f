      SUBROUTINE FGMTRX(I3N)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CONSTRUCT AN EFFECTIVE WILSON -FG- MATRIX BY INTRODUCING MASS 
C         DEPENDENCE INTO THE POTENTIAL FORCE CONSTANT MATRIX. THE NORMAL
C         MODES AND THE SPECTROSCOPIC FREQUENCIES ARE THEN EVALUATED.          
C
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      COMMON/SELTB/QZ(NDA3),NSELT,NSFLAG,NACTA,NACTB,NLINA,NLINB,NSURF
      COMMON/ARRAYS/A(NDA3yf,NDA3yf),DA(NDA3),B(NDA3yf,NDA3yf),DB(NDA3)
      COMMON/EIGVL/EIG(NDA3yf)
      COMMON/RSTART/HINC,NPTS
      COMMON/FR2/DG(NDA3,2),DIM(NDA3)
      DATA TFACT/2.81837552D05/
      DATA RHO/1.0D-08/
      save tfact,rho
  101 FORMAT(///)
  102 FORMAT(/10X,'EIGENVECTORS OF CARTESIAN FORCE CONSTANT MATRIX'/)
  103 FORMAT(/18X,' NORMAL MODES AND VIBRATIONAL FREQUENCIES (CM-1)')
  104 FORMAT(/)
C
C         MOVE DIAGONAL OF -A- TO -DA- AND SAVE
C
      DO I=1,I3N
         DA(I)=A(I,I)
      ENDDO
      DO I=1,I3N
         DO J=1,I
            A(J,I)=A(I,J)
         ENDDO
         A(I,I)=DA(I)
      ENDDO
C
C         NOW DIAGONALIZE CARTESIAN FORCE MATRIX
C         SET PRINT FILE FOR EIGOUT
C
      IP=77
      CALL EIGN(A,B,I3N,RHO)
      WRITE(77,101)
      WRITE(77,102)
      CALL EIGOUT(B,I3N,IP)
C
C         CREATE FULL FORCE MATRIX FOR MASS-WEIGHTED  TRANSFORMATION
C
      DO I=1,I3N
         DO J=1,I
            A(I,J)=A(J,I)
         ENDDO
         A(I,I)=DA(I)
      ENDDO
C
C         CONSTRUCT MASS WEIGHTED MATRIX
C
      DO I=1,I3N
         DO J=1,I3N
            A(I,J)=DIM(I)*A(I,J)*DIM(J)
         ENDDO
      ENDDO
C
C         DIAGONALIZE MASS-WEIGHTED MATRIX AND CONVERT FREQUENCIES
C         TO WAVE NUMBERS
C
      CALL EIGN(A,B,I3N,RHO)
      DO I=1,I3N
         EIG(I)=SQRT(ABS(TFACT*EIG(I)))
      ENDDO
C
C         NORMALIZE THE EIGENVECTORS
C
      DO I=1,I3N
         DO J=1,I3N
            A(I,J)=B(I,J)
         ENDDO
      ENDDO
      DO I=1,I3N
         DUM=0.0D0
         DO J=1,I3N
            DUM=DUM+A(I,J)*A(I,J)
         ENDDO
         DUM=SQRT(DUM)
         DO J=1,I3N
            A(I,J)=A(I,J)/DUM
         ENDDO
      ENDDO
C
C         SET PHASE OF EIGENVECTORS
C
      DO 45 I=1,I3N
         AMAX=0.0D0
         DO 43 J=1,I3N
            IF (ABS(A(I,J)).GE.ABS(AMAX)) AMAX=A(I,J)
   43    CONTINUE
         IF (AMAX.LT.0.0D0) THEN
            DO 44 J=1,I3N
               A(I,J)=-A(I,J)
   44       CONTINUE
         ENDIF
   45 CONTINUE
C
C         MASS-WEIGHT THE EIGENVECTORS SO THAT FURTHER NORMAL MODE
C         TRANSFORMATION IS PERFORMED FROM THE CARTESIAN COORDINATES
C
      DO I=1,I3N
         DO J=1,I3N
            A(I,J)=DIM(J)*A(I,J)
         ENDDO
      ENDDO
C
C         WRITE EIGENVALUES AND EIGENVECTORS
C
      IP=6
      IF (NTZ.EQ.1.OR.NSELT.LT.0) THEN
         WRITE(6,103)
         CALL EIGOUT(A,I3N,IP)
         WRITE(6,104)
      ENDIF
      RETURN
      END
