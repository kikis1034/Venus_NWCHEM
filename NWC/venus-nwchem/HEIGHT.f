      SUBROUTINE HEIGHT(IHT,HT)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE VALUE OF HEIGHT ABOVE THE SURFACE FOR SOME ATOMS 
C             input:  IHT -- atom number  
C            output:   HT -- height of the atom
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PRLIST/T,V,H,TIME,NTZ,NT,ISEED0(8),NC,NX
      COMMON/SURFB/NN1,NN2,NN3,NN4,THTA,PHI,NCHI,CHI,RX0,RY0,RZ0,
     *NTHET,THET,NPHI1,PHI1,NPHI2,PHI2
      SAVE DUMX,DUMY,DUMZ,NC0,NTZ0
      DATA NC0/-1/,NTZ0/-1/
C
C         DUMX,DUMY,DUMZ ARE THE COORDINATE OF A UNIT VECTOR ORTHOGONAL
C         TO THE SURFACE PLANE THAT HAS BEEN DEFINED BY THE USER
C
      IF ((NC.NE.NC0).OR.(NTZ.NE.NTZ0)) THEN
         DUMX1=Q(3*NN2-2)-Q(3*NN1-2)
         DUMY1=Q(3*NN2-1)-Q(3*NN1-1)
         DUMZ1=Q(3*NN2)-Q(3*NN1)
         DUMX2=Q(3*NN3-2)-Q(3*NN1-2)
         DUMY2=Q(3*NN3-1)-Q(3*NN1-1)
         DUMZ2=Q(3*NN3)-Q(3*NN1)
         DUMX=DUMY1*DUMZ2-DUMZ1*DUMY2
         DUMY=DUMX2*DUMZ1-DUMX1*DUMZ2
         DUMZ=DUMX1*DUMY2-DUMY1*DUMX2
         PNORM=SQRT(DUMX*DUMX+DUMY*DUMY+DUMZ*DUMZ)
         DUMX=DUMX/PNORM
         DUMY=DUMY/PNORM
         DUMZ=DUMZ/PNORM
      ENDIF
      NC0=NC
      NTZ0=NTZ
C
C        POSITION HEIGHT ABOVE THE SURFACE PLANE
C
      DUMIX=Q(3*IHT-2)-Q(3*NN1-2)
      DUMIY=Q(3*IHT-1)-Q(3*NN1-1)
      DUMIZ=Q(3*IHT)-Q(3*NN1)
      HT=DUMX*DUMIX+DUMY*DUMIY+DUMZ*DUMIZ
C
      RETURN
      END
