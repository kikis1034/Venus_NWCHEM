      SUBROUTINE DMBE
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE THE HO2 DMBE IV POTENTIAL ENERGY AND DERIVATIVE
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/PQDOT/P(NDA3),QDOT(NDA3),W(NDA)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
      COMMON/DMBEB/VDMBE,NDMB(3)
      DIMENSION RR(3),GRAD(3)
      LOGICAL FIRST
      DATA FIRST/.TRUE./CA0,CA1/0.5291772D0,627.51D0/
      SAVE FIRST
C
C         SCREEN FOR ATOM LABELS
C
      IF (FIRST) THEN
         DO I=1,NATOMS
            TMP=ABS(1.0D0-W(I)/1.0D0)
            IF (TMP.LT.0.05D0) NDMB(3)=I
         ENDDO
         K=1
         DO I=1,NATOMS
            TMP=ABS(1.0D0-W(I)/16.0D0)
            IF (TMP.LT.0.05D0.AND.K.LE.2) THEN
               NDMB(K)=I
               K=K+1
            ENDIF
         ENDDO
         IDUM=NDMB(1)*NDMB(2)*NDMB(3)
         IF (IDUM.EQ.0) STOP
         FIRST=.FALSE.
      ENDIF
C
C         INTERATOMIC DISTANCES IN ATOMIC UNITS
C
      DO J=1,3
         DO K=J+1,3
            JK=(J-1)*(2*3-J)/2+K-J
            J3=3*NDMB(J)
            J2=J3-1
            J1=J2-1
            K3=3*NDMB(K)
            K2=K3-1
            K1=K2-1
            T1=Q(J1)-Q(K1)
            T2=Q(J2)-Q(K2)
            T3=Q(J3)-Q(K3)
            RR(JK)=SQRT(T1*T1+T2*T2+T3*T3)/CA0
         ENDDO
      ENDDO
C
C         ENERGY AND DERIVATIVES WITH RESPECT TO
C         INTERATOMIC DISTANCES
C
      CALL HO2SUR(RR,VDMBE)
      CALL HO2DER(RR,GRAD)
      CC1=CA1*C1
      CC0=CC1/CA0/CA0
      VDMBE=VDMBE*CC1
      DO I=1,3
         GRAD(I)=GRAD(I)/RR(I)*CC0
      ENDDO
C
C        CALCULATE (DV/DQ)'S
C
      DO J=1,3
         DO K=J+1,3
            JK=(J-1)*(2*3-J)/2+K-J
            J3=3*NDMB(J)
            J2=J3-1
            J1=J2-1
            K3=3*NDMB(K)
            K2=K3-1
            K1=K2-1
            T1=Q(J1)-Q(K1)
            T2=Q(J2)-Q(K2)
            T3=Q(J3)-Q(K3)
            TMP=GRAD(JK)*T1
            PDOT(K1)=PDOT(K1)-TMP
            PDOT(J1)=PDOT(J1)+TMP
            TMP=GRAD(JK)*T2
            PDOT(K2)=PDOT(K2)-TMP
            PDOT(J2)=PDOT(J2)+TMP
            TMP=GRAD(JK)*T3
            PDOT(K3)=PDOT(K3)-TMP
            PDOT(J3)=PDOT(J3)+TMP
         ENDDO
      ENDDO
C
      RETURN
      END
      SUBROUTINE HO2SUR(X,F)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         THE FOLLOWING IS A COLLECTION OF FUNCTIONS AND ROUTINES
C         FROM A.J.C. VARANDAS, UNIVERSITY OF COIMBRA, PORTUGAL:
C         HO2SUR, HO2DER, DEXDIS, DELECT, THREBQ, VOH, EHFOH,
C         DISOH, DVOH, VOO, EHFOO, DISOO, DVOO, CEF, EXDIS, ELECT,
C         DISP, DDISP, AND HO2DAT DATA BLOCK
C
C         CALCULATE THE HO2 DMBE IV POTENTIAL ENERGY SURFACE 
C
      DIMENSION X(3)
      COMMON/HCOEFF/C(52)
      COMMON/THRBOD/POLQ,DECAY1,DECAY2,DECAY3,R1,R2,R3
      COMMON/REFGEO/R10,R20,R30
      COMMON/TESTD/ITEST
      R1=X(1)
      R2=X(2)
      R3=X(3)
      Q1=1.0D0/SQRT(3.0D0)*(R1+R2+R3)
      Q2=1.0D0/SQRT(2.0D0)*(R2-R3)
      Q3=1.0D0/SQRT(6.0D0)*(2.0D0*R1-R2-R3)
      F=VOO(R1)+VOH(R2)+VOH(R3)+THREBQ(Q1,Q2,Q3)+
     1  EXDIS(R1,R2,R3)+ELECT(R1,R2,R3)
      RETURN
      END
      SUBROUTINE HO2DER(X,G)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE HO2 DMBE IV POTENTIAL ENERGY SURFACE AND
C         ITS FIRST DERIVATIVES
C
      DIMENSION X(3),G(3)
      COMMON/HCOEFF/C(52)
      COMMON/THRBOD/POLQ,DECAY1,DECAY2,DECAY3,R1,R2,R3
      COMMON/REFGEO/R10,R20,R30
      COMMON/TESTD/ITEST
      R1=X(1)
      R2=X(2)
      R3=X(3)
      Q1=1.0D0/SQRT(3.0D0)*(R1+R2+R3)
      Q2=1.0D0/SQRT(2.0D0)*(R2-R3)
      Q3=1.0D0/SQRT(6.0D0)*(2.0D0*R1-R2-R3)
      F=THREBQ(Q1,Q2,Q3)
      S1=R1-R10
      S2=R2-R20
      S3=R3-R30
      Q12=Q1*Q1
      Q13=Q12*Q1
      Q14=Q13*Q1
      Q15=Q14*Q1
      Q16=Q15*Q1
      Q22=Q2*Q2
      Q32=Q3*Q3
      TQ1=Q22+Q32
      TQ2=Q32-3.0D0*Q22
      TQ3=Q22-Q32
      TQ12=TQ1*TQ1
      TQ13=TQ12*TQ1
      TQ22=TQ2*TQ2
      DQ1R1=1.0D0/SQRT(3.0D0)
      DQ1R2=DQ1R1
      DQ1R3=DQ1R1
      DQ2R1=0.0D0
      DQ2R2=1.0D0/SQRT(2.0D0)
      DQ2R3=-DQ2R2
      DQ3R1=2.0D0/SQRT(6.0D0)
      DQ3R2=-0.5D0*DQ3R1
      DQ3R3=DQ3R2
      DTQ2=2.0D0*Q2
      DTQ3=2.0D0*Q3
      DPOQ1=C(1)+2.0D0*C(2)*Q1+3.0D0*C(4)*Q12+C(5)*TQ1+4.0D0*C(7)*Q13+
     1 2.0D0*C(8)*Q1*TQ1+C(10)*Q3*TQ2+C(12)*Q3+C(14)*Q1*DTQ3+C(15)*TQ3+
     2  3.0D0*C(17)*Q12*Q3+2.0D0*C(18)*Q1*TQ3+C(19)*Q3*TQ1+5.0D0*
     3 C(23)*Q14+3.0D0*C(24)*Q12*TQ1+C(25)*TQ12+C(26)*Q1*DTQ3*TQ2+4.0D0
     4  *C(28)*Q13*Q3+3.0D0*C(29)*Q12*TQ3+C(30)*Q1*DTQ3*TQ1+C(31)*Q32*
     5  TQ2+C(32)*TQ1*TQ3+6.0D0*C(35)*Q15+4.0D0*C(36)*Q13*TQ1+2.0D0*
     6  C(37)*Q1*TQ12+3.0D0*C(38)*Q12*Q3*TQ2+C(39)*Q3*TQ1*TQ2+5.0D0*
     7  C(42)*Q14*Q3+4.0D0*C(43)*Q13*TQ3+3.0D0*C(44)*Q12*Q3*TQ1+2.0D0*
     8  C(45)*Q1*Q32*TQ2+2.0D0*C(46)*Q1*TQ1*TQ3+C(47)*Q3*TQ12+C(48)*Q3*
     9  TQ2*TQ3
      DPOQ2=C(3)*DTQ2+C(5)*Q1*DTQ2-C(6)*Q3*6.0D0*Q2+C(8)*Q12*DTQ2
     1   +C(9)*2.0D0*TQ1*DTQ2-C(10)*Q1*Q3*6.0D0*Q2+C(13)*DTQ2+
     2   C(15)*Q1*DTQ2+C(16)*Q3*DTQ2+C(18)*Q12*DTQ2+
     3   C(19)*Q1*Q3*DTQ2-C(20)*Q32*6.0D0*Q2+C(21)*DTQ2*TQ3+
     4   C(21)*TQ1*DTQ2+C(24)*Q13*DTQ2+C(25)*Q1*2.0D0*TQ1*DTQ2-
     5   C(26)*Q12*Q3*6.0D0*Q2+C(27)*Q3*DTQ2*TQ2-C(27)*Q3*TQ1*6.0D0*
     6   Q2+C(29)*Q13*DTQ2+C(30)*Q12*Q3*DTQ2-C(31)*Q1*Q32*6.0D0*Q2
     7   +C(32)*Q1*DTQ2*(TQ1+TQ3)+C(33)*DTQ3*TQ1*DTQ2+C(34)*Q3*DTQ2*
     8   (TQ2-3.0D0*TQ3)+C(36)*Q14*DTQ2+4.0D0*C(37)*Q12*Q2*TQ1-
     9   C(38)*Q13*Q3*6.0D0*Q2+C(39)*Q1*Q3*DTQ2*(TQ2-3.0D0*TQ1)+
     A   C(40)*3.0D0*TQ12*DTQ2-C(41)*Q32*TQ2*6.0D0*DTQ2+
     B   C(43)*Q14*DTQ2+C(44)*Q13*Q3*DTQ2-C(45)*Q12*Q32*6.0D0*Q2+
     C   C(46)*Q12*DTQ2*(TQ3+TQ1)+C(47)*Q1*DTQ3*TQ1*DTQ2+
     D  C(48)*Q1*Q3*DTQ2*(TQ2-3.0D0*TQ3)+C(49)*Q32*DTQ2*(TQ2-3.0D0*TQ1)
     E   +C(50)*DTQ2*TQ1*(2.0D0*TQ3+TQ1)
      DPOQ3=C(3)*DTQ3+C(5)*Q1*DTQ3+C(6)*TQ2+C(6)*Q3*DTQ3+C(8)*Q12*DTQ3+
     1   C(9)*TQ1*2.0D0*DTQ3+C(10)*Q1*(TQ2+Q3*DTQ3)+C(11)+
     2   C(12)*Q1-C(13)*DTQ3+C(14)*Q12-C(15)*Q1*DTQ3+C(16)*(TQ1+
     3   Q3*DTQ3)+C(17)*Q13-C(18)*Q12*DTQ3+C(19)*Q1*(TQ1+Q3*DTQ3)
     4   +C(20)*DTQ3*(TQ2+Q32)+C(21)*DTQ3*(TQ3-TQ1)
     5  +C(24)*Q13*DTQ3+C(25)*Q1*2.0D0*TQ1*DTQ3+C(26)*Q12*(TQ2+Q3*DTQ3)
     6   +C(27)*(TQ1*TQ2+Q3*DTQ3*(TQ2+TQ1))+C(28)*Q14-C(29)*Q13*DTQ3
     7   +C(30)*Q12*(TQ1+Q3*DTQ3)+C(31)*Q1*DTQ3*(TQ2+Q32)+C(32)*Q1*
     8   DTQ3*(TQ3-TQ1)+C(33)*TQ1*(TQ1+DTQ3*DTQ3)+C(34)*TQ2*TQ3+
     9  C(34)*Q3*DTQ3*(TQ3-TQ2)+C(36)*Q14*DTQ3+C(37)*Q12*2.0D0*TQ1*DTQ3
     A   +C(38)*Q13*(TQ2+Q3*DTQ3)+C(39)*Q1*TQ1*TQ2+C(39)*Q1*Q3*DTQ3*
     B  (TQ1+TQ2)+C(40)*3.0D0*TQ12*DTQ3+C(41)*DTQ3*TQ2*(TQ2+2.0D0*Q32)+
     C  C(42)*Q15-C(43)*Q14*DTQ3+C(44)*Q13*(TQ1+Q3*DTQ3)+C(45)*Q12*DTQ3
     D   *(TQ2+Q32)+C(46)*Q12*DTQ3*(TQ3-TQ1)+C(47)*Q1*(TQ12+TQ1*4.0D0*
     E   Q32)+C(48)*Q1*TQ2*TQ3+C(48)*Q1*Q3*DTQ3*(TQ3-TQ2)+C(49)*DTQ3*
     F  TQ1*TQ2+C(49)*Q32*DTQ3*(TQ1+TQ2)+C(50)*DTQ3*TQ1*(2.0D0*TQ3-TQ1)
      CALL  DEXDIS(R1,R2,R3,DER1,DER2,DER3)
      CALL  DELECT(R1,R2,R3,DERI1,DERI2,DERI3)
      G(1)=((DPOQ1*DQ1R1+DPOQ2*DQ2R1+DPOQ3*DQ3R1)*DECAY1-POLQ*C(51)/
     1   COSH(C(51)*S1)**2)*DECAY2*DECAY3+DVOO(R1)+DER1+DERI1
      G(2)=((DPOQ1*DQ1R2+DPOQ2*DQ2R2+DPOQ3*DQ3R2)*DECAY2-POLQ*C(52)/
     1   COSH(C(52)*S2)**2)*DECAY1*DECAY3+DVOH(R2)+DER2+DERI2
      G(3)=((DPOQ1*DQ1R3+DPOQ2*DQ2R3+DPOQ3*DQ3R3)*DECAY3-POLQ*C(52)/
     1   COSH(C(52)*S3)**2)*DECAY1*DECAY2+DVOH(R3)+DER3+DERI3
      RETURN
      END
      SUBROUTINE DEXDIS(R1,R2,R3,DER1,DER2,DER3)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C         CALCULATE THE 1ST DERIVATIVES OF THE EXCHANGE-DISPERSION TERM
C
      DIMENSION CEFOO(10),CEFOH2(10),CEFOH3(10),CEDOO(10),CEDOH2(10)
      DIMENSION CEDOH3(10)
      COMMON/DISPC/COO(10),COH(10)
      COMMON/RKVAL/RK0OO(10),RK1OO(10),RK0OH(10),RK1OH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      T1=R1-RMOO
      T5=R2-RMOH
      T6=R3-RMOH
      T7=0.5D0*COO(6)
      T8=0.5D0*COO(8)
      T9=0.5D0*COO(10)
      T10=0.5D0*COH(6)
      T11=0.5D0*COH(8)
      T12=0.5D0*COH(10)
      T13=RK1OH(6)*RK0OH(6)
      T14=RK1OH(8)*RK0OH(8)
      T15=RK1OH(10)*RK0OH(10)
      T16=RK1OO(6)*RK0OO(6)
      T18=RK1OO(8)*RK0OO(8)
      T19=RK1OO(10)*RK0OO(10)
      T20=EXP(-RK1OH(6)*T5)
      T21=EXP(-RK1OH(6)*T6)
      T22=EXP(-RK1OH(8)*T5)
      T23=EXP(-RK1OH(8)*T6)
      T24=EXP(-RK1OH(10)*T5)
      T25=EXP(-RK1OH(10)*T6)
      T26=EXP(-RK1OO(6)*T1)
      T27=EXP(-RK1OO(8)*T1)
      T28=EXP(-RK1OO(10)*T1)
      T29=COSH(RK1OH(6)*R2)
      T30=COSH(RK1OH(6)*R3)
      T31=COSH(RK1OH(8)*R2)
      T32=COSH(RK1OH(8)*R3)
      T33=COSH(RK1OH(10)*R2)
      T34=COSH(RK1OH(10)*R3)
      T35=COSH(RK1OO(6)*R1)
      T36=COSH(RK1OO(8)*R1)
      T37=COSH(RK1OO(10)*R1)
      T38=TANH(RK1OH(6)*R2)
      T39=TANH(RK1OH(6)*R3)
      T40=TANH(RK1OH(8)*R2)
      T41=TANH(RK1OH(8)*R3)
      T42=TANH(RK1OH(10)*R2)
      T43=TANH(RK1OH(10)*R3)
      T44=TANH(RK1OO(6)*R1)
      T45=TANH(RK1OO(8)*R1)
      T46=TANH(RK1OO(10)*R1)
      DO IN=6,10,2
         CEFOO(IN)=CEF(COO(IN),RK0OH(IN),RK1OH(IN),RK0OH(IN),RK1OH(IN),
     1          RMOH,RMOH,R2,R3)
         CEDOO(IN)=CEFOO(IN)-COO(IN)
         CEFOH2(IN)=CEF(COH(IN),RK0OO(IN),RK1OO(IN),RK0OH(IN),RK1OH(IN),
     1          RMOO,RMOH,R1,R3)
         CEDOH2(IN)=CEFOH2(IN)-COH(IN)
         CEFOH3(IN)=CEF(COH(IN),RK0OO(IN),RK1OO(IN),RK0OH(IN),RK1OH(IN),
     1          RMOO,RMOH,R1,R2)
         CEDOH3(IN)=CEFOH3(IN)-COH(IN)
      ENDDO
      RR1=2.0D0*R1/(RMOO+2.5D0*R0OO)
      T6R1=((1.0D0-EXP(-ADAMP(6)*RR1-BDAMP(6)*RR1**2))/R1)**6
      T8R1=((1.0D0-EXP(-ADAMP(8)*RR1-BDAMP(8)*RR1**2))/R1)**8
      T10R1=((1.0D0-EXP(-ADAMP(10)*RR1-BDAMP(10)*RR1**2))/R1)**10
      RR2=2.0D0*R2/(RMOH+2.5D0*R0OH)
      T6R2=((1.0D0-EXP(-ADAMP(6)*RR2-BDAMP(6)*RR2**2))/R2)**6
      T8R2=((1.0D0-EXP(-ADAMP(8)*RR2-BDAMP(8)*RR2**2))/R2)**8
      T10R2=((1.0D0-EXP(-ADAMP(10)*RR2-BDAMP(10)*RR2**2))/R2)**10
      RR3=2.0D0*R3/(RMOH+2.5D0*R0OH)
      T6R3=((1.0D0-EXP(-ADAMP(6)*RR3-BDAMP(6)*RR3**2))/R3)**6
      T8R3=((1.0D0-EXP(-ADAMP(8)*RR3-BDAMP(8)*RR3**2))/R3)**8
      T10R3=((1.0D0-EXP(-ADAMP(10)*RR3-BDAMP(10)*RR3**2))/R3)**10
      DC61R2=T7*(T13
     1      *T20*T39+
     1      (1.0D0-RK0OH(6)*T21)
     1      *RK1OH(6)/T29**2)
      DC61R3=T7*(T13
     1      *T21*T38+
     1      (1.0D0-RK0OH(6)*T20)
     1      *RK1OH(6)/T30**2)
      DC81R2=T8*(T14
     1      *T22*T41+
     1      (1.0D0-RK0OH(8)*T23)
     1      *RK1OH(8)/T31**2)
      DC81R3=T8*(T14
     1      *T23*T40+
     1      (1.0D0-RK0OH(8)*T22)
     1      *RK1OH(8)/T32**2)
      D101R2=T9*(T15
     1      *T24*T43+
     1      (1.0D0-RK0OH(10)*T25)
     1      *RK1OH(10)/T33**2)
      D101R3=T9*(T15
     1      *T25*T42+
     1      (1.0D0-RK0OH(10)*T24)
     1      *RK1OH(10)/T34**2)
      DC62R1=T10*(T16
     1      *T26*T39+
     1      (1.0D0-RK0OH(6)*T21)
     1      *RK1OO(6)/T35**2)
      DC62R3=T10*(T13
     1      *T21*T44+
     1      (1.0D0-RK0OO(6)*T26)
     1      *RK1OH(6)/T30**2)
      DC82R1=T11*(T18
     1      *T27*T41+
     1      (1.0D0-RK0OH(8)*T23)
     1      *RK1OO(8)/T36**2)
      DC82R3=T11*(T14
     1      *T23*T45+
     1      (1.0D0-RK0OO(8)*T27)
     1      *RK1OH(8)/T32**2)
      D102R1=T12*(T19
     1      *T28*T43+
     1      (1.0D0-RK0OH(10)*T25)
     1      *RK1OO(10)/T37**2)
      D102R3=T12*(T15
     1      *T25*T46+
     1      (1.0D0-RK0OO(10)*T28)
     1      *RK1OH(10)/T34**2)
      DC63R1=T10*(T16
     1      *T26*T38+
     1      (1.0D0-RK0OH(6)*T20)
     1      *RK1OO(6)/T35**2)
      DC63R2=T10*(T13
     1      *T20*T44+
     1      (1.0D0-RK0OO(6)*T26)
     1      *RK1OH(6)/T29**2)
      DC83R1=T11*(T18
     1      *T27*T40+
     1      (1.0D0-RK0OH(8)*T22)
     1      *RK1OO(8)/T36**2)
      DC83R2=T11*(T14
     1      *T22*T45+
     1      (1.0D0-RK0OO(8)*T27)
     1      *RK1OH(8)/T31**2)
      D103R1=T12*(T19
     1      *T28*T42+
     1      (1.0D0-RK0OH(10)*T24)
     1      *RK1OO(10)/T37**2)
      D103R2=T12*(T15
     1      *T24*T46+
     1      (1.0D0-RK0OO(10)*T28)
     1      *RK1OH(10)/T33**2)
      DER1=DDISP(R1,CEDOO(6),CEDOO(8),CEDOO(10),R0OO,RMOO)-DC62R1*
     1         T6R2-DC63R1*T6R3-DC82R1*T8R2-DC83R1*T8R3-D102R1*T10R2-
     2     D103R1*T10R3
      DER2=DDISP(R2,CEDOH2(6),CEDOH2(8),CEDOH2(10),R0OH,RMOH)-DC61R2*
     1         T6R1-DC63R2*T6R3-DC81R2*T8R1-DC83R2*T8R3-D101R2*T10R1-
     2     D103R2*T10R3
      DER3=DDISP(R3,CEDOH3(6),CEDOH3(8),CEDOH3(10),R0OH,RMOH)-DC61R3*
     1         T6R1-DC62R3*T6R2-DC81R3*T8R1-DC82R3*T8R2-D101R3*T10R1-
     2     D102R3*T10R2
      RETURN
      END
      SUBROUTINE DELECT(R1,R2,R3,DER1,DER2,DER3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE DERIVATIVES OF THE ELECTROSTATIC TERM
C
      COMMON/POLAR/C4,C5
      COMMON/RKVAL/RK0OO(10),RK1OO(10),RK0OH(10),RK1OH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      C42=C4
      C43=C4
      C52=C5
      C53=C5
      RR1=2.0D0*R1/(RMOO+2.5D0*R0OO)
      D4R1=1.0D0-EXP(-ADAMP(4)*RR1-BDAMP(4)*RR1*RR1)
      D5R1=1.0D0-EXP(-ADAMP(5)*RR1-BDAMP(5)*RR1*RR1)
      T4R1=(D4R1/R1)**4
      T5R1=(D5R1/R1)**5
      RR2=2.0D0*R2/(RMOH+2.5D0*R0OH)
      D4R2=1.0D0-EXP(-ADAMP(4)*RR2-BDAMP(4)*RR2*RR2)
      D5R2=1.0D0-EXP(-ADAMP(5)*RR2-BDAMP(5)*RR2*RR2)
      T4R2=(D4R2/R2)**4
      T5R2=(D5R2/R2)**5
      RR3=2.0D0*R3/(RMOH+2.5D0*R0OH)
      D4R3=1.0D0-EXP(-ADAMP(4)*RR3-BDAMP(4)*RR3*RR3)
      D5R3=1.0D0-EXP(-ADAMP(5)*RR3-BDAMP(5)*RR3*RR3)
      T4R3=(D4R3/R3)**4
      T5R3=(D5R3/R3)**5
      R23=R2**3
      R24=R23*R2
      R33=R3**3
      R34=R33*R3
      R14=R1**4
      R15=R14*R1
      R25=R24*R2
      R35=R34*R3
      RMQ=RMOH**4
      RMQ5=0.50D0/RMQ
      RMR3=RMQ5*R34
      RMR2=RMQ5*R24
      RMR33=RMQ5*R33
      RMR23=RMQ5*R23
      TAO=TANH(RK1OO(4)*R1)
      TAH2=TANH(RK1OH(4)*R2)
      TAH3=TANH(RK1OH(4)*R3)
      EX3=EXP(-RK1OH(4)*(R3-RMOH))
      EX2=EXP(-RK1OH(4)*(R2-RMOH))
      COR=1.0D0/COSH(RK1OO(4)*R1)
      COR2=1.0D0/COSH(RK1OH(4)*R2)
      COR3=1.0D0/COSH(RK1OH(4)*R3)
      R3E3=RMR3*EX3
      R2E2=RMR2*EX2
      RKOCC=RK1OO(4)*COR*COR
      RKHC3=RK1OH(4)*COR3*COR3
      RKHC2=RK1OH(4)*COR2*COR2
      CRE43=C4*R3E3
      CRE42=C4*R2E2
      CRE53=C5*R3E3
      CRE52=C5*R2E2
      DIVR3=4.0D0/R3-RK1OH(4)
      DIVR2=4.0D0/R2-RK1OH(4)
      DIF3=4.0D0-R3*RK1OH(4)
      DIF2=4.0D0-R2*RK1OH(4)
      C4OHR2=CRE43*TAO
      C5OHR2=CRE53*TAO
      C4OHR3=CRE42*TAO
      C5OHR3=CRE52*TAO
      C4OO=CRE43*TAH2+CRE42*TAH3
      C5OO=CRE53*TAH2+CRE52*TAH3
      DC42R3=C4OHR2*DIVR3
      DC52R3=C5OHR2*DIVR3
      DC43R2=C4OHR3*DIVR2
      DC53R2=C5OHR3*DIVR2
      DC42R1=CRE43*RKOCC
      DC52R1=CRE53*RKOCC
      DC43R1=CRE42*RKOCC
      DC53R1=CRE52*RKOCC
      DC42R2=0.0D0
      DC52R2=0.0D0
      DC43R3=0.0D0
      DC53R3=0.0D0
      DC41R3=C4*RMR33*EX3*TAH2*DIF3+C4*RMR2*EX2*RKHC3
      DC41R2=C4*RMR23*EX2*TAH3*DIF2+C4*RMR3*EX3*RKHC2
      DC51R3=C5*RMR33*EX3*TAH2*DIF3+C5*RMR2*EX2*RKHC3
      DC51R2=C5*RMR23*EX2*TAH3*DIF2+C5*RMR3*EX3*RKHC2
      DC41R1=0.0D0
      DC51R1=0.0D0
      DRR1=RR1/R1
      DRR2=RR2/R2
      DRR3=RR3/R3
      DDISP1=-4.0D0*C4OO/R14*D4R1**3*(D4R1/R1+(1.0D0-D4R1)*(-ADAMP(4)-
     1       2.0D0*BDAMP(4)*RR1)*DRR1)
     2      -5.0D0*C5OO/R15*D5R1**4*(D5R1/R1+(1.0D0-D5R1)*(-ADAMP(5)-
     3       2.0D0*BDAMP(5)*RR1)*DRR1)
      DDISP2=-4.D0*C4OHR2/R24*D4R2**3*(D4R2/R2+(1.0D0-D4R2)*(-ADAMP(4)-
     1       2.0D0*BDAMP(4)*RR2)*DRR2)
     2      -5.0D0*C5OHR2/R25*D5R2**4*(D5R2/R2+(1.0D0-D5R2)*(-ADAMP(5)-
     3       2.0D0*BDAMP(5)*RR2)*DRR2)
      DDISP3=-4.D0*C4OHR3/R34*D4R3**3*(D4R3/R3+(1.0D0-D4R3)*(-ADAMP(4)-
     1       2.0D0*BDAMP(4)*RR3)*DRR3)
     2      -5.0D0*C5OHR3/R35*D5R3**4*(D5R3/R3+(1.0D0-D5R3)*(-ADAMP(5)-
     3       2.0D0*BDAMP(5)*RR3)*DRR3)
      DER1=DDISP1+DC42R1*T4R2+DC43R1*T4R3+DC52R1*T5R2+DC53R1*T5R3
     1     +DC41R1*T4R1+DC51R1*T5R1
      DER2=DDISP2+DC41R2*T4R1+DC43R2*T4R3+DC51R2*T5R1+DC53R2*T5R3
     1     +DC42R2*T4R2+DC52R2*T5R2
      DER3=DDISP3+DC42R3*T4R2+DC41R3*T4R1+DC52R3*T5R2+DC51R3*T5R1
     1     +DC43R3*T4R3+DC53R3*T5R3
      RETURN
      END
      FUNCTION THREBQ(Q1,Q2,Q3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE THREE-BODY ENERGY TERM IN COORDINATES Q1,Q2,Q3
C
      COMMON/HCOEFF/C(52)
      COMMON/THRBOD/POLQ,DECAY1,DECAY2,DECAY3,R1,R2,R3
      COMMON/REFGEO/R10,R20,R30
      Q12=Q1*Q1
      Q13=Q12*Q1
      Q14=Q13*Q1
      Q15=Q14*Q1
      Q16=Q15*Q1
      Q22=Q2*Q2
      Q32=Q3*Q3
      TQ1=Q22+Q32
      TQ2=Q32-3.0D0*Q22
      TQ3=Q22-Q32
      TQ12=TQ1*TQ1
      TQ13=TQ12*TQ1
      TQ22=TQ2*TQ2
      S1=R1-R10
      S2=R2-R20
      S3=R3-R30
      POLQ=C(1)*Q1+C(2)*Q12+C(3)*TQ1+C(4)*Q13+C(5)*Q1*TQ1+
     1C(6)*Q3*TQ2+C(7)*Q14+C(8)*Q12*TQ1+C(9)*TQ1**2+C(10)*Q1*Q3*TQ2+
     2C(11)*Q3+C(12)*Q1*Q3+C(13)*TQ3+C(14)*Q12*Q3+C(15)*Q1*TQ3+
     3C(16)*Q3*TQ1+C(17)*Q13*Q3+C(18)*Q12*TQ3+C(19)*Q1*Q3*TQ1+
     4C(20)*Q32*TQ2+C(21)*TQ1*TQ3+C(22)+C(23)*Q15+C(24)*Q13*TQ1+
     5C(25)*Q1*TQ12+C(26)*Q12*Q3*TQ2+C(27)*Q3*TQ1*TQ2+C(28)*Q14*Q3+
     6C(29)*Q13*TQ3+C(30)*Q12*Q3*TQ1+C(31)*Q1*Q32*TQ2+C(32)*Q1*TQ1*TQ3+
     7C(33)*Q3*TQ12+C(34)*Q3*TQ2*TQ3+C(35)*Q16+C(36)*Q14*TQ1+
     8C(37)*Q12*TQ12+C(38)*Q13*Q3*TQ2+C(39)*Q1*Q3*TQ1*TQ2+C(40)*TQ13+
     9C(41)*Q32*TQ22+C(42)*Q15*Q3+C(43)*Q14*TQ3+C(44)*Q13*Q3*TQ1+
     AC(45)*Q12*Q32*TQ2+C(46)*Q12*TQ1*TQ3+C(47)*Q1*Q3*TQ12+
     BC(48)*Q1*Q3*TQ2*TQ3+C(49)*Q32*TQ1*TQ2+C(50)*TQ12*TQ3
      DECAY1=1.0D0-TANH(C(51)*S1)
      DECAY2=1.0D0-TANH(C(52)*S2)
      DECAY3=1.0D0-TANH(C(52)*S3)
      THREBQ=POLQ*DECAY1*DECAY2*DECAY3
      RETURN
      END
      FUNCTION VOH(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EHFACE2 POTENTIAL FOR  O...H
C
      VOH=EHFOH(R)+DISOH(R)
      RETURN
      END
      FUNCTION EHFOH(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EHF ENERGY CONTRIBUTION FOR  O...H
C
      DIMENSION ASV(4)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DATA D,ASV/0.13825385D0,2.6564788D0,1.7450528D0,0.71014391D0,
     1           2.5453276D0/
      X=R-RMOH
      R2=X*X
      R3=R2*X
      EHFOH=-D*(1.0D0+ASV(1)*X+ASV(2)*R2+ASV(3)*R3)*EXP(-ASV(4)*X)
      RETURN
      END
      FUNCTION DISOH(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE DISPERSION ENERGY FOR  O...H
C
      COMMON/DISPC/COO(10),COH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DISOH=DISP(R,COH(6),COH(8),COH(10),R0OH,RMOH)
      RETURN
      END
      FUNCTION DVOH(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE 1ST DERIVATIVE OF EHFACE2 CURVE FOR  O...H
C
      DIMENSION ASV(4)
      COMMON/DISPC/COO(10),COH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DATA D,ASV/0.13825385,2.6564788,1.7450528,0.71014391,2.5453276/
      X=R-RMOH
      R2=X*X
      R3=R2*X
      POL=-D*(1.0D0+ASV(1)*X+ASV(2)*R2+ASV(3)*R3)
      DPOL=-D*(ASV(1)+2.0D0*ASV(2)*X+3.0D0*ASV(3)*R2)
      POT=EXP(-ASV(4)*X)
      DVOH=-ASV(4)*POT*POL+DPOL*POT+DDISP(R,COH(6),COH(8),COH(10),
     1   R0OH,RMOH)
      RETURN
      END
      FUNCTION VOO(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EHFACE2 POTENTIAL FOR  O...O
C
      VOO=EHFOO(R)+DISOO(R)
      RETURN
      END
      FUNCTION EHFOO(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EHF ENERGY CONTRIBUTION FOR  O...O
C
      DIMENSION ASV(4)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DATA D,ASV/0.14291202D0,3.6445906D0,3.9281238D0,2.0986689D0,
     1           3.3522498D0/
      X=R-RMOO
      R2=X*X
      R3=R2*X
      EHFOO=-D*(1.0D0+ASV(1)*X+ASV(2)*R2+ASV(3)*R3)*EXP(-ASV(4)*X)
      RETURN
      END
      FUNCTION DISOO(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE DISPERSION ENERGY FOR  O...O
C
      COMMON/DISPC/COO(10),COH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DISOO=DISP(R,COO(6),COO(8),COO(10),R0OO,RMOO)
      RETURN
      END
      FUNCTION DVOO(R)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE 1ST DERIVATIVE OF EHFACE2 CURVE FOR  O...O
C
      DIMENSION ASV(4)
      COMMON/DISPC/COO(10),COH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DATA D,ASV/0.14291202D0,3.6445906D0,3.9281238D0,2.0986689D0,
     1           3.3522498D0/
      X=R-RMOO
      R2=X*X
      R3=R2*X
      POL=-D*(1.0D0+ASV(1)*X+ASV(2)*R2+ASV(3)*R3)
      DPOL=-D*(ASV(1)+2.0D0*ASV(2)*X+3.0D0*ASV(3)*R2)
      POT=EXP(-ASV(4)*X)
      DVOO=-ASV(4)*POT*POL+DPOL*POT+DDISP(R,COO(6),COO(8),COO(10),
     1   R0OO,RMOO)
      RETURN
      END
      FUNCTION CEF(CAS,RK01,RK11,RK02,RK12,RE1,RE2,R1,R2)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EFECTIVE Cn
C
      CEF=0.5D0*CAS*((1.0D0-RK01*EXP(-RK11*(R1-RE1)))*TANH(RK12*R2)+
     1  (1.0D0-RK02*EXP(-RK12*(R2-RE2)))*TANH(RK11*R1))
      RETURN
      END
      FUNCTION EXDIS (R1,R2,R3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE EXCHANGE - DISPERSION TERM
C
      COMMON/DISPC/COO(10),COH(10)
      COMMON/RKVAL/RK0OO(10),RK1OO(10),RK0OH(10),RK1OH(10)
      COMMON/DISCO/CEFOO(10),CEFOH2(10),CEFOH3(10),CEDOO(10),CEDOH2(10)
      COMMON/DISCO2/CEDOH3(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      DO IN=6,10,2
         CEFOO(IN)=CEF(COO(IN),RK0OH(IN),RK1OH(IN),RK0OH(IN),RK1OH(IN),
     1      RMOH,RMOH,R2,R3)
         CEDOO(IN)=CEFOO(IN)-COO(IN)
         CEFOH2(IN)=CEF(COH(IN),RK0OO(IN),RK1OO(IN),RK0OH(IN),RK1OH(IN),
     1      RMOO,RMOH,R1,R3)
         CEDOH2(IN)=CEFOH2(IN)-COH(IN)
         CEFOH3(IN)=CEF(COH(IN),RK0OO(IN),RK1OO(IN),RK0OH(IN),RK1OH(IN),
     1      RMOO,RMOH,R1,R2)
         CEDOH3(IN)=CEFOH3(IN)-COH(IN)
      ENDDO
      EXDIS=DISP(R1,CEDOO(6),CEDOO(8),CEDOO(10),R0OO,RMOO)
     1     +DISP(R2,CEDOH2(6),CEDOH2(8),CEDOH2(10),R0OH,RMOH)
     2     +DISP(R3,CEDOH3(6),CEDOH3(8),CEDOH3(10),R0OH,RMOH)
      RETURN
      END
      FUNCTION ELECT(R1,R2,R3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         CALCULATE THE ELECTROSTATIC TERM
C
      COMMON/POLAR/C4,C5
      COMMON/RKVAL/RK0OO(10),RK1OO(10),RK0OH(10),RK1OH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      COMMON/WELECT/C4OHR2,C5OHR2,C4OHR3,C5OHR3,C4OO,C5OO,TERM4,TERM5
      C42=C4
      C43=C4
      C52=C5
      C53=C5
      R23=R2**3
      R24=R23*R2
      R33=R3**3
      R34=R33*R3
      R14=R1**4
      R15=R14*R1
      R25=R24*R2
      R35=R34*R3
      RMQ=RMOH**4
      RMQ5=0.50D0/RMQ
      RMR3=RMQ5*R34
      RMR2=RMQ5*R24
      RMR33=RMQ5*R33
      RMR23=RMQ5*R23
      TAO=TANH(RK1OO(4)*R1)
      TAH2=TANH(RK1OH(4)*R2)
      TAH3=TANH(RK1OH(4)*R3)
      EX3=EXP(-RK1OH(4)*(R3-RMOH))
      EX2=EXP(-RK1OH(4)*(R2-RMOH))
      R3E3=RMR3*EX3
      R2E2=RMR2*EX2
      CRE43=C4*R3E3
      CRE42=C4*R2E2
      CRE53=C5*R3E3
      CRE52=C5*R2E2
      C4OHR2=CRE43*TAO
      C5OHR2=CRE53*TAO
      C4OHR3=CRE42*TAO
      C5OHR3=CRE52*TAO
      C4OO=CRE43*TAH2+CRE42*TAH3
      C5OO=CRE53*TAH2+CRE52*TAH3
      RROH2=2.0D0*R2/(RMOH+2.5D0*R0OH)
      RROH3=2.0D0*R3/(RMOH+2.5D0*R0OH)
      RROO=2.0D0*R1/(RMOO+2.5D0*R0OO)
      TERM4=C4OO/R14*(1.0D0-EXP(-ADAMP(4)*RROO-BDAMP(4)*RROO**2))**4+
     1 C4OHR2/R24*(1.0D0-EXP(-ADAMP(4)*RROH2-BDAMP(4)*RROH2**2))**4+
     2 C4OHR3/R34*(1.0D0-EXP(-ADAMP(4)*RROH3-BDAMP(4)*RROH3**2))**4
      TERM5=C5OO/R15*(1.0D0-EXP(-ADAMP(5)*RROO-BDAMP(5)*RROO**2))**5+
     1 C5OHR2/R25*(1.0D0-EXP(-ADAMP(5)*RROH2-BDAMP(5)*RROH2**2))**5+
     2 C5OHR3/R35*(1.0D0-EXP(-ADAMP(5)*RROH3-BDAMP(5)*RROH3**2))**5
      ELECT=TERM4+TERM5
      RETURN
      END
      FUNCTION DISP(R,C6,C8,C10,R0,RM)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      R6=R**6
      R8=R6*R*R
      R10=R8*R*R
      RR=2.0D0*R/(RM+2.5D0*R0)
      D6=(1.0D0-EXP(-ADAMP(6)*RR-BDAMP(6)*RR*RR))**6
      D8=(1.0D0-EXP(-ADAMP(8)*RR-BDAMP(8)*RR*RR))**8
      D10=(1.0D0-EXP(-ADAMP(10)*RR-BDAMP(10)*RR*RR))**10
      DISP=-C6/R6*D6-C8/R8*D8-C10/R10*D10
      RETURN
      END
      FUNCTION DDISP(R,C6,C8,C10,R0,RM)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      R6=R**6
      R8=R6*R*R
      R10=R8*R*R
      RR=2.0D0*R/(RM+2.5D0*R0)
      DRR=RR/R
      T6=1.0D0-EXP(-ADAMP(6)*RR-BDAMP(6)*RR*RR)
      T8=1.0D0-EXP(-ADAMP(8)*RR-BDAMP(8)*RR*RR)
      T10=1.0D0-EXP(-ADAMP(10)*RR-BDAMP(10)*RR*RR)
      DDISP=6.D0*C6/R6*T6**5*(T6/R+(1.0D0-T6)*(-ADAMP(6)-2.0D0*BDAMP(6)
     1      *RR)*DRR)+
     2      8.D0*C8/R8*T8**7*(T8/R+(1.0D0-T8)*(-ADAMP(8)-2.0D0*BDAMP(8)
     3      *RR)*DRR)+
     4      10.0D0*C10/R10*T10**9*(T10/R+(1.0D0-T10)*(-ADAMP(10)-2.0D0*
     5     BDAMP(10)*RR)*DRR)
      RETURN
      END
      BLOCK DATA HO2DAT
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C         DATA FOR HO2 DMBE IV POTENTIAL ENERGY SURFACE
C
      COMMON/HCOEFF/C(52)
      COMMON/DISPC/COO(10),COH(10)
      COMMON/DIATDI/R0OO,RMOO,R0OH,RMOH
      COMMON/RKVAL/RK0OO(10),RK1OO(10),RK0OH(10),RK1OH(10)
      COMMON/POLAR/C4,C5
      COMMON/DAMPC/ADAMP(10),BDAMP(10)
      COMMON/REFGEO/R10,R20,R30
      DATA C/
     1  .49040645D+01, -.86748216D+01,  .50555792D+01,  .42941301D+01,
     1 -.41874792D+01,  .13461379D+00, -.99064922D+00,  .13358488D+01,
     1  .13495231D+01, -.18529696D+00, -.23534213D+02,  .24289930D+02,
     1 -.50209026D+01, -.10365484D+02,  .46692224D+01, -.14747138D+01,
     1  .23119718D+01, -.18247842D+01, -.28472166D+00,  .51036509D+00,
     1  .19124083D+00,  .45405729D+01,  .11087611D+00, -.19990481D+00,
     1 -.37356178D+00,  .46142042D-01, -.20565580D+00, -.27015963D+00,
     1  .34085281D+00,  .28321162D+00, -.11558481D+00, -.29448886D+00,
     1 -.52932488D+00,  .58159523D-01, -.48649560D-02,  .11949167D-01,
     1  .21409804D-01, -.20620608D-02,  .30177088D-01,  .27880291D-01,
     1  .88458711D-02,  .13137410D-01, -.24705619D-01, -.31085889D-01,
     1  .34317857D-02,  .52593878D-01,  .79500714D-01, -.79782216D-02,
     2  .31164575D-01, -.28737598D-01,  .98201698D+00,  .62000000D+00/
      DATA R0OO,RMOO,R0OH,RMOH/5.661693D0,2.2818D0,6.294894D0,1.8344D0/
      DATA COO/0.0D0,0.0D0,0.0D0,0.0D0,0.D0,15.40D0,0.0D0,235.219943D0,
     1         0.0D0,4066.23929D0/
      DATA COH/0.0D0,0.0D0,0.0D0,0.0D0,0.D0,10.00D0,0.0D0,180.447673D0,
     1         0.0D0,3685.25842D0/
      DATA C4,C5/-0.92921D0,-1.79000D0/
      DATA RK0OO/0.0D0,0.0D0,0.0D0,0.0D0,0.0D0,-.27847758D0,0.0D0,
     1           -.46815641D0,0.0D0,-1.20506384D0/
      DATA RK1OO/0.0D0,0.0D0,0.0D0,3.35224980D0,3.35224980D0,
     1           0.95273753D0,0.0D0,0.94148408D0,0.0D0,0.72379129D0/
      DATA RK0OH/0.0D0,0.0D0,0.0D0,0.0D0,0.0D0,0.02465005D0,0.0D0,
     1           0.05036950D0,0.0D0,0.06294371D0/
      DATA RK1OH/0.0D0,0.0D0,0.0D0,2.54532760D0,2.54532760D0,
     1           0.68758097D0,0.0D0,0.82542359D0,0.0D0,0.94034225D0/
      DATA ADAMP/0.0D0,0.0D0,0.0D0,5.0079875D0,3.8428294D0,3.0951333D0,
     1           0.0D0,2.1999000D0,0.0D0,1.6880714D0/
      DATA BDAMP/0.0D0,0.0D0,0.D0,10.6645006D0,9.6758155D0,8.7787895D0,
     1           0.0D0,7.2265123D0,0.0D0,5.9487108D0/
      DATA R10,R20,R30/2.5143000D0,2.6469057D0,2.6469057D0/
      END
