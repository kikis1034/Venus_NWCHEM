      SUBROUTINE HEXP(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE GENERAL EXPONENTIAL REPULSION OR ATTRACTION
C         POTENTIAL ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/EXPB/AEX(ND07),BEX(ND07),CEX(ND07),N7J(ND07),N7K(ND07),
     *RNPOW(ND07)
      COMMON/FORCES/N,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
C
C         CALCULATE INDICES FOR COORDINATES
C
      J3=3*N7J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N7K(NL)
      K2=K3-1
      K1=K2-1
C
C         CALCULATE INDEX FOR R
C
      JK=(N7J(NL)-1)*(2*N-N7J(NL))/2+N7K(NL)-N7J(NL)
C
C         CALCULATE RELATIVE COORDINATES AND R
C
      T1=Q(K1)-Q(J1)
      T2=Q(K2)-Q(J2)
      T3=Q(K3)-Q(J3)
      R(JK)=SQRT(T1*T1+T2*T2+T3*T3)
C
C         CALCULATE (DV/DQ)'S.  IF RNPOW(NL)=0 PROGRAM BYPASSES
C         ELECTROSTATIC POTENTIAL.  IF AEX(NL).LT.0 PROGRAM
C         BYPASSES REPLUSION
C
      DUM1=0.D0
      IF (AEX(NL).LT.0) GOTO 1
      DUM1=-AEX(NL)*BEX(NL)*EXP(-BEX(NL)*R(JK))/R(JK)
      IF (RNPOW(NL).EQ.0) GOTO 2
    1 DUM1=DUM1-CEX(NL)*RNPOW(NL)/R(JK)**(RNPOW(NL)+2)
    2 DUM2=T1*DUM1
      PDOT(K1)=PDOT(K1)+DUM2
      PDOT(J1)=PDOT(J1)-DUM2
      DUM2=T2*DUM1
      PDOT(K2)=PDOT(K2)+DUM2
      PDOT(J2)=PDOT(J2)-DUM2
      DUM2=T3*DUM1
      PDOT(K3)=PDOT(K3)+DUM2
      PDOT(J3)=PDOT(J3)-DUM2
      RETURN
      END
