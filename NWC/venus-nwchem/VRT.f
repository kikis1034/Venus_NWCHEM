      SUBROUTINE VRT(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C         CALCULATE NON DIAGONAL STRETCH-BEND POTENTIAL
C         ENERGY DERIVATIVES
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/VRTB/FKRTZ(ND11),FKRT(ND11),CRT(ND11),R110(ND11),
     *            N11I(ND11),N11J(ND11),N11B(ND11),NRT(ND11)
      COMMON/BENDB/THETAZ(ND03),FBZ(ND03),CJ(ND03),CK(ND03),RJZ(ND03),
     *RKZ(ND03),FB(ND03),N3J(ND03),N3K(ND03),N3M(ND03)
      COMMON/CONSTN/C1,C2,C3,C4,C5,C6,C7,PI,HALFPI,TWOPI
C
C         ATOM INDECIES
C
      MI=N11I(NL)
      MJ=N11J(NL)
      MK=N3J(N11B(NL))
      ML=N3K(N11B(NL))
      MM=N3M(N11B(NL))
C
C         DISTANCE INDEX
C
      IJ=(MI-1)*(2*NATOMS-MI)/2+MJ-MI
      MKX=MK
      MMX=MM
      IF (MK.GE.MM) THEN
         MKX=MM
         MMX=MK
      ENDIF
      KM=(MKX-1)*(2*NATOMS-MKX)/2+MMX-MKX
      MMX=MM
      MLX=ML
      IF (ML.GE.MM) THEN
         MLX=MM
         MMX=ML
      ENDIF
      LM=(MLX-1)*(2*NATOMS-MLX)/2+MMX-MLX
C
C         X,Y,Z INDECIES
C
      IZ=3*MI
      JZ=3*MJ
      KZ=3*MK
      LZ=3*ML
      MZ=3*MM
      IY=IZ-1
      JY=JZ-1
      KY=KZ-1
      LY=LZ-1
      MY=MZ-1
      IX=IY-1
      JX=JY-1
      KX=KY-1
      LX=LY-1
      MX=MY-1
C
C         RE-COMPUTE RELATIVE VECTORS
C
      XIJ=Q(IX)-Q(JX)
      YIJ=Q(IY)-Q(JY)
      ZIJ=Q(IZ)-Q(JZ)
      XKM=Q(KX)-Q(MX)
      YKM=Q(KY)-Q(MY)
      ZKM=Q(KZ)-Q(MZ)
      XLM=Q(LX)-Q(MX)
      YLM=Q(LY)-Q(MY)
      ZLM=Q(LZ)-Q(MZ)
C
C         REDEFINE SWITCHING PARAMETER AND
C         COMPUTE SOME QUANTITIES
C
      CKM=CJ(N11B(NL))
      CLM=CK(N11B(NL))
      CIJ=CRT(NL)
      IF(NRT(NL).EQ.-1) THEN
        CIJ=-ABS(CIJ)
        CKM=-ABS(CKM)
        CLM=-ABS(CLM)
      ELSE
	IF(IJ.EQ.KM) CKM=-ABS(CKM)
	IF(IJ.EQ.LM) CLM=-ABS(CLM)
      ENDIF
      R1=R(IJ)-R110(NL)
      R2=R(KM)-RJZ(N11B(NL))
      R3=R(LM)-RKZ(N11B(NL))
C
C         CALCULATE SWITCHING FUNCTION AND FORCE CONSTANTS
C
      SRIJ=0.0D0
      DUM1=CIJ*R1**2
      IF(DUM1.LE.85.0D0)THEN
         SRIJ=1.0D0
         IF(CIJ.GT.0.0D0.AND.R1.GT.0.0D0) SRIJ=EXP(-DUM1)
      ENDIF
      SRKM=0.0D0
      DUM2=CKM*R2**2
      IF(DUM2.LE.85.0D0)THEN
         SRKM=1.0D0
         IF(CKM.GT.0.0D0.AND.R2.GT.0.0D0) SRKM=EXP(-DUM2)
      ENDIF
      SRLM=0.0D0
      DUM3=CLM*R3**2
      IF(DUM3.LE.85.0D0)THEN
         SRLM=1.0D0
         IF(CLM.GT.0.0D0.AND.R3.GT.0.0D0) SRLM=EXP(-DUM3)
      ENDIF
      FKRT(NL)=FKRTZ(NL)*SRIJ*SRKM*SRLM
C
C         DEFINE CONSTANTS
C
      COST=COS(THETA(N11B(NL)))
      RS=R(KM)*R(LM)
      RSKM=R(KM)*R(KM)
      RSLM=R(LM)*R(LM)
      RTKM=-COST/RSKM
      RTLM=-COST/RSLM
      TEM0=FKRT(NL)*(THETA(N11B(NL))-THETAZ(N11B(NL)))
      TEM1=TEM0/R(IJ)
      TEM11=-2.0D0*TEM1*R1*R1*CIJ
      TEM21=-2.0D0*CKM*TEM0*R1*R2/R(KM)
      TEM22=-2.0D0*CLM*TEM0*R1*R3/R(LM)
C
C         CALCULATE DV/DQ
C
      DUMX1=TEM1*XIJ
      DUMY1=TEM1*YIJ
      DUMZ1=TEM1*ZIJ
      PDOT(IX)=PDOT(IX)+DUMX1
      PDOT(IY)=PDOT(IY)+DUMY1
      PDOT(IZ)=PDOT(IZ)+DUMZ1
      PDOT(JX)=PDOT(JX)-DUMX1
      PDOT(JY)=PDOT(JY)-DUMY1
      PDOT(JZ)=PDOT(JZ)-DUMZ1
      IF(CIJ.ge.0.0D0.and.R1.ge.0.0D0)then
         DUMX11=TEM11*XIJ
         DUMY11=TEM11*YIJ
         DUMZ11=TEM11*ZIJ
         PDOT(IX)=PDOT(IX)+DUMX11
         PDOT(IY)=PDOT(IY)+DUMY11
         PDOT(IZ)=PDOT(IZ)+DUMZ11
         PDOT(JX)=PDOT(JX)-DUMX11
         PDOT(JY)=PDOT(JY)-DUMY11
         PDOT(JZ)=PDOT(JZ)-DUMZ11
      endif
      IF(CKM.ge.0.0D0.and.R2.ge.0.0D0) then
         DUMX21=TEM21*XKM
         DUMY21=TEM21*YKM
         DUMZ21=TEM21*ZKM
         PDOT(KX)=PDOT(KX)+DUMX21
         PDOT(KY)=PDOT(KY)+DUMY21
         PDOT(KZ)=PDOT(KZ)+DUMZ21
         PDOT(MX)=PDOT(MX)-DUMX21
         PDOT(MY)=PDOT(MY)-DUMY21
         PDOT(MZ)=PDOT(MZ)-DUMZ21
      endif
      IF(CLM.ge.0.0D0.and.R3.ge.0.0D0) then
         DUMX22=TEM22*XLM
         DUMY22=TEM22*YLM
         DUMZ22=TEM22*ZLM
         PDOT(LX)=PDOT(LX)+DUMX22
         PDOT(LY)=PDOT(LY)+DUMY22
         PDOT(LZ)=PDOT(LZ)+DUMZ22
         PDOT(MX)=PDOT(MX)-DUMX22
         PDOT(MY)=PDOT(MY)-DUMY22
         PDOT(MZ)=PDOT(MZ)-DUMZ22
      endif
C
      IF (ABS(THETA(N11B(NL))-PI).GT.0.1D0) THEN
	  TEM2=-FKRT(NL)*R1/SQRT(1.0D0-COST*COST)
	  DUMX2=TEM2*(XLM/RS+RTKM*XKM)
	  DUMY2=TEM2*(YLM/RS+RTKM*YKM)
	  DUMZ2=TEM2*(ZLM/RS+RTKM*ZKM)
	  PDOT(KX)=PDOT(KX)+DUMX2
	  PDOT(KY)=PDOT(KY)+DUMY2
	  PDOT(KZ)=PDOT(KZ)+DUMZ2
	  PDOT(MX)=PDOT(MX)-DUMX2
	  PDOT(MY)=PDOT(MY)-DUMY2
	  PDOT(MZ)=PDOT(MZ)-DUMZ2
	  DUMX3=TEM2*(XKM/RS+RTLM*XLM)
	  DUMY3=TEM2*(YKM/RS+RTLM*YLM)
	  DUMZ3=TEM2*(ZKM/RS+RTLM*ZLM)
	  PDOT(LX)=PDOT(LX)+DUMX3
	  PDOT(LY)=PDOT(LY)+DUMY3
	  PDOT(LZ)=PDOT(LZ)+DUMZ3
	  PDOT(MX)=PDOT(MX)-DUMX3
	  PDOT(MY)=PDOT(MY)-DUMY3
	  PDOT(MZ)=PDOT(MZ)-DUMZ3
      ELSE
	  DNRMW=SQRT(XKM*XKM+YKM*YKM+ZKM*ZKM)
	  DWX=-XKM/DNRMW
	  DWY=-YKM/DNRMW
	  DWZ=-ZKM/DNRMW
	  DVX0=(ZKM*YLM-YKM*ZLM)/R(KM)
	  DVY0=(XKM*ZLM-ZKM*XLM)/R(KM)
	  DVZ0=(YKM*XLM-XKM*YLM)/R(KM)
	  DNRMV=SQRT(DVX0*DVX0+DVY0*DVY0+DVZ0*DVZ0)
	  DVX=DVX0/DNRMV
	  DVY=DVY0/DNRMV
	  DVZ=DVZ0/DNRMV
	  DUX0=DVY*DWZ-DVZ*DWY
	  DUY0=DVZ*DWX-DVX*DWZ
	  DUZ0=DVX*DWY-DVY*DWX
	  DNRMU=SQRT(DUX0*DUX0+DUY0*DUY0+DUZ0*DUZ0)
	  DUX=DUX0/DNRMU
	  DUY=DUY0/DNRMU
	  DUZ=DUZ0/DNRMU
C
C         CALCULATE NON-ZERO DV/DQ IN
C         LOCAL COORDINATE SYSTEM (U,V,W)
C
	  DUM4=THETA(N11B(NL))-PI
	  DUMUK=-1.0D0/R(KM)
	  DUMUL=-(1.0D0-0.5D0*DUM4*DUM4+DUM4**4/2.4D1-DUM4**6/7.2D2)
	  DUMUL=DUMUL/R(LM)
	  DUMUM=-(DUMUK+DUMUL)
	  DUMWK=0.0D0
	  DUMWL=-(1.0D0-DUM4*DUM4/6.0D0+DUM4**4/1.2D2)*DUM4/R(LM)
	  DUMWM=-(DUMWK+DUMWL)
C
	  TEM3=FKRT(NL)*R1
	  PDOT(KX)=PDOT(KX)+(DUX*DUMUK+DWX*DUMWK)*TEM3
	  PDOT(KY)=PDOT(KY)+(DUY*DUMUK+DWY*DUMWK)*TEM3
	  PDOT(KZ)=PDOT(KZ)+(DUZ*DUMUK+DWZ*DUMWK)*TEM3
	  PDOT(LX)=PDOT(LX)+(DUX*DUMUL+DWX*DUMWL)*TEM3
	  PDOT(LY)=PDOT(LY)+(DUY*DUMUL+DWY*DUMWL)*TEM3
	  PDOT(LZ)=PDOT(LZ)+(DUZ*DUMUL+DWZ*DUMWL)*TEM3
	  PDOT(MX)=PDOT(MX)+(DUX*DUMUM+DWX*DUMWM)*TEM3
	  PDOT(MY)=PDOT(MY)+(DUY*DUMUM+DWY*DUMWM)*TEM3
	  PDOT(MZ)=PDOT(MZ)+(DUZ*DUMUM+DWZ*DUMWM)*TEM3
      ENDIF
C
      RETURN
      END
