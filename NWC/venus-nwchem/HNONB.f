      SUBROUTINE HNONB(NL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
C
C        CALCULATE POTENTIAL ENERGY AND DERIVATIVES OF H---H NON-BONDED
C        INTERACTIONS. FROM J. CHEM. PHYS. 101, 2476(1994).
C
      COMMON/QPDOT/Q(NDA3),PDOT(NDA3)
      COMMON/COORS/R(NDA*(NDA+1)/2),THETA(ND03),ALPHA(ND04),CTAU(ND06),
     *GR(ND08,5),TT(ND09,6),DANG(ND13I)
      COMMON/FORCES/NATOMS,I3N,NST,NM,NB,NA,NLJ,NTAU,NEXP,NGHOST,NTET,
     *NVRR,NVRT,NVTT,NANG,NAXT,NSN2,NRYD,NHFD,NLEPSA,NLEPSB,NDMBE,
     *NRAX,NONB,NMO,NCRCO6
      COMMON/NNB/N22J(ND22),N22K(ND22),ASR(ND22),BSR(ND22),CSR(ND22),
     *NDSR(ND22),ALR(ND22),BLR(ND22),CLR(ND22),NDLR(ND22),AAS(ND22),
     *AA0(ND22),NAA(ND22)
C
C           CALCULATE INDICES FOR COORDINATES
C
      J3=3*N22J(NL)
      J2=J3-1
      J1=J2-1
      K3=3*N22K(NL)
      K2=K3-1
      K1=K2-1
C
C           CALCULATE INDEX FOR R
C
      N=NATOMS
      JK=(N22J(NL)-1)*(2*N-N22J(NL))/2+N22K(NL)-N22J(NL)
C
C           CALCULATE RELATIVE COORDINATES AND R
C
      T1=Q(K1)-Q(J1)
      T2=Q(K2)-Q(J2)
      T3=Q(K3)-Q(J3)
      R(JK)=SQRT(T1*T1+T2*T2+T3*T3)
C
C           CALCULATE (DV/DQ)'S. 
C           NO CONSIDERATIONS ON THE A SIGN  
C           NO CONSIDERATIONS ON N. (I.E CHOOSE N>0)
C
      DUM03=-ASR(NL)*BSR(NL)*EXP(-BSR(NL)*R(JK))
      DUM03=DUM03-NDSR(NL)*CSR(NL)/(R(JK)**(NDSR(NL)+1))
      IF(R(JK).GE.AA0(NL)) THEN
        DUM00=ASR(NL)*EXP(-BSR(NL)*R(JK))+CSR(NL)/(R(JK)**NDSR(NL))
        DUM01=ALR(NL)*EXP(-BLR(NL)*R(JK))+CLR(NL)/(R(JK)**NDLR(NL))
        TDT=TANH(AAS(NL)*((R(JK)-AA0(NL))**NAA(NL)))
        DUM02=1.0D0-TDT
        DUM04=-ALR(NL)*BLR(NL)*EXP(-BLR(NL)*R(JK))
        DUM04=DUM04-NDLR(NL)*CLR(NL)/(R(JK)**(NDLR(NL)+1))
        DUM05=AAS(NL)*NAA(NL)*((R(JK)-AA0(NL))**(NAA(NL)-1))*(TDT**2
     *        -1.0D0)
        DUM1=(DUM04+(DUM03-DUM04)*DUM02+DUM05*(DUM00-DUM01))/R(JK)
      ELSE
        DUM1=DUM03/R(JK)
      ENDIF
      DUM2=T1*DUM1
      PDOT(K1)=PDOT(K1)+DUM2
      PDOT(J1)=PDOT(J1)-DUM2
      DUM2=T2*DUM1
      PDOT(K2)=PDOT(K2)+DUM2
      PDOT(J2)=PDOT(J2)-DUM2
      DUM2=T3*DUM1
      PDOT(K3)=PDOT(K3)+DUM2
      PDOT(J3)=PDOT(J3)-DUM2
      RETURN
      END
